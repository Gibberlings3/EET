BACKUP ~EET/backup~
AUTHOR ~K4thos (swit)~
VERSION ~beta 0.9~
//README ~EET/docs/[%LANGUAGE%]readme.htm~ ~EET/docs/[en_US]readme.htm~

LANGUAGE ~English~
	~en_US~
	~EET/lang/en_US/prompts.tra~
	~EET/lang/en_US/setup.tra~
	~EET/lang/en_US/dialog.tra~
	~EET/lang/en_US/scripts.tra~
	~EET/lang/en_US/2da.tra~
LANGUAGE ~Polski (Polish)~
	~pl_PL~
	~EET/lang/pl_PL/prompts.tra~
	~EET/lang/pl_PL/setup.tra~
	~EET/lang/pl_PL/dialog.tra~
	~EET/lang/pl_PL/scripts.tra~
	~EET/lang/pl_PL/2da.tra~
LANGUAGE ~Deutsch (German)~
	~de_DE~
	~EET/lang/de_DE/prompts.tra~
	~EET/lang/de_DE/setup.tra~
	~EET/lang/de_DE/dialog.tra~
	~EET/lang/de_DE/scripts.tra~
	~EET/lang/de_DE/2da.tra~
	
BEGIN ~Baldur's Gate: Enhanced Edition Trilogy (EET)~
REQUIRE_PREDICATE ENGINE_IS ~bg2ee~ ~Please install it on BG2:EE engine.~
FORBID_COMPONENT ~setup-bp-bgt-worldmap.tp2~ ~0~ ~EET should be installed BEFORE BP-BGT Worldmap~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Initialise installation                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/*OUTER_SET files_count = 0
ACTION_BASH_FOR ~override~ ~^.+$~ BEGIN
	COPY + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_FILESPEC%~
	OUTER_SET files_count = files_count + 1
END
PRINT ~Total number of files in override directory = %files_count%~
ACTION_IF ("%files_count%" > 3) BEGIN
	FAIL ~Unexpected files detected within override directory. EET should be installed on clean BG2:EE.~
END*/

//create dummy file with UTF-8 without BOM encoding - used many times during installation
<<<<<<<< EET-inlined/blank.txt
>>>>>>>>

COPY - ~EET-inlined/blank.txt~ ~EET-inlined/blank.baf~

//Nothing done within EET/temp folder is registered for uninstallation (saves hundreds of MB disk space).
//The whole folder is removed after the installation is complete (saves 1GB+ disk space) unless the debug mode argument is parsed.
MKDIR ~EET/temp~
COPY + ~EET-inlined/blank.txt~ ~EET/temp/uninstall.flag~

OUTER_SPRINT ~newline~ ~%WNL%%LNL%%MNL%%TAB% ~
OUTER_SPRINT slash ~/~
OUTER_SPRINT quote ~"~
OUTER_SPRINT tilde "~"
OUTER_SPRINT bg2ee_dir ~.~
OUTER_SPRINT tra_path ~EET/lang~
OUTER_SPRINT patch_dir ~EET/temp/patch~
OUTER_SPRINT biff_dir ~EET/temp/biff~
OUTER_SPRINT arch_var ~~

OUTER_SET auto_install = 0
OUTER_SET valid_dir = 0
OUTER_SET first_prompt = 0
OUTER_SET allow_mod = 0

ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ BEGIN
	OUTER_SPRINT os_slash ~\~
	OUTER_SPRINT exe ~.exe~
	OUTER_SPRINT WEIDU_EXECUTABLE ~setup-EET.exe~
	OUTER_SPRINT bash_debug ~ 2>&1|EET\bin\win32\mtee.exe EET\temp\bash.debug /A~
	ACTION_IF (FILE_EXISTS ~EET/temp/uninstall.flag~) BEGIN
		AT_INTERACTIVE_UNINSTALL ~rmdir /s /q EET\temp~ EXACT
	END
	ACTION_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[dD]~ = 1) AND (FILE_EXISTS ~EET/temp/uninstall.flag~) BEGIN
		AT_INTERACTIVE_EXIT ~rmdir /s /q EET\temp~ EXACT
	END
<<<<<<<< .../arch_var.bat
::32/64Bit Switch
ECHO %PROCESSOR_ARCHITEW6432%|FINDSTR AMD64>NUL && SET arch_var=AMD64 || SET arch_var=x86
ECHO %arch_var%>EET\temp\arch_var.txt
>>>>>>>>
	COPY ~.../arch_var.bat~ ~EET/temp/arch_var.bat~
	AT_NOW ~CALL EET\temp\arch_var.bat~
	COPY - ~EET/temp/arch_var.txt~ ~EET/temp~
		REPLACE_EVALUATE CASE_INSENSITIVE ~AMD64~ BEGIN
			SPRINT arch_var ~x86_64\~
			PATCH_PRINT ~arch_var = %arch_var%~
		END ~~
END ELSE ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ BEGIN
	OUTER_SPRINT os_slash ~/~
	OUTER_SPRINT exe ~~
	OUTER_SPRINT WEIDU_EXECUTABLE ~./EET/weidu~
	OUTER_SPRINT bash_debug ~ 2>>EET/temp/bash.debug | tee -a EET/temp/bash.debug~
	ACTION_IF (FILE_EXISTS ~EET/temp/uninstall.flag~) BEGIN
		AT_INTERACTIVE_UNINSTALL ~rm -rf EET/temp~
	END
	ACTION_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[dD]~ = 1) AND (FILE_EXISTS ~EET/temp/uninstall.flag~) BEGIN
		AT_INTERACTIVE_EXIT ~rm -rf EET/temp~
	END
END

ACTION_FOR_EACH tool IN tile2ee tileconv BEGIN
	OUTER_SPRINT EVAL ~%tool%~ ~EET%os_slash%bin%os_slash%%WEIDU_OS%%os_slash%%arch_var%%tool%%exe%~
	PRINT ~%tool% path assigned to EET%os_slash%bin%os_slash%%WEIDU_OS%%os_slash%%arch_var%%tool%%exe%~
END
OUTER_SPRINT tis2bg2 ~EET%os_slash%bin%os_slash%%WEIDU_OS%%os_slash%tis2bg2%exe%~
PRINT ~tis2bg2 path assigned to EET%os_slash%bin%os_slash%%WEIDU_OS%%os_slash%tis2bg2%exe%~

/////                                                  \\\\\
///// Include                                          \\\\\
/////                                                  \\\\\

COPY - ~EET/other/cpmvars/eet_cpmvars.tpa~ ~EET-inlined/eet_cpmvars.tpa~
	REPLACE_TEXTUALLY ~\(GAME_IS "\)eet~ ~\1bg2ee~//at this point WeiDU thinks it's still BG2:EE installation
	REPLACE_TEXTUALLY ~FAIL .*~ ~~
INCLUDE ~EET-inlined/eet_cpmvars.tpa~

INCLUDE ~EET/lib/macros.tph~
INCLUDE ~EET/lib/PCU_Dict.tph~
INCLUDE ~EET/lib/strings.tph~

/////                                                  \\\\\
///// Get location of BG:EE installation               \\\\\
/////                                                  \\\\\

ACTION_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[Pp]~ = 0) BEGIN
	OUTER_SPRINT bgee_dir ~%argv[1]%~
	OUTER_SET auto_install = 1
	PRINT ~%bgee_dir% directory assigned via auto install batch command~
END ELSE ACTION_IF (FILE_EXISTS ~EET/bgee_dir.txt~) BEGIN
	COPY - ~EET/bgee_dir.txt~ ~EET~
		READ_ASCII 0x0 bgee_dir (%SOURCE_SIZE%)
	OUTER_SET first_prompt = 1
	PRINT ~%bgee_dir% directory assigned via EET/bgee_dir.txt~
END ELSE ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ BEGIN
<<<<<<<< EET-inlined/programfiles.bat
echo %%var%%>EET/temp/%var%.txt
>>>>>>>>
	ACTION_FOR_EACH var IN ~programfiles~ ~programfiles(x86)~ ~ProgramW6432~ BEGIN
		COPY + ~EET-inlined/programfiles.bat~ ~EET/temp~ EVALUATE_BUFFER
		AT_NOW ~EET/temp/programfiles.bat~
		COPY - ~EET/temp/%var%.txt~ ~EET/temp~//for some reason the same file can't be used for all 3 vars
			READ_ASCII 0x0 dir (%SOURCE_SIZE%)
		OUTER_INNER_PATCH_SAVE dir ~%dir%~ BEGIN
			REPLACE_TEXTUALLY ~%WNL%~ ~~//needed due to newline added by windows > command
		END
		PRINT ~%var% = %dir%~
		ACTION_FOR_EACH dir2 IN ~/BeamDog/Games/00766~ ~/Baldur's Gate Enhanced Edition/Data/00766~ BEGIN
			ACTION_IF (FILE_EXISTS ~%dir%%dir2%/data/v2017.bif~) AND (first_prompt = 0) BEGIN
				OUTER_SPRINT bgee_dir ~%dir%%dir2%~
				OUTER_SET first_prompt = 1
				PRINT ~%bgee_dir% directory assigned to default dir~
			END
		END
	END
END ELSE ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ BEGIN
	ACTION_FOR_EACH dir IN ~%quote%/Applications/Baldur's Gate - Enhanced Edition/Game Data/00777/Baldur's Gate - Enhanced Edition.app/Contents/Resources%quote%~ ~%quote%/Applications/Baldur's Gate - Enhanced Edition.app/Contents/Resources%quote%~ ~%quote%%tilde%/Library/Application Support/Steam/SteamApps/common/Baldur's Gate Enhanced Edition/Baldur's Gate - Enhanced Edition.app/Contents/Resources%quote%~ ~%quote%/Applications/Baldur's Gate Enhanced Edition.app/Contents/Resources/game/Baldur's Gate - Enhanced Edition.app/Contents/Resources%quote%~ BEGIN
		ACTION_IF (FILE_EXISTS ~%dir%/data/v2017.bif~) AND (first_prompt = 0) BEGIN
			OUTER_SPRINT bgee_dir ~%dir%~
			OUTER_SET first_prompt = 1
			PRINT ~%bgee_dir% directory assigned to default dir~
		END
	END
END

ACTION_IF (auto_install = 0) BEGIN
	ACTION_IF (first_prompt = 1) BEGIN
		OUTER_SPRINT bgee_prompt ~a~
		OUTER_WHILE (~%bgee_prompt%~ STRING_MATCHES_REGEXP ~[YyNn]~) BEGIN
			PRINT ~Detected BG:EE directory as %bgee_dir%.
Is this correct? [Y]es or [N]o~
			ACTION_READLN ~bgee_prompt~
		END
		ACTION_IF ((~%bgee_prompt%~ STRING_EQUAL_CASE ~y~)=0) BEGIN
			OUTER_SET first_prompt = 0
		END
	END
	ACTION_IF (first_prompt = 0) BEGIN
		PRINT ~~
		PRINT ~Enter the full path to your BG:EE installation then press Enter.
Example: C:\Program Files (x86)\BeamDog\Games\00766~
		ACTION_READLN ~bgee_dir~
	END
END

OUTER_WHILE (valid_dir = 0) BEGIN
	OUTER_INNER_PATCH_SAVE ~bgee_dir~ ~%bgee_dir%~ BEGIN
		PATCH_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ BEGIN //Win32
			REPLACE_TEXTUALLY ~[*?"<>|]~ ~~ //invalid characters in a path
			REPLACE_TEXTUALLY ~\~ ~/~ //change slashes
			REPLACE_TEXTUALLY ~[\\/]*$~ ~~ //remove terminal slashes
		END ELSE PATCH_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ BEGIN
			REPLACE_TEXTUALLY ~\\~ ~~ //remove quotes
			REPLACE_TEXTUALLY ~^"\(.*\)"$~ ~\1~ //if the whole text is quoted, remove quotes
			REPLACE_TEXTUALLY ~/*$~ ~~ //remove terminal slashes//*/
		END
	END
	ACTION_IF NOT (FILE_EXISTS ~%bgee_dir%/data/v2017.bif~) BEGIN
		PRINT ~~
		PRINT ~Invalid BG:EE directory, or incorrect BG:EE installation.
It is safe to abort this installation by closing this window or pressing Ctrl+C.~
		PRINT ~Enter the full path to your BG:EE installation then press Enter.
Example: C:\Program Files (x86)\BeamDog\Games\00766~
		ACTION_READLN ~bgee_dir~
	END ELSE BEGIN
		OUTER_SET valid_dir = 1
		PRINT ~BG:EE directory used for this installation:
%bgee_dir%~
	END
END

<<<<<<<< EET-inlined/bgee_dir.txt
%bgee_dir%>>>>>>>>
COPY ~EET-inlined/bgee_dir.txt~ ~EET/bgee_dir.txt~ EVALUATE_BUFFER

/////                                                  \\\\\
///// Detect mods installed on BG:EE                   \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~%bgee_dir%/WeiDU.log~ BEGIN
	COPY ~%bgee_dir%/WeiDU.log~ ~override~ //store the file in override, so other mods could check if components have been installed on BG:EE
		REPLACE_TEXTUALLY ~^//.*~ ~~//removes "Recently uninstalled" lines
	ACTION_IF (FILE_CONTAINS ~override/WeiDU.log~ ~SETUP-SOA\.TP2. #[0-9]+ #0 ~) BEGIN
		FAIL ~You've installed wrong Stone of Askavar component. EET is compatible with BP-BGT Worldmap version (option 2 during installation).~
	END
	ACTION_IF (FILE_CONTAINS ~override/WeiDU.log~ ~DRIZZTSAGA\.TP2. #[0-9]+ #0 ~) BEGIN
		FAIL ~You've installed wrong Drizzt Saga component. EET is compatible with BP-BGT Worldmap version (option 2 during installation).~
	END
	COPY + ~%bgee_dir%/WeiDU.log~ ~EET/temp~
		REPLACE_TEXTUALLY ~^//.*~ ~~
		REPLACE_TEXTUALLY ~^.~ ~~
		REPLACE_TEXTUALLY ~^.*/\(.*.TP2\)~ ~\1~
		REPLACE_TEXTUALLY ~\.TP2.~ ~~
		REPLACE_TEXTUALLY ~SETUP-~ ~~
		COUNT_2DA_ROWS 3 "cntrow"
		PATCH_PRINT ~%cntrow% BG:EE mods detected. Checking compatibility with EET...~
		FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
			READ_2DA_ENTRY cnt 0 1 "col1"
			INNER_ACTION BEGIN
				COPY - ~EET/tbl/compatibility.tbl~ ~EET/tbl~
					COUNT_2DA_ROWS 1 "cntrow2"
					FOR (cnt2 = 0; cnt2 < "%cntrow2%"; cnt2 = cnt2 + 1) BEGIN
						READ_2DA_ENTRY cnt2 0 1 "compareWith"
						PATCH_IF (("%col1%" STRING_EQUAL_CASE "%compareWith%")=1) BEGIN
							SET allow_mod = 1
						END
					END
					PATCH_IF ("%allow_mod%" = 1) BEGIN
						PATCH_PRINT ~%col1% BG:EE component detected~
						SET allow_mod = 0
					END ELSE PATCH_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[Mm]~ = 0) BEGIN
						PATCH_WARN ~Warning: %col1% BG:EE mod is not recognized by EET, but M/m flag is assigned, so installation will continue~
					END ELSE BEGIN
						PATCH_FAIL ~%col1% BG:EE mod is not recognized by EET, so it can't be installed on BG2:EE~
					END
			END
		END
END

/////                                                  \\\\\
///// Confirm / create weidu.conf                      \\\\\
/////                                                  \\\\\

OUTER_SPRINT ~LANGUAGEcase~ ~%LANGUAGE%~
ACTION_TO_LOWER LANGUAGEcase

<<<<<<<< EET-inlined/weidu.conf
lang_dir = %LANGUAGEcase%>>>>>>>>

ACTION_FOR_EACH dir IN "%bgee_dir%" "%bg2ee_dir%" BEGIN
	ACTION_IF NOT FILE_EXISTS ~%dir%/weidu.conf~ BEGIN
		COPY ~EET-inlined/weidu.conf~ ~%dir%/weidu.conf~ EVALUATE_BUFFER
			PATCH_PRINT ~weidu.conf created within BG:EE directory and set to %LANGUAGEcase%~
	END ELSE BEGIN
		COPY - ~%dir%/weidu.conf~ ~%dir%~
			READ_2DA_ENTRY 0 2 3 "compareWith"
			PATCH_IF (("%LANGUAGEcase%" STRING_EQUAL_CASE "%compareWith%")=1) BEGIN
				PATCH_PRINT ~%dir%/weidu.conf detected with the same language setting as the installation: %LANGUAGEcase%~
			END ELSE PATCH_IF FILE_EXISTS ~%dir%/lang/%LANGUAGE%/dialog.tlk~ BEGIN
				PATCH_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~0~ = 0) BEGIN
					SPRINT conf_prompt ~y~
				END ELSE PATCH_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~1~ = 0) BEGIN
					SPRINT conf_prompt ~n~
				END ELSE BEGIN
					SPRINT conf_prompt ~a~
					WHILE (~%conf_prompt%~ STRING_MATCHES_REGEXP ~[YyNn]~) BEGIN
						PATCH_PRINT ~%dir%/weidu.conf file has %compareWith% language set instead of %LANGUAGEcase%.
Is this correct? [Y]es (keep it as it is) or [N]o (change it to %LANGUAGEcase%)~
						PATCH_READLN ~conf_prompt~
					END
				END
				PATCH_IF ((~%conf_prompt%~ STRING_EQUAL_CASE ~n~)=1) BEGIN
					PATCH_PRINT ~%dir%/weidu.conf %compareWith% changed to %LANGUAGEcase%~
					INNER_ACTION BEGIN
						COPY ~EET-inlined/weidu.conf~ ~%dir%/weidu.conf~ EVALUATE_BUFFER
					END
				END ELSE BEGIN
					SPRINT LANGUAGEcase ~%LANGUAGE%~ EVALUATE_BUFFER
				END
			END
	END
END

COPY - ~weidu.conf~ ~weidu.conf~
	READ_2DA_ENTRY 0 2 3 "LANGUAGE_BG2"
PRINT ~LANGUAGE_BG2 = %LANGUAGE_BG2%~

/////                                                  \\\\\
///// Extraction of TLK from BG:EE                     \\\\\
/////                                                  \\\\\

PRINT ~~
PRINT ~Extracting texts for translation from BG:EE...~

ACTION_IF FILE_EXISTS ~%bgee_dir%/lang/%LANGUAGEcase%/dialog.tlk~ BEGIN
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGEcase%" --traify-tlk --out "EET/bgee.tra"~
	LAM bash_log
END ELSE BEGIN
	FAIL ~TLK for language: %LANGUAGEcase% not found in %bgee_dir%~
END

COPY + ~EET/bgee.tra~ ~EET~
	REPLACE_TEXTUALLY ~~~~~\([~"%]\)\[[^~"%]*\][ ]*~~~~~ ~~~~~\1~~~~~ //clear []s at the start of StrRefs//"
	REPLACE_TEXTUALLY ~\[BLACK\([0-9]\)\([0-9]\)\]~ ~[BLACK\1\2_]~
	REPLACE_TEXTUALLY ~\[EDWIN\([0-9]\)\([0-9]\)\]~ ~[EDWIN\1\2_]~ //STRING_SET 11618
	REPLACE_TEXTUALLY ~\[IMOEN\([0-9]\)\([0-9]\)\]~ ~[IMOEN\1\2_]~
	REPLACE_TEXTUALLY ~\[MINSC\([0-9]\)\([0-9]\)\]~ ~[MINSC\1\2_]~
	REPLACE_TEXTUALLY ~\[UMBER\([0-9]\)\([0-9]\)\]~ ~[UMBER\1\2_]~
	FOR( i=1; i<="%resref_wav_total%"; i=i+1 ) BEGIN
		SPRINT name_source "resref_wav_source%i%"
		SPRINT name_dest "resref_wav_dest%i%"
		SPRINT resref_index_source EVALUATE_BUFFER ~%%name_source%%~
		SPRINT resref_index_dest EVALUATE_BUFFER ~%%name_dest%%~
		REPLACE_TEXTUALLY ~~~~~\(\[\)%resref_index_source%\(\]\)~~~~~ ~\1%resref_index_dest%\2~
	END

/////                                                  \\\\\
///// Clear []s at the start of BG2:EE StrRefs         \\\\\
/////                                                  \\\\\

PRINT ~~
PRINT ~Clearing []s at the start of BG2:EE StrRefs...~

//not needed for English language, but other TLK files (for example Polish) may have these tags, which result in hundreds of useless TLK entries
/*ACTION_IF (~%LANGUAGE_BG2%~ STRING_EQUAL_CASE ~pl_PL~) BEGIN
	MKDIR ~lang/xx_xx~
	ACTION_FOR_EACH tlk IN dialog dialogf BEGIN
		ACTION_IF FILE_EXISTS ~lang/%LANGUAGE_BG2%/%tlk%.tlk~ BEGIN
			COPY + ~lang/%LANGUAGE_BG2%/%tlk%.tlk~ ~lang/xx_xx/dialog.tlk~
			AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --use-lang "xx_xx" --traify-tlk --out "EET/temp/%tlk%.tra"~
			COPY + ~EET/temp/%tlk%.tra~ ~EET/temp~
				REPLACE_TEXTUALLY ~~~~~\([~"%]\)\[[^~"%]*\][ ]*~~~~~ ~~~~~\1~~~~~ //"
			AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --use-lang "xx_xx" --make-tlk "EET/temp/%tlk%.tra" --tlkout "lang/%LANGUAGE_BG2%/%tlk%.tlk"~
			DELETE + ~lang/xx_xx/dialog.tlk~
		END
	END
	COPY ~weidu.conf~ ~weidu.conf~
		SET_2DA_ENTRY 0 2 3 "%LANGUAGE_BG2%"
	ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ BEGIN
		AT_NOW ~rmdir /s /q lang/xx_xx~
	END ELSE ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ BEGIN
		AT_NOW ~rm -rf lang/xx_xx~
	END
END*/

/////                                                  \\\\\
///// Get total number of strings in BG:EE TLK         \\\\\
/////                                                  \\\\\

COPY - ~EET/bgee.tra~ ~EET-inlined~
	REPLACE_TEXTUALLY ~^\([0-9]+\).*~ ~~
	REPLACE_TEXTUALLY ~^[^@0-9]+.*~ ~~
	REPLACE_TEXTUALLY ~^@\([0-9]+\).*~ ~\1~
	COUNT_2DA_ROWS 0 "tra_rows"
	WHILE ("%tra_rows%" > 0) BEGIN
		StrRef_cutoff = 0
		READ_2DA_ENTRY tra_rows 0 0 "StrRef_cutoff"
		PATCH_IF ("%StrRef_cutoff%" > 33998) BEGIN
			tra_rows = 0
		END ELSE BEGIN
			tra_rows -= 1
		END
	END
PRINT ~StrRef_cutoff = %StrRef_cutoff%~

/////                                                  \\\\\
///// HANDLE_CHARSETS                                  \\\\\
/////                                                  \\\\\

ACTION_IF NOT ~%LANGUAGE%~ STRING_EQUAL_CASE ~en_US~ BEGIN
	ACTION_DEFINE_ASSOCIATIVE_ARRAY charsetsTable BEGIN
		"cs_cz" => "CP1250"
		"de_de" => "CP1252"
		"en_us" => "CP1252"
		"es_es" => "CP1252"
		"fr_fr" => "CP1252"
		"it_it" => "CP1252"
		"ja_jp" => "CP932"
		"ko_kr" => "CP949"
		"pl_pl" => "CP1250"
		//"pt_br" => ""
		"ru_ru" => "CP1251"
		//"tr_tr" => ""
		//"uk_ua" => ""
		//"zh_cn" => ""
	END
	ACTION_DEFINE_ARRAY charsetsConvertArray BEGIN dialog scripts 2da END
	ACTION_DEFINE_ARRAY charsetsReloadArray BEGIN dialog scripts 2da END
	LAF ~HANDLE_CHARSETS~
		INT_VAR
		infer_charsets = 0
		STR_VAR
		tra_path = EVAL "%tra_path%"
		iconv_path = "EET/bin/win32/iconv"//available as part of the base system on OS X and GNU/Linux
		charset_table = charsetsTable
		convert_array = charsetsConvertArray
		reload_array = charsetsReloadArray
	END
END

LOAD_TRA ~EET/bgee.tra~

/////                                                  \\\\\
///// Tools 2da variables                              \\\\\
/////                                                  \\\\\

OUTER_SET jobs = 0
OUTER_SET quality = 0

ACTION_IF ((~%argv[0]%~ STRING_CONTAINS_REGEXP ~[Jj][0-9]+~ = 0) OR (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[Qq][0-9]+~ = 0)) BEGIN
	OUTER_SPRINT argv ~%argv[0]%~
	OUTER_PATCH_SAVE argv ~%argv%~ BEGIN
		//check jobs tp2 variable
		REPLACE_EVALUATE CASE_INSENSITIVE ~j\([0-9]+\)~ BEGIN
			PATCH_IF ( MATCH1 >= 0 ) AND ( MATCH1 <= 16 ) BEGIN
				SET jobs = MATCH1
			END ELSE BEGIN
				PATCH_WARN ~Amount of jobs (%MATCH1%) set via tp2 variable out of range~
			END
		END ~~

		//check quality tp2 variable
		REPLACE_EVALUATE CASE_INSENSITIVE ~q\([0-9]+\)~ BEGIN
			PATCH_IF ( MATCH1 >= 0 ) AND ( MATCH1 <= 9 ) BEGIN
				SET quality = MATCH1
			END ELSE BEGIN
				PATCH_WARN ~TIS output quality (%MATCH1%) set via tp2 variable out of range~
			END
		END ~~
	END
END
PRINT ~tile2ee and tileconv will use following settings: j%jobs%, q%quality%~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Extract and convert BG:EE resources              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~Extract BG:EE resources...~

/////                                                  \\\\\
///// tile2ee conversion                               \\\\\
/////                                                  \\\\\

ACTION_FOR_EACH var IN tis pvrz BEGIN
	PRINT ~~
	PRINT ~Preparing %var% files...~
	MKDIR ~%biff_dir%/v2/%var%~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --out "EET%os_slash%temp%os_slash%biff%os_slash%v2%os_slash%%var%" --biff-get-rest "^.*\.%var%$"~
	LAM bash_log
	ACTION_BASH_FOR ~%bgee_dir%/override~ ~^.+\.%var%$~ BEGIN
		COPY_LARGE + ~%BASH_FOR_FILESPEC%~ ~%biff_dir%/v2/%var%~
	END
	INCLUDE ~EET/lib/prep_%var%.tph~
END

ACTION_BASH_FOR ~%biff_dir%/v2/tis~ ~^.+\.tis$~ BEGIN
	AT_NOW ~%tile2ee% -e -f 1 -j %jobs% -q %quality% -d "EET%os_slash%temp%os_slash%biff%os_slash%v2%os_slash%pvrz" -o "EET%os_slash%temp%os_slash%biff" "EET%os_slash%temp%os_slash%biff%os_slash%v2%os_slash%tis%os_slash%%BASH_FOR_FILE%" %bash_debug%~ EXACT
	LAM bash_log
END

ACTION_BASH_FOR ~%biff_dir%/v2/tis~ ~^.+\.tis$~ BEGIN
	ACTION_IF NOT FILE_EXISTS ~%biff_dir%/%BASH_FOR_FILE%~ BEGIN
		MOVE + ~%BASH_FOR_FILESPEC%~ ~%biff_dir%~
	END
END

/////                                                  \\\\\
///// tis2bg2 conversion                               \\\\\
/////                                                  \\\\\

ACTION_FOR_EACH var IN bmp mos wed BEGIN
	PRINT ~~
	PRINT ~Preparing %var% files...~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --out "EET%os_slash%temp%os_slash%biff" --biff-get-rest "^.*\.%var%$"~
	LAM bash_log
	ACTION_BASH_FOR ~%bgee_dir%/override~ ~^.+\.%var%$~ BEGIN
		COPY_LARGE + ~%BASH_FOR_FILESPEC%~ ~%biff_dir%~
	END
	INCLUDE ~EET/lib/prep_%var%.tph~
END

//fix for OH2000 water issue
COPY + ~%biff_dir%/OH2000.wed~ ~%biff_dir%~
	READ_LONG 0x8 overlay_num
	READ_LONG 0x10 overlay_off
	READ_SHORT (overlay_off) tilemap_width
	READ_SHORT (overlay_off+0x2) tilemap_height
	READ_LONG (overlay_off+0x10) tilemap_off
	SET tilemap_cnt = tilemap_height * tilemap_width
	FOR (i = 0; i < tilemap_cnt; i = i + 1 ) BEGIN
		READ_SHORT ((tilemap_off + (0xa * i)) + 0x4) tilemap_startindex
		READ_BYTE ((tilemap_off + (0xa * i)) + 0x6) tilemap_drawoverlay
		PATCH_IF ((tilemap_drawoverlay = 2) AND !(tilemap_startindex = 65535)) BEGIN
			//PATCH_PRINT ~i: %i%; tilemap_startindex: %tilemap_startindex%; tilemap_drawoverlay: %tilemap_drawoverlay%~
			WRITE_SHORT  ((tilemap_off + (0xa * i)) + 0x4) ~-1~
		END
	END
BUT_ONLY

COPY + ~EET/base/palette~ ~%biff_dir%~

COPY + ~%tis2bg2%~ ~%biff_dir%~
OUTER_SPRINT tis2bg2 ~EET%os_slash%temp%os_slash%biff%os_slash%tis2bg2%exe%~

//alternative way to convert files, currently disabled
/*ACTION_FOR_EACH dir IN AR00 AR01 AR02 AR03 AR04 AR05 AR06 AR07 AR08 AR09 AR10 AR11 AR12 AR13 AR14 AR15 AR16 AR17 AR18 AR19 AR20 AR21 AR22 AR23 AR24 AR26 AR27 AR28 AR29 AR30 AR31 AR32 AR33 AR34 AR35 AR36 AR37 AR38 AR39 AR40 AR41 AR42 AR43 AR44 AR45 AR46 AR47 AR48 AR49 AR50 AR51 AR52 AR53 AR54 AR55 AR56 AR57 AR58 AR59 AR60 AR61 OH2 OH3 OH9 TU00 BEGIN
	MKDIR ~%biff_dir%/eet%dir%~
END

INCLUDE ~EET/lib/prep_tis2bg2.tph~ //define tis2bg2 array, with all rename rules and -s option where necessary

ACTION_PHP_EACH table_EET_tis2bg2 AS name => silent BEGIN
	AT_NOW ~%tis2bg2% -d -h -l %silent% %name% %name_1%~
END*/

ACTION_FOR_EACH dir IN AR00 AR01 AR02 AR03 AR04 AR05 AR06 AR07 AR08 AR09 AR10 AR11 AR12 AR13 AR14 AR15 AR16 AR17 AR18 AR19 AR20 AR21 AR22 AR23 AR24 AR26 AR27 AR28 AR29 AR30 AR31 AR32 AR33 AR34 AR35 AR36 AR37 AR38 AR39 AR40 AR41 AR42 AR43 AR44 AR45 AR46 AR47 AR48 AR49 AR50 AR51 AR52 AR53 AR54 AR55 AR56 AR57 AR58 AR59 AR60 AR61 OH2 OH3 OH9 TU00 BEGIN
	MKDIR ~%biff_dir%/eet%dir%~
	ACTION_BASH_FOR ~%biff_dir%~ ~^%dir%.+\.tis$~ BEGIN
		ACTION_IF FILE_EXISTS ~%biff_dir%/%BASH_FOR_RES%.wed~ BEGIN
			OUTER_PATCH_SAVE tis_new ~%BASH_FOR_RES%~ BEGIN
				REPLACE_TEXTUALLY ~^AR~ ~BG~
			END
			PRINT ~tis2bg2 conversion: %BASH_FOR_RES% => eet%dir%/%tis_new%~
			AT_NOW ~%tis2bg2% -d -h -l -s "%BASH_FOR_RES%" "eet%dir%%os_slash%%tis_new%"~
		END
	END
END

MOVE + ~%biff_dir%/OH3000N.BMP~ ~%biff_dir%/eetOH3~
	~%biff_dir%/OH9400LM.BMP~ ~%biff_dir%/eetOH9~
	~%biff_dir%/AR1320LM.BMP~ ~%biff_dir%/eetAR13/BG1320LM.BMP~
	~%biff_dir%/AR1006.MOS~ ~%biff_dir%/eetAR10/BG1006.MOS~
	~%biff_dir%/AR1007.MOS~ ~%biff_dir%/eetAR10/BG1007.MOS~
	~%biff_dir%/AR2003.MOS~ ~%biff_dir%/eetAR20/BG2003.MOS~
	~%biff_dir%/AR2005.MOS~ ~%biff_dir%/eetAR20/BG2005.MOS~
	~%biff_dir%/AR2007.MOS~ ~%biff_dir%/eetAR20/BG2007.MOS~
	~%biff_dir%/AR2009.MOS~ ~%biff_dir%/eetAR20/BG2009.MOS~
	~%biff_dir%/AR2010.MOS~ ~%biff_dir%/eetAR20/BG2010.MOS~
	~%biff_dir%/AR2011.MOS~ ~%biff_dir%/eetAR20/BG2011.MOS~
	~%biff_dir%/AR0506AC.TIS~ ~%biff_dir%/eetAR05/BG0506AC.TIS~
	~%biff_dir%/AR0506AO.TIS~ ~%biff_dir%/eetAR05/BG0506AO.TIS~

MKDIR ~%biff_dir%/eetMis0~
OUTER_SET currentTotal = 0
OUTER_SET currentFile = 0

ACTION_BASH_FOR ~%biff_dir%~ ~^.+\.tis$~ BEGIN
	ACTION_IF FILE_EXISTS ~%biff_dir%/%BASH_FOR_RES%.wed~ BEGIN
		ACTION_IF ( BASH_FOR_SIZE + currentTotal > 30000000 /* 30M */ && currentTotal > 0 ) BEGIN
			OUTER_SET currentFile = currentFile + 1
			OUTER_SET currentTotal = 0
			MKDIR ~EET/temp/biff/eetMis%currentFile%~
		END
		PRINT ~tis2bg2 conversion: %BASH_FOR_RES% => eetMis%currentFile%/%BASH_FOR_RES%~
		AT_NOW ~%tis2bg2% -d -h -l -s "%BASH_FOR_RES%" "eetMis%currentFile%%os_slash%%BASH_FOR_RES%"~
		OUTER_SET currentTotal = currentTotal + BASH_FOR_SIZE
	END
END

ACTION_IF FILE_EXISTS ~%biff_dir%/tis2bg2.log~ BEGIN //modified bash_log macro
	COPY + ~%biff_dir%/tis2bg2.log~ ~%biff_dir%~
		READ_ASCII 0 log (%SOURCE_SIZE%)
		PATCH_LOG ~%log%~
		SPRINT log ~~
		//DELETE_BYTES 0 %SOURCE_SIZE%
END

DELETE + ~%biff_dir%/tis2bg2.exe~
	~%biff_dir%/tis2bg2.log~

/////                                                  \\\\\
///// prepare BG:EE resources for PCU conversion       \\\\\
/////                                                  \\\\\

MKDIR ~%patch_dir%/bcs_app~
	~%patch_dir%/wed~

ACTION_FOR_EACH dir IN "%biff_dir%" "%biff_dir%/eetMis0" BEGIN
	ACTION_BASH_FOR ~%dir%~ ~^.+\.wed$~ BEGIN
		MOVE + ~%BASH_FOR_FILESPEC%~ ~%patch_dir%/wed~
	END
END

ACTION_FOR_EACH var IN are bcs cre dlg eff itm spl sto vvc BEGIN
	PRINT ~~
	PRINT ~Preparing %var% files...~
	MKDIR ~%patch_dir%/%var%~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --out "%patch_dir%/%var%" --biff-get-rest "^.*\.%var%$"~
	LAM bash_log
	ACTION_BASH_FOR ~%bgee_dir%/override~ ~^.+\.%var%$~ BEGIN
		COPY + ~%BASH_FOR_FILESPEC%~ ~%patch_dir%/%var%~
	END
	INCLUDE ~EET/lib/prep_%var%.tph~
END

ACTION_FOR_EACH file IN baldur.gam worldmap.wmp BEGIN
	ACTION_IF (FILE_EXISTS ~%bgee_dir%/override/%file%~) BEGIN
		COPY + ~%bgee_dir%/override/%file%~ ~%patch_dir%~
	END ELSE BEGIN
		AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --out "%patch_dir%" --biff-get-rest "%file%"~
		LAM bash_log
	END
END

/////                                                  \\\\\
///// prepare other BG:EE resources                    \\\\\
/////                                                  \\\\\

MKDIR ~EET/temp/array~
	~%bgee_dir%/lang/%LANGUAGE%/override~
	~EET/temp/wav~
	~EET/temp/chr~

//additional CRE sounds from IWD:EE moved to biff dir (overwritten if needed)
COPY + ~EET/base/wav~ ~EET/temp/wav~

ACTION_FOR_EACH var IN 2da bam pro wav BEGIN
	PRINT ~~
	PRINT ~Preparing %var% files...~
	MKDIR ~EET/temp/%var%~
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --out "EET/temp/%var%" --biff-get-rest "^.*\.%var%$"~
	LAM bash_log
	ACTION_BASH_FOR ~%bgee_dir%/override~ ~^.+\.%var%$~ BEGIN
		COPY_LARGE + ~%BASH_FOR_FILESPEC%~ ~EET/temp/%var%~
	END
	ACTION_IF ~%var%~ STRING_EQUAL_CASE ~WAV~ BEGIN
		//optional sounds stored in language dir
		ACTION_BASH_FOR ~%bgee_dir%/lang/%LANGUAGE%/override~ ~^.+\.WAV$~ BEGIN
			COPY + ~%BASH_FOR_FILESPEC%~ ~EET/temp/wav~
		END
	END
	INCLUDE ~EET/lib/prep_%var%.tph~
END

AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --out "EET/temp/CHR" --biff-get-rest "^.*\.CHR$"~
LAM bash_log

ACTION_FOR_EACH file IN animate.ids anisnd.ids gtimes.ids kit.ids bgee.sql BEGIN
	ACTION_IF (FILE_EXISTS ~%bgee_dir%/override/%file%~) BEGIN
		COPY + ~%bgee_dir%/override/%file%~ ~EET/temp/array~
	END ELSE BEGIN
		AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --out "EET/temp/array" --biff-get-rest "%file%"~
		LAM bash_log
	END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BG2:EE files patching                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~BG2:EE files patching...~

/////                                                  \\\\\
///// Soundsets GUI names                              \\\\\
/////                                                  \\\\\

ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EET_nameString" BEGIN
	~FEMALE4~ => ~4000024~
	~FEMALE3~ => ~4000023~
	~FEMALE2~ => ~4000022~
	~FEMALE1~ => ~4000021~
	~FEMALE~ => ~4000020~
	~MALE5~ => ~4000019~
	~MALE4~ => ~4000018~
	~MALE3~ => ~4000017~
	~MALE2~ => ~4000016~
	~MALE1~ => ~4000015~
	~MALE~ => ~4000014~
	~BGFeml6~ => ~4000013~
	~BGFeml5~ => ~4000012~
	~BGFeml4~ => ~4000011~
	~BGFeml3~ => ~4000010~
	~BGFeml2~ => ~4000009~
	~BGFeml1~ => ~4000008~
	~BGMainF~ => ~4000007~
	~BGMale6~ => ~4000006~
	~BGMale5~ => ~4000005~
	~BGMale4~ => ~4000004~
	~BGMale3~ => ~4000003~
	~BGMale2~ => ~4000002~
	~BGMale1~ => ~4000001~
	~BGMainM~ => ~4000000~
END

APPEND ~BGEE.SQL~ ~
INSERT INTO filenames_stringrefs ROWS (
);~ UNLESS ~filenames_stringrefs~

ACTION_PHP_EACH table_EET_nameString AS name => string BEGIN
	COPY_EXISTING ~BGEE.SQL~ ~override~
		REPLACE_TEXTUALLY ~\(INSERT INTO filenames_stringrefs ROWS (\)~ ~\1
	'%name%', 999%string%, 2,~
		REPLACE ~999%string%~ ( AT "string" )
	BUT_ONLY
END

/////                                                  \\\\\
///// DLC                                              \\\\\
/////                                                  \\\\\

COPY_EXISTING - ~BGEE.SQL~ ~override~
	REPLACE_TEXTUALLY ~,~ ~ ~
	COUNT_2DA_ROWS 4 "cntrow"
	SET dlc_found = 0
	SET dlc_cnt = 0
	FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		PATCH_IF (dlc_found = 0) BEGIN
			READ_2DA_ENTRY cnt 2 4 "col3"
			PATCH_IF (("%col3%" STRING_EQUAL_CASE "DLC")=1) BEGIN
				SET dlc_found = 1
			END
		END ELSE BEGIN
			READ_2DA_ENTRY cnt 0 4 "col1"
			PATCH_IF IS_AN_INT "%col1%" BEGIN
				PATCH_IF (col1 > dlc_cnt) BEGIN
					SET dlc_cnt = col1
				END
			END ELSE BEGIN
				PATCH_PRINT ~dlc_cnt = %dlc_cnt%~
				SET dlc_cnt = dlc_cnt + 2//in patch 1.3 MANLEY portraits already have number 6 assigned in dlc_content ROWS
				SET cnt = "%cntrow%"
			END
		END
	END

COPY - ~EET/temp/array/BGEE.SQL~ ~EET/temp/array~
	PATCH_FOR_EACH dlc IN BGEE_IOS_VOICEPACK1 BGEE_IOS_PORTRAITPACK1 BEGIN
		COUNT_REGEXP_INSTANCES ~'%dlc%'~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			INNER_ACTION BEGIN
				COPY_EXISTING ~BGEE.SQL~ ~override~
					COUNT_REGEXP_INSTANCES ~'%dlc%'~ num_matches
					PATCH_IF (num_matches = 0) BEGIN
						PATCH_PRINT ~Adding %dlc% DLC~
						PATCH_IF ~%dlc%~ STRING_EQUAL_CASE ~BGEE_IOS_PORTRAITPACK1~ BEGIN
							REPLACE_TEXTUALLY ~\(INSERT INTO DLC ROWS[%newline%]*(\)~ ~\1
	6, 'manley', 'DLCP1', 99931094, 99931095, 'BGEE_IOS_PORTRAITPACK1', 0, 'baldursgate_android_portraitpack1',~
							REPLACE ~99931094~ @31094
							REPLACE ~99931095~ @31095
						END ELSE PATCH_IF ~%dlc%~ STRING_EQUAL_CASE ~BGEE_IOS_VOICEPACK1~ BEGIN
							REPLACE_TEXTUALLY ~\(INSERT INTO DLC ROWS[%newline%]*(\)~ ~\1
	%dlc_cnt%, 'voice', 'DLCV1', 99931096, 99931097, 'BGEE_IOS_VOICEPACK1', 0, 'baldursgate_android_voicepack1',~
							REPLACE ~99931096~ @31096
							REPLACE ~99931097~ @31097
							REPLACE_TEXTUALLY ~\(INSERT INTO dlc_content ROWS[%newline%]*(\)~ ~\1
	'BGFeml4', %dlc_cnt%,	
	'BGFeml5', %dlc_cnt%,	
	'BGFeml6', %dlc_cnt%,	
	'BGMale4', %dlc_cnt%,
	'BGMale5', %dlc_cnt%,
	'BGMale6', %dlc_cnt%,~
						END
					END
				BUT_ONLY
			END
		END
	END

/*
COPY - ~EET/temp/array/BGEE.SQL~ ~EET/temp/array~
	REPLACE_TEXTUALLY ~,~ ~ ~
	COUNT_2DA_ROWS 4 "cntrow"
	SET dlc_found = 0
	FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		PATCH_IF (dlc_found = 0) BEGIN
			READ_2DA_ENTRY cnt 2 4 "col3"
			PATCH_IF (("%col3%" STRING_EQUAL_CASE "DLC")=1) BEGIN
				SET dlc_found = 1
			END
		END ELSE BEGIN
			READ_2DA_ENTRY cnt 0 4 "col1"
			PATCH_IF IS_AN_INT "%col1%" BEGIN
				READ_2DA_ENTRY cnt 1 4 "col2"
				READ_2DA_ENTRY cnt 2 4 "col3"
				READ_2DA_ENTRY cnt 3 4 "col4"
				READ_2DA_ENTRY cnt 4 4 "col5"
				READ_2DA_ENTRY cnt 5 4 "col6"
				READ_2DA_ENTRY cnt 6 4 "col7"
				READ_2DA_ENTRY cnt 7 4 "col8"
				INNER_ACTION BEGIN
					COPY_EXISTING ~BGEE.SQL~ ~override~
						COUNT_REGEXP_INSTANCES ~%col6%~ num_matches
						PATCH_IF (num_matches = 0) BEGIN
							PATCH_PRINT ~Adding %col6% DLC~
							PATCH_IF ~%col6%~ STRING_EQUAL_CASE ~'BGEE_IOS_PORTRAITPACK1'~ BEGIN
								REPLACE_TEXTUALLY ~\(INSERT INTO DLC ROWS[%newline%]*(\)~ ~\1
	6, %col2%, %col3%, 999%col4%, 999%col5%, %col6%, 0, %col8%,~
								REPLACE ~999%col4%~ ( AT "col4" )
								REPLACE ~999%col5%~ ( AT "col5" )
							END ELSE BEGIN
								REPLACE_TEXTUALLY ~\(INSERT INTO DLC ROWS[%newline%]*(\)~ ~\1
	%dlc_cnt%, %col2%, %col3%, 999%col4%, 999%col5%, %col6%, 0, %col8%,~
								REPLACE ~999%col4%~ ( AT "col4" )
								REPLACE ~999%col5%~ ( AT "col5" )
								SET dlc_cnt = dlc_cnt + 1
							END
						END
					BUT_ONLY
				END
			END ELSE BEGIN
				SET cnt = "%cntrow%"
			END
		END
	END
*/

/////                                                  \\\\\
///// Journal                                          \\\\\
/////                                                  \\\\\

COPY_EXISTING ~BGEE.SQL~ ~override~
	REPLACE_TEXTUALLY ~,~ ~ replace_me_back~
	COUNT_2DA_ROWS 4 "cntrow"
	FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 2 4 "col3"
		PATCH_IF (("%col3%" STRING_EQUAL_CASE "quests")=1) BEGIN
			SET quests_row = cnt
		END ELSE PATCH_IF (("%col3%" STRING_EQUAL_CASE "journals_quests")=1) BEGIN
			SET journals_quests_row = cnt
		END ELSE PATCH_IF (("%col3%" STRING_EQUAL_CASE "portraits")=1) BEGIN
			SET portraits_row = cnt
			SET cnt = "%cntrow%"
		END
	END
	READ_2DA_ENTRY ( journals_quests_row - 1 ) 0 4 "last_quest"
	FOR (cnt = (quests_row + 1); cnt < journals_quests_row; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 4 "compareWith"
		PATCH_IF (("%last_quest%" < "%compareWith%")=1) BEGIN
			SET last_quest = compareWith
		END
	END
	PATCH_PRINT ~last_quest = %last_quest%~
	READ_2DA_ENTRY ( journals_quests_row - 1 ) 0 4 "col1"
	READ_2DA_ENTRY ( journals_quests_row - 1 ) 1 4 "col2"
	READ_2DA_ENTRY ( journals_quests_row - 1 ) 2 4 "col3"
	READ_2DA_ENTRY ( journals_quests_row - 1 ) 3 4 "col4"
	READ_2DA_ENTRY ( journals_quests_row - 1 ) 4 4 "col5"
	READ_2DA_ENTRY ( journals_quests_row - 1 ) 5 4 "col6"
	READ_2DA_ENTRY ( journals_quests_row - 1 ) 6 4 "col7"
	REPLACE_TEXTUALLY ~\(%col1%[ ]+%col2%[ ]+%col3%[ ]+%col4%[ ]+%col5%[ ]+%col6%[ ]+%col7%.*\)~ ~\1
append_quests_here~
	READ_2DA_ENTRY ( portraits_row - 1 ) 0 4 "col1"
	READ_2DA_ENTRY ( portraits_row - 1 ) 1 4 "col2"
	READ_2DA_ENTRY ( portraits_row - 1 ) 2 4 "col3"
	READ_2DA_ENTRY ( portraits_row - 1 ) 3 4 "col4"
	READ_2DA_ENTRY ( portraits_row - 1 ) 4 4 "col5"
	READ_2DA_ENTRY ( portraits_row - 1 ) 5 4 "col6"
	REPLACE_TEXTUALLY ~\(%col1%[ ]+%col2%[ ]+%col3%[ ]+%col4%[ ]+%col5%[ ]+%col6%.*\)~ ~\1
append_journals_quests_here~
	REPLACE_TEXTUALLY ~ replace_me_back~ ~,~

COPY - ~EET/temp/array/BGEE.SQL~ ~EET/temp/array~
	REPLACE_TEXTUALLY ~,~ ~ ~
	COUNT_2DA_ROWS 4 "cntrow"
	FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 2 4 "col3"
		PATCH_IF (("%col3%" STRING_EQUAL_CASE "quests")=1) BEGIN
			SET quests_row = cnt
		END ELSE PATCH_IF (("%col3%" STRING_EQUAL_CASE "journals_quests")=1) BEGIN
			SET journals_quests_row = cnt
		END ELSE PATCH_IF (("%col3%" STRING_EQUAL_CASE "portraits")=1) BEGIN
			SET portraits_row = cnt
			SET cnt = "%cntrow%"
		END
	END
	FOR (cnt2 = "%journals_quests_row%" - 1; cnt2 > "%quests_row%"; cnt2 = cnt2 - 1) BEGIN
		READ_2DA_ENTRY cnt2 0 4 "col1"
		READ_2DA_ENTRY cnt2 1 4 "col2"
		READ_2DA_ENTRY cnt2 2 4 "col3"
		READ_2DA_ENTRY cnt2 3 4 "col4"
		READ_2DA_ENTRY cnt2 4 4 "col5"
		READ_2DA_ENTRY cnt2 5 4 "col6"
		READ_2DA_ENTRY cnt2 6 4 "col7"
		PATCH_IF NOT ("%col1%" = 1) BEGIN //not "Important Matters"
			SET col1 = col1 + last_quest - 1
			SPRINT ~string~ ~%col1%,%col2%,%col3%,%col4%,%col5%,%col6%,%col7%,~
			PATCH_PRINT ~quest added: %string%~
			INNER_ACTION BEGIN
				COPY_EXISTING ~BGEE.SQL~ ~override~
					REPLACE_TEXTUALLY ~%col3%~ ~rep_col3~
					REPLACE_TEXTUALLY ~append_quests_here~ ~append_quests_here
%string%~
					PATCH_IF ((col3 > 0) AND (col3 <= StrRef_cutoff)) BEGIN
						REPLACE ~%col3%~ ( AT "col3" )
					END
					REPLACE_TEXTUALLY ~rep_col3~ ~%col3%~
			END
		END
	END
	FOR (cnt2 = "%portraits_row%" - 1; cnt2 > "%journals_quests_row%"; cnt2 = cnt2 - 1) BEGIN
		READ_2DA_ENTRY cnt2 0 4 "col1"
		READ_2DA_ENTRY cnt2 1 4 "col2"
		READ_2DA_ENTRY cnt2 2 4 "col3"
		READ_2DA_ENTRY cnt2 3 4 "col4"
		READ_2DA_ENTRY cnt2 4 4 "col5"
		READ_2DA_ENTRY cnt2 5 4 "col6"
		PATCH_IF NOT ("%col2%" = 1) BEGIN //not "Important Matters"
			SET col2 = col2 + last_quest - 1
		END
		SPRINT ~string~ ~%col1%,%col2%,%col3%,%col4%,%col5%,%col6%,~
		PATCH_PRINT ~journal quest added: %string%~
		INNER_ACTION BEGIN
			COPY_EXISTING ~BGEE.SQL~ ~override~
				REPLACE_TEXTUALLY ~%col1%~ ~rep_col1~
				REPLACE_TEXTUALLY ~append_journals_quests_here~ ~append_journals_quests_here
%string%~
				PATCH_IF ((col1 > 0) AND (col1 <= StrRef_cutoff)) BEGIN
					REPLACE ~%col1%~ ( AT "col1" )
				END
				REPLACE_TEXTUALLY ~rep_col1~ ~%col1%~
		END
	END

COPY_EXISTING ~BGEE.SQL~ ~override~
	REPLACE_TEXTUALLY ~[%newline%]*append_quests_here~ ~~
	REPLACE_TEXTUALLY ~[%newline%]*append_journals_quests_here~ ~~

/////                                                  \\\\\
///// RNDTRES.2DA - new 2da added in BG2:EE patch 1.3  \\\\\
/////                                                  \\\\\

COPY + ~EET/temp/array/RNDEQUIP.2DA~ ~EET/temp/array~
	~EET/temp/array/RNDMAGIC.2DA~ ~EET/temp/array~
	~EET/temp/array/RNDSCROL.2DA~ ~EET/temp/array~
	~EET/temp/array/RNDTREAS.2DA~ ~EET/temp/array~
	~EET/temp/array/RNDWEP.2DA~ ~EET/temp/array~
	PRETTY_PRINT_2DA
	REPLACE_TEXTUALLY ~2DA[ 	]+V1.0[%newline%]+.[%newline%]+.+[%newline%]+~ ~~
	SPRINT filename "%SOURCE_RES%"
	COUNT_2DA_ROWS 1 "cntrow"
	FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		PATCH_IF ("%filename%" STRING_EQUAL_CASE "RNDEQUIP") BEGIN
			SPRINT item ~RNDEQU~
		END ELSE PATCH_IF ("%filename%" STRING_EQUAL_CASE "RNDMAGIC") BEGIN
			SPRINT item ~RNDMAG~
		END ELSE PATCH_IF ("%filename%" STRING_EQUAL_CASE "RNDSCROL") BEGIN
			SPRINT item ~RNDSCR~
		END ELSE PATCH_IF ("%filename%" STRING_EQUAL_CASE "RNDTREAS") BEGIN
			SPRINT item ~RNDTRE~
		END ELSE PATCH_IF ("%filename%" STRING_EQUAL_CASE "RNDWEP") BEGIN
			SPRINT item ~RNDWEP~
		END
		SET value = cnt + 1
		SET_2DA_ENTRY cnt 0 1 ~%item%%value%_~
		SET resref_itm_total = resref_itm_total + 1
		SPRINT EVAL "resref_itm_source%resref_itm_total%" "%item%0%value%"
		SPRINT EVAL "resref_itm_dest%resref_itm_total%" "%item%%value%_"
		INNER_ACTION BEGIN
			ACTION_IF NOT FILE_EXISTS_IN_GAME ~%item%%value%_.ITM~ BEGIN
				COPY_EXISTING ~RNDTRE01.ITM~ ~override/%item%%value%_.ITM~
			END ELSE BEGIN
				WARN ~%item%%value%_.ITM already exists in game, possible conflict~
			END
			ACTION_IF FILE_EXISTS ~%patch_dir%/itm/%item%0%value%.ITM~ BEGIN
				DELETE + ~%patch_dir%/itm/%item%0%value%.ITM~
			END
		END
	END
	PATCH_PRINT ~resref_itm_total updated with random items = %resref_itm_total%~
	FOR (i=1; i<="%resref_itm_total%"; i=i+1) BEGIN
		SPRINT name_source "resref_itm_source%i%"
		SPRINT name_dest "resref_itm_dest%i%"
		SPRINT resref_index_source EVALUATE_BUFFER ~%%name_source%%~
		SPRINT resref_index_dest EVALUATE_BUFFER ~%%name_dest%%~
		REPLACE_TEXTUALLY ~\([ 	]\)%resref_index_source%\([^A-Z^a-z]\)~ ~\1%resref_index_dest%\2~
	END

ACTION_IF FILE_EXISTS_IN_GAME rndtres.2da BEGIN
	COPY_EXISTING ~rndtres.2da~ ~override~
		APPEND_FILE ~EET/temp/array/RNDEQUIP.2DA~
		APPEND_FILE ~EET/temp/array/RNDMAGIC.2DA~
		APPEND_FILE ~EET/temp/array/RNDSCROL.2DA~
		APPEND_FILE ~EET/temp/array/RNDTREAS.2DA~
		APPEND_FILE ~EET/temp/array/RNDWEP.2DA~
		PRETTY_PRINT_2DA
	BUT_ONLY
END ELSE BEGIN
<<<<<<<< EET-inlined/rndtres.2da
2DA V1.0
*
 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20
>>>>>>>>
	COPY ~EET-inlined/rndtres.2da~ ~override~
		APPEND_FILE ~EET/temp/array/RNDEQUIP.2DA~
		APPEND_FILE ~EET/temp/array/RNDMAGIC.2DA~
		APPEND_FILE ~EET/temp/array/RNDSCROL.2DA~
		APPEND_FILE ~EET/temp/array/RNDTREAS.2DA~
		APPEND_FILE ~EET/temp/array/RNDWEP.2DA~
		PRETTY_PRINT_2DA
	BUT_ONLY
END

/////                                                  \\\\\
///// Patching each group of BG2:EE resources          \\\\\
/////                                                  \\\\\

////////////////////////
// Viader's Extended Animations
////////////////////////
//0xE240 - 0xE2FF - reserved by Extended Animations
//0xE330 - 0xE3FF - free
//0xE440 - 0xE4FF - free
//0xE530 - 0xE5FF - free
//0xE620 - 0xE6FF - free
//0xE730 - 0xE7FF - free
//0xE850 - 0xE8FF - free
//0xE920 - 0xE9FF - free
//0xEA30 - 0xEAFF - free
//0xEB30 - 0xEBFF - free
//0xEC30 - 0xECFF - free
//0xED30 - 0xEDFF - free
//0xEE20 - 0xEEFF - free

ACTION_FOR_EACH var IN IDS TLK 2DA CRE ITM BCS DLG BEGIN
	INCLUDE ~EET/lib/bg2_%var%.tph~
END

/////                                                  \\\\\
///// Dynamic biography                                \\\\\
/////                                                  \\\\\

OUTER_SET bio = 0
COPY ~EET/base/compile/baf/K#BIO.baf~ ~EET/base/compile/baf~
	PATCH_FOR_EACH clastext IN ~%bio15881%~ /*FIGHTER_ALL*/ ~15888~ /*RANGER_ALL*/ ~15887~ /*PALADIN_ALL*/ ~15884~ /*CLERIC_ALL*/ ~15885~ /*DRUID_ALL*/ ~%bio15882%~ /*MAGE_ALL*/ ~15883~ /*THIEF_ALL*/ ~15886~ /*BARD_ALL*/ ~15884~ /*MONK*/ ~15889~ /*multi class*/ BEGIN
		PATCH_FOR_EACH racetext IN ~15895~ /*HUMAN*/ ~15891~ /*ELF*/ ~15892~ /*HALF_ELF*/ ~15890~ /*DWARF*/ ~15893~ /*HALFLING*/ ~15894~ /*GNOME*/ ~%bio31709%~ /*HALF_ORC*/ BEGIN
			GET_STRREF ~%clastext%~ bio_clas
			GET_STRREF ~%racetext%~ bio_race
			SET strref = RESOLVE_STR_REF ("%bio_clas%

%bio_race%")
			SET bio = bio + 1
			REPLACE_TEXTUALLY ~,K#%bio%,~ ~,%strref%,~
		END
	END
	GET_STRREF 33347 bio_bg2
STRING_SET ~33347~ ~~
STRING_SET ~15881~ ~%bio_bg2%~

/////                                                  \\\\\
///// Compatibility patches                            \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~EET/temp/WeiDU.log~ BEGIN
	COPY - ~EET/tbl/compatibility.tbl~ ~EET/tbl~
		COUNT_2DA_ROWS 2 "cntrow"
		FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
			READ_2DA_ENTRY cnt 0 2 "col1"
			READ_2DA_ENTRY cnt 1 2 "col2"
			PATCH_IF ("%col2%" = 1) BEGIN
				INNER_ACTION BEGIN
					ACTION_IF (FILE_CONTAINS ~EET/temp/WeiDU.log~ EVAL ~^%col1%~) BEGIN
						PRINT ~Including %col1% compatibility patches~
						INCLUDE ~EET/compat/%col1%/include.tph~
					END
				END
			END
		END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Dialogue resolving                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~Dialogue resolving...~

/////                                                  \\\\\
///// Decompile BG:EE DLG files                        \\\\\
/////                                                  \\\\\

COPY + ~%patch_dir%/dlg~ ~%patch_dir%/dlg~
	DECOMPILE_DLG_TO_D
	//REPLACE_TEXTUALLY CASE_SENSITIVE ~~~~~^BEGIN \(~\).*%SOURCE_RES%\(~\)$~~~~~ ~BEGIN \1%SOURCE_RES%\2~
	REPLACE_TEXTUALLY ~EET/TEMP/PATCH/DLG/~ ~~

ACTION_BASH_FOR ~%patch_dir%/dlg~ ~.*~ BEGIN
	MOVE + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.D~
END

/////                                                  \\\\\
///// BG:EE DLG patching                               \\\\\
/////                                                  \\\\\

//SAREVO.D - replaces a reference to Angelo who isn't actually in the Duchal Palace, # will be changed to @ during PCU conversion
COPY + ~%patch_dir%/dlg/SAREVO.D~ ~%patch_dir%/dlg~
	REPLACE_TEXTUALLY ~THEN BEGIN 9[%newline%]*SAY #2317~ ~THEN BEGIN 9
  SAY #1000300~
	REPLACE_TEXTUALLY ~THEN BEGIN 24[%newline%]*SAY #17334~ ~THEN BEGIN 24
  SAY #1000300~
BUT_ONLY

COPY + ~%patch_dir%/dlg/BPNAJIM.D~ ~%patch_dir%/dlg~
	REPLACE_TEXTUALLY ~IncrementChapter("BPEND")[%newline%]GoToStartScreen()~ ~ClearAllActions()
CreateCreatureObject("K#FAMREM",Player1,0,0,0)
StartCutScene("K#CUTBP2")~
BUT_ONLY

/////                                                  \\\\\
///// Resolve BG:EE <-> BG2:EE dialogue conflicts      \\\\\
/////                                                  \\\\\

ACTION_FOR_EACH "dialogue" IN BDORN BEDWIN BIMOEN2 BJAHEIR BMINSC BNEERA BRASAAD BVICONI BEGIN
	//BG2:EE files
	COPY_EXISTING ~%dialogue%.DLG~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY CASE_SENSITIVE ~\(^IF .*\)  ~ ~\1Global("ENDOFBG1","GLOBAL",2)
~
//			REPLACE_TEXTUALLY CASE_SENSITIVE ~~~~~\(^IF [^~]*~\)\([^~]\)~~~~~ ~\1Global("ENDOFBG1","GLOBAL",2)
//\2~/*~*/
		END
	//BG:EE files
	COPY + ~%patch_dir%/dlg/%dialogue%.D~ ~%patch_dir%/dlg~
		REPLACE_TEXTUALLY ~~~~~^BEGIN \(~\)%SOURCE_RES%\(~\)$~~~~~ ~APPEND \1%SOURCE_RES%\2~
		REPLACE_TEXTUALLY CASE_SENSITIVE ~\(^IF .*\)  ~ ~\1GlobalLT("ENDOFBG1","GLOBAL",2)
~
//		REPLACE_TEXTUALLY CASE_SENSITIVE ~~~~~\(^IF [^~]*~\)\([^~]\)~~~~~ ~\1GlobalLT("ENDOFBG1","GLOBAL",2)
//\2~/*~*/
		REPLACE_TEXTUALLY CASE_SENSITIVE ~THEN BEGIN ~ ~THEN BEGIN s~
		REPLACE_TEXTUALLY CASE_SENSITIVE ~GOTO ~ ~GOTO s~
	APPEND_OUTER + ~%patch_dir%/dlg/%dialogue%.D~ ~
END~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// PCU conversion                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~PCU conversion...~

//FOR variables
OUTER_SET count = 0
OUTER_SET cntrow = 0
OUTER_SET cnt = 0
OUTER_SET cntare = 0
OUTER_SET cntcre = 0
OUTER_SET cntwav = 0

//reporting variables
OUTER_SET are_check = 0
OUTER_SET baf_check = 0
OUTER_SET bcs_check = 0
OUTER_SET cre_check = 0
OUTER_SET d_check = 0
OUTER_SET dlg_check = 0
OUTER_SET eff_check = 0
OUTER_SET gam_check = 0
OUTER_SET itm_check = 0
OUTER_SET spl_check = 0
OUTER_SET sto_check = 0
OUTER_SET tra_check = 0
OUTER_SET tp2_check = 0
OUTER_SET vvc_check = 0
OUTER_SET wed_check = 0
OUTER_SET wmp_check = 0
OUTER_SET ignore_check = 0
OUTER_SET ren_check = 0

//create file list for conversion
COPY + ~EET-inlined/blank.txt~ ~EET/temp/Filelist.txt~
ACTION_FOR_EACH "var" IN ARE BCS BCS_APP CRE DLG EFF ITM SPL STO VVC WED "" BEGIN
	ACTION_BASH_FOR ~%patch_dir%/%var%~ ~^.+$~ BEGIN
		APPEND_OUTER + ~EET/temp/Filelist.txt~ ~%var%/%BASH_FOR_FILE%~
	END
END

//conversion
PRINT ~Please be patient during the conversion process. This may take some time...~

COPY + ~EET/temp/Filelist.txt~ ~EET/temp~
	REPLACE_TEXTUALLY ~ ~ ~?~
	COUNT_2DA_ROWS 1 "cntrow"
	PATCH_PRINT ~%cntrow% entries parsed, commencing patching...~
	FOR (count = 0; count < "%cntrow%"; count = count + 1) BEGIN
		SET cntcurrent = "%count%" + 1
		READ_2DA_ENTRY "%count%" 0 1 "patch_name"
		INNER_PATCH_SAVE ~patch_name~ ~%patch_name%~ BEGIN
			REPLACE_TEXTUALLY ~\?~ ~ ~
		END
		INNER_ACTION BEGIN
			COPY + ~%patch_dir%/%patch_name%~ ~%patch_dir%/%patch_name%~
				SET checked = 0
				READ_ASCII 0x0 "filetype" ELSE ~Ignoring 0 byte file~
				LAUNCH_PATCH_MACRO ~PCU_core_patch~
			//Unrecognised files
			ACTION_IF ("%checked%" = 0) BEGIN
				OUTER_SET ignore_check = "%ignore_check%" + 1
				PRINT ~%cntcurrent%/%cntrow% Ignoring %patch_name%...~
			END
			//Complete INNER_PATCH and FOR commands during reading of Filelist.txt
		END
	END

PRINT ~Conversion complete. Statistics:
ARE %are_check%
BAF %baf_check%
BCS %bcs_check%
CRE %cre_check%
D   %d_check%
DLG %dlg_check%
EFF %eff_check%
GAM %gam_check%
ITM %itm_check%
SPL %spl_check%
STO %sto_check%
TP* %tp2_check%
TRA %tra_check%
VVC %vvc_check%
WED %wed_check%
WMP %wmp_check%

%ignore_check% files ignored.
%ren_check% files renamed.~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BG:EE files patching and installation            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

PRINT ~~
PRINT ~BG:EE files patching and installation...~

/////                                                  \\\\\
///// Dummy mod files                                  \\\\\
/////                                                  \\\\\

ACTION_FOR_EACH var IN g3 000 UB xxx BEGIN
	ACTION_BASH_FOR ~%bgee_dir%/override~ ~^.+\.%var%$~ BEGIN
		COPY ~%BASH_FOR_FILESPEC%~ ~override~
	END
END

/////                                                  \\\\\
///// 2DA                                              \\\\\
/////                                                  \\\\\

<<<<<<<< .../chapter.2da
2DA V1.0
NONE
                        0           1
SWITCH                  DEFAULT     DEFAULT
DEFAULT                 title       text
DWARF                   0           0
ELF                     0           0
HALFELF                 0           0
HALFLING                0           0
GNOME                   0           0
HUMAN                   0           0
MAGE                    0           0
FIGHTER                 0           0
CLERIC                  0           0
THIEF                   0           0
BARD                    0           0
PALADIN                 0           0
FIGHTER_MAGE            0           0
FIGHTER_CLERIC          0           0
FIGHTER_THIEF           0           0
FIGHTER_MAGE_THIEF      0           0
DRUID                   0           0
RANGER                  0           0
MAGE_THIEF              0           0
CLERIC_MAGE             0           0
CLERIC_THIEF            0           0
FIGHTER_DRUID           0           0
FIGHTER_MAGE_CLERIC     0           0
CLERIC_RANGER           0           0
>>>>>>>>

//Prologue
COPY + ~.../chapter.2da~ ~EET/temp/2da/CHPTXT0_.2da~
	GET_STRREF 15879 text_1
	GET_STRREF 15880 text_2
	GET_STRREF 15896 text_3
	GET_STRREF 15898 text_4
	SET text = RESOLVE_STR_REF (~%text_1%

%text_2%

%text_3%

%text_4%~ [INTRO_])
	REPLACE_TEXTUALLY ~title~ ~16202~
	REPLACE_TEXTUALLY ~text~ ~%text%~
	PRETTY_PRINT_2DA

//Chapter1
COPY + ~.../chapter.2da~ ~EET/temp/2da/CHPTXT1_.2da~
	GET_STRREF 11898 text_1
	GET_STRREF 16190 text_2
	GET_STRREF 11899 text_3
	SET text = RESOLVE_STR_REF (~%text_1%

%text_2%

%text_3%~ [CHAP00_])
	REPLACE_TEXTUALLY ~title~ ~16203~
	REPLACE_TEXTUALLY ~text~ ~%text%~
	PRETTY_PRINT_2DA

ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EET_nameMos" BEGIN
	"BPEND" => "BPEND"
	"CHPTXT0_" => "GUICHP0B"
	"CHPTXT1_" => "GUICHP1B"
	"CHPTXT2_" => "GUICHP2B"
	"CHPTXT3_" => "GUICHP3B"
	"CHPTXT4_" => "GUICHP4B"
	"CHPTXT5_" => "GUICHP5B"
	"CHPTXT6_" => "GUICHP6B"
	"CHPTXT7_" => "GUICHP7B"
	"CHPTXT8_" => "GUICHP8B"
	"DRMTXT2_" => "GUIDRM2"
	"DRMTXT3_" => "GUIDRM3"
	"DRMTXT4_" => "GUIDRM4"
	"DRMTXT5_" => "GUIDRM5"
	"DRMTXT6_" => "GUIDRM6"
	"DRMTXT7_" => "GUIDRM7"
	"ISLOFF" => "LEAVE"
	"ISLON" => "ARRIVE"
	"TOSCEND" => "TOSCEND"
	"TOSCST" => "ULGOTHST"
END
ACTION_PHP_EACH table_EET_nameMos AS name => mos BEGIN
	COPY + ~EET/temp/2da/%name%.2DA~ ~EET/temp/2da~
		SET_2DA_ENTRY 1 0 0 ~%mos%~
END

COPY + ~EET/temp/2da/BPEND.2DA~ ~EET/temp/2da~
	REPLACE ~31809~ @31809
	PRETTY_PRINT_2DA

COPY + ~EET/temp/2da/TOSCST.2DA~ ~EET/temp/2da~
	REPLACE ~24112~ @24105
	PRETTY_PRINT_2DA

COPY + ~EET/temp/2da/TOSCEND.2DA~ ~EET/temp/2da~
	REPLACE ~24117~ @24117
	PRETTY_PRINT_2DA

COPY ~EET/temp/2da~ ~override~

/////                                                  \\\\\
///// ARE                                              \\\\\
/////                                                  \\\\\

// Area script assigner
COPY + ~%patch_dir%/are/BG1213.ARE~ ~%patch_dir%/are~
	WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8 //typo in BG:EE, assigned AE1213.BCS instead of AR1213.BCS
BUT_ONLY
COPY + ~%patch_dir%/are~ ~%patch_dir%/are~
	WRITE_SHORT 0x52 ~0~ //fixes stencil water transparency (0 sets it to 50%)
	PATCH_IF (SOURCE_SIZE > 0x11b) BEGIN
		READ_ASCII 0x94 "script"
		SET "exists" = 0
		SET "scexists" = 0
		SET "unassigned" = 0
		SET "missing" = 0
		PATCH_IF (FILE_EXISTS ~%patch_dir%/bcs/%SOURCE_RES%.BCS~) BEGIN
			SET "exists" = 1
		END
		PATCH_IF (FILE_EXISTS ~%patch_dir%/bcs/%script%.BCS~) BEGIN
			SET "scexists" = 1
		END
		PATCH_IF (("%scexists%" = 0) AND ("%exists%" = 0)) BEGIN
			SET "missing" = 1
		END
		PATCH_IF (("%scexists%" = 0) AND ("%exists%" = 1)) BEGIN
			SET "unassigned" = 1
		END
		PATCH_IF ("%unassigned%" = 1) BEGIN
			PATCH_PRINT ~%SOURCE_FILE% - Area Script (%SOURCE_RES%.BCS) assigned~
			WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
		END ELSE PATCH_IF ("%missing%" = 1) BEGIN
			PATCH_IF NOT (("%script%" STRING_EQUAL_CASE "NONE") OR ("%script%" STRING_EQUAL_CASE "")) BEGIN
				PATCH_PRINT ~%SOURCE_FILE% - Missing area Script (%script%.BCS) created~
				INNER_ACTION BEGIN
					COPY + ~EET/base/blank.bcs~ ~%patch_dir%/bcs/%script%.BCS~
				END
			END ELSE BEGIN
				PATCH_PRINT ~%SOURCE_FILE% - Missing area Script (%SOURCE_RES%.BCS) created and assigned~
				INNER_ACTION BEGIN
					COPY + ~EET/base/blank.bcs~ ~%patch_dir%/bcs/%SOURCE_RES%.BCS~
				END
				WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
			END
		END
	END
BUT_ONLY

// Area traification and installation
OUTER_SPRINT ~log~ ~%slash%%slash%log file generated during EET installation~
COPY ~%patch_dir%/are~ ~override~
	SPRINT ~log~ ~%log%%WNL%%WNL%COPY "%SOURCE_FILESPEC%" "override"~
	//Info points, trigger points, and exit (regions/triggers)
	READ_LONG 0x5c "info_off"
	READ_SHORT 0x5a "info_cnt"
	FOR (cnt=0; cnt<"%info_cnt%"; cnt=cnt+1) BEGIN
		PATCH_FOR_EACH ~add_off~ IN 0x64 0xb8 BEGIN
			SET offset = ("%info_off%"+0xc4*cnt+"%add_off%") //Information text (for info points), Name
			READ_LONG offset "StrRef"
			PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
				SAY offset ( AT "StrRef" )
				SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
			END
		END
	END
	//Containers
	READ_LONG 0x70 "contain_off"
	READ_SHORT 0x74 "contain_cnt"
	FOR (cnt=0; cnt<"%contain_cnt%"; cnt=cnt+1) BEGIN
		SET offset = ("%contain_off%"+0xc0*cnt+0x84) //Lockpick string
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
		END
	END
	//Doors
	READ_LONG 0xa8 "door_off"
	READ_SHORT 0xa4 "door_cnt"
	FOR (cnt=0; cnt<"%door_cnt%"; cnt=cnt+1) BEGIN
		PATCH_FOR_EACH ~add_off~ IN 0x98 0xb4 BEGIN
			SET offset = ("%door_off%"+0xc8*cnt+"%add_off%") //Lockpick string, Dialog speaker name
			READ_LONG offset "StrRef"
			PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
				SAY offset ( AT "StrRef" )
				SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
			END
		END
	END
	//Automap Notes
	READ_LONG 0xc4 "note_off"
	READ_LONG 0xc8 "note_cnt"
	FOR (cnt=0; cnt<"%note_cnt%"; cnt=cnt+1) BEGIN
		SET offset = ("%note_off%"+0x34*cnt+0x4) //Note text
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
		END
	END
	//Rest Interruptions
	READ_LONG 0xc0 "rest_off"
	FOR (cnt=0; cnt<10; cnt=cnt+1) BEGIN
		SET offset = ("%rest_off%"+0x4*cnt+0x20) //Interruption explantion text (10*0x4)
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
		END
	END
COPY + ~EET-inlined/blank.txt~ ~EET/temp/are.tph~
	PATCH_LOG ~%log%~
APPEND_OUTER + ~EET/temp/are.tph~ ~%log%~

COPY_EXISTING ~OH1000.ARE~ ~override/BG0000.ARE~
	WRITE_ASCII 0x94 ~BG0000~ #8

/////                                                  \\\\\
///// BCS                                              \\\\\
/////                                                  \\\\\

// PDIALOG.2DA dream script merging
ACTION_BASH_FOR ~%patch_dir%/bcs_app~ ~.*\.BAF$~ BEGIN
	COPY + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%~
		REPLACE_TEXTUALLY ~^IF~ ~IF
  Global("ENDOFBG1","GLOBAL",0)~
		REPLACE_TEXTUALLY ~Global("ENDOFBG1","GLOBAL",0)[%newline%]*Global("ENDOFBG1","GLOBAL",0)~ ~Global("ENDOFBG1","GLOBAL",0)~
	ACTION_IF (FILE_EXISTS_IN_GAME ~%BASH_FOR_RES%.BCS~) BEGIN
		COPY_EXISTING ~%BASH_FOR_RES%.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY ~^IF~ ~IF
  Global("ENDOFBG1","GLOBAL",2)~
				REPLACE_TEXTUALLY ~Global("ENDOFBG1","GLOBAL",2)[%newline%]*Global("ENDOFBG1","GLOBAL",2)~ ~Global("ENDOFBG1","GLOBAL",2)~
			END
		EXTEND_TOP ~%BASH_FOR_RES%.BCS~ ~%BASH_FOR_FILESPEC%~
	END ELSE BEGIN
		COMPILE ~%BASH_FOR_FILESPEC%~
	END
END

//decompile all bcs
COPY + ~%patch_dir%/bcs~ ~%patch_dir%/bcs~
	~%patch_dir%/bcs_app~ ~%patch_dir%/bcs_app~
	DECOMPILE_BCS_TO_BAF
	REPLACE_TEXTUALLY ~\(Allegiance(.*,\)ANYONE)~ ~\10)~ //http://forums.gibberlings3.net/index.php?showtopic=12932

ACTION_FOR_EACH ~dir~ IN BCS BCS_APP BEGIN
	ACTION_BASH_FOR ~%patch_dir%/%dir%~ ~.*\.BCS~ BEGIN
		MOVE + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.BAF~
	END
END

//compile scripts
COMPILE ~%patch_dir%/bcs~
	~%patch_dir%/bcs_app~
	~EET/base/compile/baf~

//patching BG:EE scripts
INCLUDE ~EET/lib/bg1_BCS.tph~

//Combine transition.tbl
ACTION_IF (FILE_EXISTS ~%bgee_dir%/override/EET-transition.tbl~) BEGIN
	COPY + ~%bgee_dir%/override/EET-transition.tbl~ ~EET/temp~
END

ACTION_IF (FILE_EXISTS ~override/EET-transition.tbl~) BEGIN
	COPY ~override/EET-transition.tbl~ ~override~
		APPEND_FILE ~EET/tbl/EET-transition.tbl~
		PATCH_IF (FILE_EXISTS ~EET/temp/EET-transition.tbl~) BEGIN
			APPEND_FILE ~EET/temp/EET-transition.tbl~
		END
END ELSE BEGIN
    COPY ~EET/tbl/EET-transition.tbl~ ~override~
		PATCH_IF (FILE_EXISTS ~EET/temp/EET-transition.tbl~) BEGIN
			APPEND_FILE ~EET/temp/EET-transition.tbl~
		END
END

////////////////////////
// Items shattering
////////////////////////

<<<<<<<< EET-inlined/shattering.baf
IF
  OR(3)
    GlobalGT("ENDOFBG1","GLOBAL",0)
    GlobalGT("OHB_BP2_CAPTURE","GLOBAL",0)
    GlobalGT("BPINTRO","GLOBAL",0)
THEN
  RESPONSE #100
	DestroySelf()
END

IF
  CheckStatLT(LastSummonerOf(Myself),50,INTOXICATION)//In vanilla BG1 weapons break almost every hit when character is drunk
THEN
  RESPONSE #%response1%
	DestroySelf()
  RESPONSE #%response2%
	Continue()
END
>>>>>>>>

<<<<<<<< EET-inlined/shattering-eb.baf
IF
  HasItemEquiped%real%("%item%",LastSummonerOf(Myself))
THEN
  RESPONSE #100
    DisplayStringHead(LastSummonerOf(Myself),#%string%)
	ActionOverride(LastSummonerOf(Myself),TransformItem("%item%","%item_broken%"))
	DestroySelf()
END
>>>>>>>>

//Weapons
OUTER_SPRINT real ~Real~
OUTER_SPRINT item_broken ~MISC56_~
OUTER_SET string = 14158
OUTER_SET response1 = 95
OUTER_SET response2 = 5 //0.5% chance
COPY - ~EET-inlined/shattering.baf~ ~EET-inlined/K#SHATWE.baf~
COMPILE ~EET-inlined/K#SHATWE.baf~ EVALUATE_BUFFER
ACTION_FOR_EACH item IN AX1H01 AX1H04 BLUN02 BLUN04 BLUN06 BLUN08 /*CATTAC1*/ DAGG01 DAGG06 DAGG07 /*DAGGSHIT*/ /*FLIND1*/ HALB01 HAMM01 SPER01 SW1H01 /*COMPS01*/ SW1H04 SW1H07 SW1H12 SW1H17 SW1H20 SW1H21 SW1H43 SW1H46 SW1H48 SW2H01 /*SW2H01B*/ /*TASLOIIL*/ /*XVARTIL*/ BEGIN
	EXTEND_BOTTOM ~K#SHATWE.BCS~ ~EET-inlined/shattering-eb.baf~ EVALUATE_BUFFER
END
OUTER_SPRINT real ~~

//Shields
OUTER_SPRINT item_broken ~MISC57~
OUTER_SET string = 14180
OUTER_SET response1 = 98
OUTER_SET response2 = 2 //0.2% chance
COPY - ~EET-inlined/shattering.baf~ ~EET-inlined/K#SHATSH.baf~
COMPILE ~EET-inlined/K#SHATSH.baf~ EVALUATE_BUFFER
ACTION_FOR_EACH item IN SHLD01 SHLD03 SHLD05 SHLD08 SHLD09 SHLD10 SHLD11 SHLD12 SHLD13 SHLD14 SHLD15 SHLD16 SHLD18 BEGIN
	EXTEND_BOTTOM ~K#SHATSH.BCS~ ~EET-inlined/shattering-eb.baf~ EVALUATE_BUFFER
END

//Armors
OUTER_SPRINT item_broken ~MISC58~
OUTER_SET string = 15817
OUTER_SET response1 = 98
OUTER_SET response2 = 2 //0.2% chance
COPY - ~EET-inlined/shattering.baf~ ~EET-inlined/K#SHATAR.baf~
COMPILE ~EET-inlined/K#SHATAR.baf~ EVALUATE_BUFFER
ACTION_FOR_EACH item IN CHAN01 CHAN04 PLAT01 PLAT04 BEGIN
	EXTEND_BOTTOM ~K#SHATAR.BCS~ ~EET-inlined/shattering-eb.baf~ EVALUATE_BUFFER
END

//Helmets
OUTER_SPRINT item_broken ~MISC59~
OUTER_SET string = 15818
OUTER_SET response1 = 99
OUTER_SET response2 = 1 //0.1% chance
COPY - ~EET-inlined/shattering.baf~ ~EET-inlined/K#SHATHE.baf~
COMPILE ~EET-inlined/K#SHATHE.baf~ EVALUATE_BUFFER
ACTION_FOR_EACH item IN HELM01 HELM08 HELM09 HELM10 HELM11 HELM12 HELM13 HELM15 BEGIN
	EXTEND_BOTTOM ~K#SHATHE.BCS~ ~EET-inlined/shattering-eb.baf~ EVALUATE_BUFFER
END

////////////////////////
// K#TELBGT.BCS
////////////////////////

//NPCs outside party
<<<<<<<< EET-inlined/K#TELBGT-et.baf
IF
  Global("K#DoOnce%DV%","LOCALS",0)
  Global("%DV%PartyBG1","GLOBAL",1)
  !InPartyAllowDead("%DV%")
  !InMyArea("%DV%")
THEN
  RESPONSE #100
    //MoveGlobal("BG0125","%DV%",[1000.1000])
	MoveGlobalObjectOffScreen("%DV%",Player1)
    Continue()
END
>>>>>>>>

//NPCs in party
<<<<<<<< EET-inlined/K#TELBGT-et2.baf
IF
  Global("K#DoOnce%DV%","LOCALS",0)
  Global("%DV%PartyBG1","GLOBAL",1)
  InPartyAllowDead("%DV%")
THEN
  RESPONSE #100
    //MoveGlobal("BG0125","%DV%",[1000.1000])
    //MoveGlobalObjectOffScreen("%DV%",Player1)
    ActionOverride("%DV%",LeaveParty())
    Continue()
END
>>>>>>>>

//NPCs without MoveBetweenAreas transition
<<<<<<<< EET-inlined/K#TELBGT-et3.baf
IF
  Global("K#DoOnce%DV%","LOCALS",0)
  Global("%DV%PartyBG1","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("K#DoOnce%DV%","LOCALS",1)
    ActionOverride("%DV%",DropInventory())
    ActionOverride("%DV%",DestroySelf())
    Continue()
END
>>>>>>>>

COPY - ~EET/temp/array/PDIALOG.2DA~ ~EET-inlined~
	COUNT_2DA_ROWS 8 "cntrow"
	FOR (cnt = 0; cnt < ("%cntrow%" - 1) ; cnt = cnt + 1) BEGIN //-1 due to dummy entry set via bg2_2DA.tph at the end
		SET noupdate = 0
		READ_2DA_ENTRY cnt 0 8 "DV"
		PATCH_PRINT ~Adding BG2 transition code for %DV%~
		INNER_ACTION BEGIN
			COPY_EXISTING - ~EET-transition.tbl~ ~override~
				COUNT_2DA_ROWS 3 "cntrow2"
				FOR (cnt2 = 0; cnt2 < "%cntrow2%" ; cnt2 = cnt2 + 1) BEGIN
					READ_2DA_ENTRY cnt2 0 3 "compareWith"
					PATCH_IF (("%DV%" STRING_EQUAL_CASE "%compareWith%")=1) BEGIN
						SET noupdate = 1
					END
				END
			ACTION_IF ("%noupdate%" = 0) BEGIN //only if no full transition is available
				EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et3.baf~ EVALUATE_BUFFER
				EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et2.baf~ EVALUATE_BUFFER
				EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et.baf~ EVALUATE_BUFFER
			END
		END
	END

//here goes EET-inlined/K#TELBGT-et.baf

//NPCs that should be resurrected if possible
<<<<<<<< EET-inlined/K#TELBGT-et4.baf
IF
  Global("K#DoOnce%DV%Res","LOCALS",0)
  Global("%DV%PartyBG1","GLOBAL",1)
  Dead("%DV%")
  NotStateCheck("%DV%",STATE_EXPLODING_DEATH)
  NotStateCheck("%DV%",STATE_STONE_DEATH)
  NotStateCheck("%DV%",STATE_FROZEN_DEATH)
  //NotStateCheck("%DV%",STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobal("K#DoOnce%DV%Res","LOCALS",1)
    ActionOverride("%DV%",ApplySpellRES("K#LIVE",Myself))
    SetGlobal("SPRITE_IS_DEAD%DV%","GLOBAL",0)
    //Continue() //1 frame delay
END
>>>>>>>>

//here goes EET-inlined/K#TELBGT-et2.baf

//NPCs with MoveBetweenAreas (still dead)
<<<<<<<< EET-inlined/K#TELBGT-et5.baf
IF
  Global("K#DoOnce%DV%","LOCALS",0)
  Global("%DV%PartyBG1","GLOBAL",1)
  OR(5)
    Dead("%DV%")
    StateCheck("%DV%",STATE_EXPLODING_DEATH)
    StateCheck("%DV%",STATE_STONE_DEATH)
    StateCheck("%DV%",STATE_FROZEN_DEATH)
    StateCheck("%DV%",STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobal("K#DoOnce%DV%","LOCALS",1)
    SetGlobal("%DV%PartyBG1","GLOBAL",0)
    ActionOverride("%DV%",DropInventory())
    ActionOverride("%DV%",DestroySelf())
    Continue()
END
>>>>>>>>

//NPCs with MoveBetweenAreas transition (not dead)
<<<<<<<< EET-inlined/K#TELBGT-et6.baf
IF
  Global("K#DoOnce%DV%","LOCALS",0)
  Global("%DV%PartyBG1","GLOBAL",1)
  !Dead("%DV%")
  NotStateCheck("%DV%",STATE_EXPLODING_DEATH)
  NotStateCheck("%DV%",STATE_STONE_DEATH)
  NotStateCheck("%DV%",STATE_FROZEN_DEATH)
  NotStateCheck("%DV%",STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobal("K#DoOnce%DV%","LOCALS",1)
    ActionOverride("%DV%",DropInventory())
    ActionOverride("%DV%",ApplySpellRES("K#DISPEL",Myself))
    ActionOverride("%DV%",ChangeAIScript("",DEFAULT))
	ActionOverride("%DV%",ChangeEnemyAlly(Myself,NEUTRAL))
    ActionOverride("%DV%",MoveBetweenAreas(%location%))
    Continue()
END
>>>>>>>>

COPY_EXISTING - ~EET-transition.tbl~ ~override~
	COUNT_2DA_ROWS 3 "cntrow"
	FOR (cnt = 0; cnt < "%cntrow%" ; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 3 "DV"
		READ_2DA_ENTRY cnt 1 3 "location"
		READ_2DA_ENTRY cnt 2 3 "resurrect"
		PATCH_PRINT ~Transition added for %DV% to area: %location% - resurrection: %resurrect%~
		INNER_ACTION BEGIN
			EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et6.baf~ EVALUATE_BUFFER
			EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et5.baf~ EVALUATE_BUFFER
			EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et2.baf~ EVALUATE_BUFFER
			//ACTION_IF ("%resurrect%" = 1) BEGIN
				EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et4.baf~ EVALUATE_BUFFER
			//END
			EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et.baf~ EVALUATE_BUFFER
		END
	END

//time pass and item variables
<<<<<<<< EET-inlined/K#TELBGT-et7.baf
IF
  Global("MeetOldenFriends","GLOBAL",0)
  PartyHasItem("MISC47") // Golden Pantaloons
THEN
  RESPONSE #100
    ActionOverride("K#TELBGT",TakePartyItem("MISC47")) // Golden Pantaloons
END

IF
  Global("K#DoOnceItemList","LOCALS",0)
THEN
  RESPONSE #100
	SetGlobal("K#DoOnceItemList","LOCALS",1)
    ActionOverride(Myself,TakeItemListPartyNum("IMPORT02",1))
    ActionOverride(Myself,TakeItemListPartyNum("IMPORT01",1))
    ActionOverride(Myself,TakeItemListPartyNum("IMPORT03",1))
    Continue()
END

IF
  Global("BG1DrizztItem","GLOBAL",0)
  OR(3)
    PartyHasItem("CHAN06") // Mithral Chain Mail +4
    PartyHasItem("SW1H15") // Icingdeath +3
    PartyHasItem("SW1H16") // Twinkle +3
THEN
  RESPONSE #100
    SetGlobal("BG1DrizztItem","GLOBAL",1)
    Continue()
END

IF
  Global("K#AdvanceTimeOneMonth","LOCALS",0)
  //GlobalLT("K#DaysPassed","GLOBAL",365)
THEN
  RESPONSE #100
    SetGlobal("K#AdvanceTimeOneMonth","LOCALS",1)
    AdvanceTime(ONE_MONTH)
    Continue()
END
>>>>>>>>

EXTEND_TOP ~K#TELBGT.BCS~ ~EET-inlined/K#TELBGT-et7.baf~

//EraseJournalEntry
COPY_EXISTING ~K#TELBGT.BCS~ ~override~
	DECOMPILE_BCS_TO_BAF //saves time with patching
COPY - ~EET/temp/array/BGEE.SQL~ ~EET/temp/array~
	REPLACE_TEXTUALLY ~,~ ~ ~
	COUNT_2DA_ROWS 4 "cntrow"
	FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 2 4 "col3"
		PATCH_IF (("%col3%" STRING_EQUAL_CASE "journals_quests")=1) BEGIN
			SET journals_quests_row = cnt
		END ELSE PATCH_IF (("%col3%" STRING_EQUAL_CASE "portraits")=1) BEGIN
			SET portraits_row = cnt
			SET cnt = "%cntrow%"
		END
	END
	FOR (cnt = "%portraits_row%" - 1; cnt > "%journals_quests_row%"; cnt = cnt - 1) BEGIN
		READ_2DA_ENTRY cnt 0 4 "col1"
		INNER_ACTION BEGIN
			COPY_EXISTING ~K#TELBGT.BCS~ ~override~
				REPLACE_TEXTUALLY ~Wait(3)~ ~EraseJournalEntry(@%col1%)
    Wait(3)~
		END
	END
COPY_EXISTING ~K#TELBGT.BCS~ ~override~
	COMPILE_BAF_TO_BCS

////////////////////////
// K#TELBG2.BCS
////////////////////////

// Skip BG1 to BG2
<<<<<<<< EET-inlined/K#TELBG2-et.baf
IF
  Global("K#GenSplState%spl_id%","GLOBAL",0)
  GlobalGT("K#GenSplLvl%spl_lvl%","GLOBAL",0)
  !Kit(Player1,%spl_kit%)
  NextTriggerObject(Player1)
  !HaveSpellRES("%spl_id%")
THEN
  RESPONSE #100
	ActionOverride(Player1,AddSpecialAbility("%spl_id%"))
    AddGlobals("K#GenSplLvl%spl_lvl%","K#GenSplGlobal-1")
	SetGlobal("K#GenSplState%spl_id%","GLOBAL",1)
END
>>>>>>>>

OUTER_SPRINT ~spl_list~ ~2DA%WNL%dummy%WNL%Name	Level	Primary#School	Opposing#School~
COPY_EXISTING - ~SPLAUTOP.2DA~ ~override~
	FOR (col = 9; col > 0; col = col - 1) BEGIN
		COUNT_2DA_ROWS 10 "cntrow"
		//FOR (row="%cntrow%"; row>0; row=row-1) BEGIN
		//FOR (row=0; row<"%cntrow%"; row=row+1) BEGIN
		WHILE ("%cntrow%" >= 0) BEGIN
			READ_2DA_ENTRY cntrow col 10 "spl_id"
			cntrow -= 1
			INNER_ACTION BEGIN
				ACTION_IF (FILE_EXISTS_IN_GAME ~%spl_id%.SPL~) BEGIN
					COPY_EXISTING ~%spl_id%.SPL~ ~override~
						READ_STRREF 0x8 "spl_name"
						//READ_BYTE 0x1e "spl_flag"// ELSE 0
						READ_BYTE 0x25 "spl_school" ELSE 0
						READ_LONG 0x34 "spl_lvl" ELSE 0
						PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "1")=1) BEGIN
							SPRINT ~spl_primschool~ ~ABJURER~
							SPRINT ~spl_opschool~ ~TRANSMUTER~
							SPRINT ~spl_kit~ ~MAGESCHOOL_%spl_opschool%~
						END ELSE PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "2")=1) BEGIN
							SPRINT ~spl_primschool~ ~CONJURER~
							SPRINT ~spl_opschool~ ~DIVINER~
							SPRINT ~spl_kit~ ~MAGESCHOOL_%spl_opschool%~
						END ELSE PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "3")=1) BEGIN
							SPRINT ~spl_primschool~ ~DIVINER~
							SPRINT ~spl_opschool~ ~CONJURER~
							SPRINT ~spl_kit~ ~MAGESCHOOL_%spl_opschool%~
						END ELSE PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "4")=1) BEGIN
							SPRINT ~spl_primschool~ ~ENCHANTER~
							SPRINT ~spl_opschool~ ~INVOKER~
							SPRINT ~spl_kit~ ~MAGESCHOOL_%spl_opschool%~
						END ELSE PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "5")=1) BEGIN
							SPRINT ~spl_primschool~ ~ILLUSIONIST~
							SPRINT ~spl_opschool~ ~NECROMANCER~
							SPRINT ~spl_kit~ ~MAGESCHOOL_%spl_opschool%~
						END ELSE PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "6")=1) BEGIN
							SPRINT ~spl_primschool~ ~INVOKER~
							SPRINT ~spl_opschool~ ~ENCHANTER~
							SPRINT ~spl_kit~ ~MAGESCHOOL_%spl_opschool%~
						END ELSE PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "7")=1) BEGIN
							SPRINT ~spl_primschool~ ~NECROMANCER~
							SPRINT ~spl_opschool~ ~ILLUSIONIST~
							SPRINT ~spl_kit~ ~MAGESCHOOL_%spl_opschool%~
						END ELSE PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "8")=1) BEGIN
							SPRINT ~spl_primschool~ ~TRANSMUTER~
							SPRINT ~spl_opschool~ ~ABJURER~
							SPRINT ~spl_kit~ ~MAGESCHOOL_%spl_opschool%~
						//END ELSE PATCH_IF (("%spl_school%" STRING_EQUAL_CASE "9")=1) BEGIN
						//	SPRINT ~spl_primschool~ ~GENERALIST~
						//	SPRINT ~spl_opschool~ ~~
						//	SPRINT ~spl_kit~ ~0~
						END ELSE BEGIN
							SPRINT ~spl_primschool~ ~~
							SPRINT ~spl_opschool~ ~~
							SPRINT ~spl_kit~ ~0~
						END
					BUT_ONLY
					OUTER_SPRINT ~spl_list~ ~%spl_list%%WNL%%spl_id%.SPL	%spl_name%	%spl_lvl%	%spl_primschool%	%spl_opschool%~
					EXTEND_TOP ~K#TELBG2.BCS~ ~EET-inlined/K#TELBG2-et.baf~ EVALUATE_BUFFER
				END
			END
		END
	END
BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EET_IDprimschool" BEGIN
	"SPWI723" => "ABJURER"
	"SPWI222" => "ABJURER"
	"SPWI124" => "INVOKER"
END
OUTER_SPRINT ~spl_kit~ ~WILDMAGE~
ACTION_PHP_EACH table_EET_IDprimschool AS spl_id => spl_primschool BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%spl_id%.SPL~) BEGIN
		COPY_EXISTING - ~%spl_id%.SPL~ ~override~
			READ_STRREF 0x8 "spl_name"
			READ_LONG 0x34 "spl_lvl" ELSE 0
		EXTEND_TOP ~K#TELBG2.BCS~ ~EET-inlined/K#TELBG2-et.baf~ EVALUATE_BUFFER
		OUTER_SPRINT ~spl_list~ ~%spl_list%%WNL%%spl_id%.SPL	%spl_name%	%spl_lvl%	%spl_primschool%#(Wildmage)	none~
	END
END

COPY_EXISTING ~K#TELBG2.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~!Kit(Player1,0)~ ~~
		REPLACE_TEXTUALLY ~!Kit(Player1,WILDMAGE)~ ~Kit(Player1,WILDMAGE)~
	END
BUT_ONLY

COPY + ~EET-inlined/blank.txt~ ~EET/temp/splautop_ref.2da~
APPEND_OUTER + ~EET/temp/splautop_ref.2da~ ~%spl_list%~
COPY + ~EET/temp/splautop_ref.2da~ ~EET/temp~
	REPLACE_TEXTUALLY ~ ~ ~#~
	PRETTY_PRINT_2DA
	REPLACE_TEXTUALLY ~#~ ~ ~
	PATCH_LOG ~%spl_list%~

<<<<<<<< EET-inlined/K#TELBG2-et2.baf
IF
  Global("K#AdvanceTimeOneYear","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("K#AdvanceTimeOneYear","LOCALS",1)
    AdvanceTime(ONE_YEAR)
    Continue()
END

IF
  Global("K#GenSplState","GLOBAL",0)
  Class(Player1,MAGE_ALL)
  OR(10)
    Kit(Player1,0)
    Kit(Player1,MAGESCHOOL_ABJURER)
    Kit(Player1,MAGESCHOOL_CONJURER)
    Kit(Player1,MAGESCHOOL_DIVINER)
    Kit(Player1,MAGESCHOOL_ENCHANTER)
    Kit(Player1,MAGESCHOOL_ILLUSIONIST)
    Kit(Player1,MAGESCHOOL_INVOKER)
    Kit(Player1,MAGESCHOOL_NECROMANCER)
    Kit(Player1,MAGESCHOOL_TRANSMUTER)
	Kit(Player1,WILDMAGE)
  XPLT(Player1,2500)
THEN
  RESPONSE #100
    ReallyForceSpellRES("K#REMSPL",Player1)
    SetGlobal("K#GenSplGlobal-1","GLOBAL",-1)
    SetGlobal("K#GenSplState","GLOBAL",1)
    SetGlobal("K#GenSplLvl1","GLOBAL",5)
	SetGlobal("K#GenSplLvl2","GLOBAL",4)
	SetGlobal("K#GenSplLvl3","GLOBAL",3)
	SetGlobal("K#GenSplLvl4","GLOBAL",2)
	Wait(2)
END

IF
  Global("K#GenSplState","GLOBAL",1)
  Class(Player1,MAGE_ALL)
  OR(9)
    Kit(Player1,MAGESCHOOL_ABJURER)
    Kit(Player1,MAGESCHOOL_CONJURER)
    Kit(Player1,MAGESCHOOL_DIVINER)
    Kit(Player1,MAGESCHOOL_ENCHANTER)
    Kit(Player1,MAGESCHOOL_ILLUSIONIST)
    Kit(Player1,MAGESCHOOL_INVOKER)
    Kit(Player1,MAGESCHOOL_NECROMANCER)
    Kit(Player1,MAGESCHOOL_TRANSMUTER)
	Kit(Player1,WILDMAGE)
  XPLT(Player1,2500)
THEN
  RESPONSE #100
    SetGlobal("K#GenSplState","GLOBAL",2)
    IncrementGlobal("K#GenSplLvl1","GLOBAL",1)
	IncrementGlobal("K#GenSplLvl2","GLOBAL",1)
	IncrementGlobal("K#GenSplLvl3","GLOBAL",1)
	IncrementGlobal("K#GenSplLvl4","GLOBAL",1)
END
>>>>>>>>

EXTEND_TOP ~K#TELBG2.BCS~ ~EET-inlined/K#TELBG2-et2.baf~

/////                                                  \\\\\
///// BS                                               \\\\\
/////                                                  \\\\\

COPY ~%bgee_dir%/scripts/ohfight1.bs~ ~scripts~

COPY ~EET/base/test/EET_test.baf~ ~scripts/EET_test.bs~
	COMPILE_BAF_TO_BCS

/////                                                  \\\\\
///// CHR                                              \\\\\
/////                                                  \\\\\

//BG2:EE BPARTY01-06.cre files has been converted to chr files, so they can be chosen instead of Black Pits 1 party
PRINT ~If you see UNIX error below this line than it's not really an error. No need to report it.~
MKDIR ~%USER_DIRECTORY%/characters~
COPY ~EET/base/chr~ ~%USER_DIRECTORY%/characters~

//BG:EE default CHR files
COPY ~EET/temp/chr~ ~%USER_DIRECTORY%/characters~
	FOR (index = 0; index < 99 ; index = index + 1) BEGIN
		SET "offset" = (0x108 + (0x4 * index))
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
		END
	END

/////                                                  \\\\\
///// CHU                                              \\\\\
/////                                                  \\\\\

COPY_EXISTING ~GUICHAP.CHU~ ~override~
	WRITE_ASCII 0x28 ~GUICHP1B~ #8
BUT_ONLY

/////                                                  \\\\\
///// CRE                                              \\\\\
/////                                                  \\\\\

OUTER_SPRINT ~log~ ~%slash%%slash%log file generated during EET installation~
COPY ~%patch_dir%/cre~ ~override~
	SPRINT ~log~ ~%log%%WNL%%WNL%COPY "%SOURCE_FILESPEC%" "override"~
	PATCH_FOR_EACH ~offset~ IN 0x8 0xc BEGIN //Long name, Short name (tooltip)
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
		END
	END
	FOR (cnt=0; cnt<99; cnt=cnt+1) BEGIN
		SET offset = (0xa4+0x4*cnt)
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
		END
	END
	READ_LONG 0x2c4 "fx_off"
	READ_LONG 0x2c8 "fx_cnt"
	FOR (cnt=0; cnt<"%fx_cnt%"; cnt=cnt+1) BEGIN
		READ_LONG ("%fx_off%"+0x108*cnt+0x8) "fx_type"
		PATCH_IF ("%fx_type%" = 103) //Change Name
		OR ("%fx_type%" = 139) //Display String
		OR ("%fx_type%" = 180) //Can't Use Item
		OR ("%fx_type%" = 206) //Protection from Spell
		OR ("%fx_type%" = 253) //Add Map Marker
		OR ("%fx_type%" = 254) //Remove Map Marker
		OR ("%fx_type%" = 267) //Protection from Display Specific String
		OR ("%fx_type%" = 290) //Change Title
		OR ("%fx_type%" = 330) //Float text
		OR ("%fx_type%" = 338) //Disable rest or save
		BEGIN
			SET offset = ("%fx_off%"+0x108*cnt+0x14)
			READ_LONG offset "StrRef"
			PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
				SET str_updated = 0
				PATCH_IF ("%fx_type%" = 267) /*OR ("%fx_type%" = 139)*/ BEGIN
					PHP_EACH "table_traify_strOldstrNew" AS "strOld" => "strNew" BEGIN
						PATCH_IF (StrRef = "%strOld_1%") OR (StrRef = "%strNew%") AND (str_updated = 0) BEGIN
							PATCH_IF ("%strOld_1%" = "%strNew%") OR (StrRef = "%strNew%") BEGIN
								//SAY offset ( AT "StrRef" )
								SPRINT ~log~ ~%log%%WNL%  %slash%%slash%SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%, %strOld%~
							END ELSE BEGIN
								WRITE_LONG offset "%strNew%"
								SPRINT ~log~ ~%log%%WNL%  WRITE_LONG %offset% %strNew% %slash%%slash%opcode=%fx_type%, %strOld%~
							END
							SET str_updated = 1
						END
					END
				END
				PATCH_IF (str_updated = 0) BEGIN
					SAY offset ( AT "StrRef" )
					SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%~
				END
			END
		END ELSE PATCH_IF ("%fx_type%" = 319) BEGIN //Item Usability
			SET offset = ("%fx_off%"+0x108*cnt+0x40)
			READ_LONG offset "StrRef"
			PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
				SAY offset ( AT "StrRef" )
				SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%~
			END
		END
	END
COPY + ~EET-inlined/blank.txt~ ~EET/temp/cre.tph~
	PATCH_LOG ~%log%~
APPEND_OUTER + ~EET/temp/cre.tph~ ~%log%~

COPY ~EET/base/cre~ ~override~

// patching BG:EE cre files
INCLUDE ~EET/lib/bg1_cre.tph~

// Find Familiar externalization
ACTION_FOR_EACH file IN FAMCAT_ FAMDUST_ FAMFAIR_ FAMFER_ FAMIMP_ FAMPSD_ FAMQUAS_ FAMRAB_ FAMCAT FAMDUST FAMFAIR FAMFER FAMIMP FAMPSD FAMQUAS FAMRAB FAMCAT25 FAMDUS25 FAMFAI25 FAMFER25 FAMIMP25 FAMPSD25 FAMQUA25 FAMRAB25 BEGIN
	COPY_EXISTING ~%file%.CRE~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode = 232 /*Cast Spell on Condition*/ parameter2 = 16 /*Target dies*/ STR_VAR resource = "K#FAMKIL" END
	BUT_ONLY
END

/////                                                  \\\\\
///// DLG                                              \\\\\
/////                                                  \\\\\

//Fluid State Compatibility

<<<<<<<< EET-inlined/FluidStates.tpa
//Dialogues always appended by EET (this file has been generated automatically during EET installation):
>>>>>>>>
COPY ~EET-inlined/FluidStates.tpa~ ~EET/FluidStates.tpa~

ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EET_BG1dlgBG2dlg" BEGIN
	"BDORN" => "BDORN"
	"BEDWIN" => "BEDWIN"
	"BIMOEN" => "BIMOEN2"
	"BJAHEI" => "BJAHEIR"
	"BMINSC" => "BMINSC"
	"BNEERA" => "BNEERA"
	"BRASAAD" => "BRASAAD"
	"BVICON" => "BVICONI"
END
ACTION_PHP_EACH table_EET_BG1dlgBG2dlg AS BG1dlg => BG2dlg BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%BG2dlg%.DLG~) BEGIN
		COPY_EXISTING - ~%BG2dlg%.DLG~ ~override~
			READ_LONG 0x8 BG1NPC_num_states
		APPEND_OUTER ~EET/FluidStates.tpa~ ~OUTER_SET bgt%BG1dlg%state0 = %BG1NPC_num_states%~
	END
END

COMPILE ~%patch_dir%/dlg~
	~EET/base/compile/d~

/////                                                  \\\\\
///// EFF                                              \\\\\
/////                                                  \\\\\

OUTER_SPRINT ~log~ ~%slash%%slash%log file generated during EET installation~
COPY ~%patch_dir%/eff~ ~override~
	SPRINT ~log~ ~%log%%WNL%%WNL%COPY "%SOURCE_FILESPEC%" "override"~
	READ_LONG 0x10 "fx_type"
	PATCH_IF ("%fx_type%" = 103) //Change Name
	OR ("%fx_type%" = 139) //Display String
	OR ("%fx_type%" = 180) //Can't Use Item
	OR ("%fx_type%" = 206) //Protection from Spell
	OR ("%fx_type%" = 253) //Add Map Marker
	OR ("%fx_type%" = 254) //Remove Map Marker
	OR ("%fx_type%" = 267) //Protection from Display Specific String
	OR ("%fx_type%" = 290) //Change Title
	OR ("%fx_type%" = 330) //Float text
	OR ("%fx_type%" = 338) //Disable rest or save
	BEGIN
		SET offset = 0x1c
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SET str_updated = 0
			PATCH_IF ("%fx_type%" = 267) /*OR ("%fx_type%" = 139)*/ BEGIN
				PHP_EACH "table_traify_strOldstrNew" AS "strOld" => "strNew" BEGIN
					PATCH_IF (StrRef = "%strOld_1%") OR (StrRef = "%strNew%") AND (str_updated = 0) BEGIN
						PATCH_IF ("%strOld_1%" = "%strNew%") OR (StrRef = "%strNew%") BEGIN
							//SAY offset ( AT "StrRef" )
							SPRINT ~log~ ~%log%%WNL%  %slash%%slash%SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%, %strOld%~
						END ELSE BEGIN
							WRITE_LONG offset "%strNew%"
							SPRINT ~log~ ~%log%%WNL%  WRITE_LONG %offset% %strNew% %slash%%slash%opcode=%fx_type%, %strOld%~
						END
						SET str_updated = 1
					END
				END
			END
			PATCH_IF (str_updated = 0) BEGIN
				SAY offset ( AT "StrRef" )
				SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%~
			END
		END
	END ELSE PATCH_IF ("%fx_type%" = 319) BEGIN //Item Usability
		SET offset = 0x48
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%~
		END
	END
COPY + ~EET-inlined/blank.txt~ ~EET/temp/eff.tph~
	PATCH_LOG ~%log%~
APPEND_OUTER + ~EET/temp/eff.tph~ ~%log%~

COPY ~EET/base/eff~ ~override~

/////                                                  \\\\\
///// GAM                                              \\\\\
/////                                                  \\\\\

OUTER_SPRINT ~gam~ ~2DA%WNL%dummy%WNL%CRE_NAME ORIENTATION AREA X Y~
COPY + ~%patch_dir%/BALDUR.GAM~ ~%patch_dir%~
	READ_LONG 0x30 npcs_off
	READ_LONG 0x34 npcs_cnt
	FOR (cnt=0; cnt<"%npcs_cnt%"; cnt=cnt+1) BEGIN
		SET offset = ("%npcs_off%"+0x160*cnt)
		READ_ASCII offset + 0xc "cre_name"
		READ_LONG offset + 0x14 "cre_orient"
		READ_ASCII offset + 0x18 "area_name"
		READ_SHORT offset + 0x20 "loc_x"
		READ_SHORT offset + 0x22 "loc_y"
		SPRINT ~gam~ ~%gam%%WNL%%cnt% %cre_name% %cre_orient% %area_name% %loc_x% %loc_y%~
	END
	PATCH_LOG ~%gam%~

COPY + ~EET-inlined/blank.txt~ ~EET/temp/gam.2da~
APPEND_OUTER + ~EET/temp/gam.2da~ ~%gam%~
COPY + ~EET/temp/gam.2da~ ~EET/temp~
	PRETTY_PRINT_2DA

COPY - ~EET/temp/gam.2da~ ~EET/temp~
	COUNT_2DA_ROWS 6 GAM_count
	READ_2DA_ENTRIES_NOW ~#GAM_patch_table~ 6
BUT_ONLY

COPY ~EET/base/BALDUR.GAM~ ~override~
	READ_LONG 0x20 "PC_offset"
	READ_LONG 0x28 "US_offset"
	READ_LONG 0x30 "npcs_off"
	READ_LONG 0x34 "npcs_cnt"
	READ_LONG 0x38 "glob_offset"
	READ_LONG 0x50 "jrnl_offset"
	READ_LONG 0x68 "famil_offset"
	READ_LONG 0x6c "locat_offset"
	READ_LONG 0x78 "US2_offset"

	FOR (cnt=0; cnt<"%GAM_count%"; cnt=cnt+1) BEGIN
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 1 "cre_name"
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 2 "cre_orient"
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 3 "area_name"
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 4 "loc_x"
		READ_2DA_ENTRY_FORMER ~#GAM_patch_table~ "cnt" 5 "loc_y"

		INSERT_BYTES "%npcs_off%" 0x160
		WRITE_ASCIIE ("%npcs_off%" + 0x0c) "%cre_name%" #8
		WRITE_ASCIIE ("%npcs_off%" + 0x18) "%area_name%" #8
		WRITE_SHORT ("%npcs_off%" + 0x20) "%loc_x%"
		WRITE_SHORT ("%npcs_off%" + 0x22) "%loc_y%"
		WRITE_SHORT ("%npcs_off%" + 0x8c) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0x8e) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0x90) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0x92) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0xb4) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0xb6) 0xffff
		WRITE_SHORT ("%npcs_off%" + 0xb8) 0xffff	  
	END

	PATCH_IF ("%cnt%" > 0) BEGIN	
		SET delta = 0x160 * cnt
		SET npcs_cnt = npcs_cnt + cnt
		WRITE_LONG 0x34 "%npcs_cnt%"

		PATCH_IF ("%PC_offset%">="%npcs_off%") BEGIN
			SET PC_offset = PC_offset + delta
			WRITE_LONG 0x20 "%PC_offset%"
		END
		PATCH_IF ("%US_offset%">="%npcs_off%") BEGIN
			SET US_offset = US_offset + delta
			WRITE_LONG 0x28 "%US_offset%"
		END
		PATCH_IF ("%glob_offset%">="%npcs_off%") BEGIN
			SET glob_offset = glob_offset + delta
			WRITE_LONG 0x38 "%glob_offset%"
		END
		PATCH_IF ("%jrnl_offset%">="%npcs_off%") BEGIN
			SET jrnl_offset = jrnl_offset + delta
			WRITE_LONG 0x50 "%jrnl_offset%"
		END
		PATCH_IF ("%famil_offset%">="%npcs_off%") BEGIN
			SET famil_offset = famil_offset + delta
			WRITE_LONG 0x68 "%famil_offset%"
		END
		PATCH_IF ("%locat_offset%">="%npcs_off%") BEGIN
			SET locat_offset = locat_offset + delta
			WRITE_LONG 0x6c "%locat_offset%"
		END
		PATCH_IF ("%US2_offset%">="%npcs_off%") BEGIN
			SET US2_offset = US2_offset + delta
			WRITE_LONG 0x78 "%US2_offset%"
		END
	END

/////                                                  \\\\\
///// GUI                                              \\\\\
/////                                                  \\\\\

COPY_EXISTING ~BGEE.GUI~ ~override~
	REPLACE_TEXTUALLY ~SELECT 450, 20, 120, 16~ ~SELECT 903, 18, 120, 16~ //move build version to the upper right corner
BUT_ONLY

COPY_LARGE ~EET/base/gui/DLCP1.bmp~ ~EET/temp/biff~
	~EET/base/gui/DLCV1.bmp~ ~EET/temp/biff~
	//~EET/base/gui/MAPICONS.bam~ ~EET/temp/biff~

ACTION_BASH_FOR ~EET/base/gui~ ~^.+\.mbc$~ BEGIN
	AT_NOW ~%tileconv% -e -j %jobs% -q %quality% -o "EET%os_slash%temp%os_slash%biff%os_slash%%BASH_FOR_RES%.mos" "EET%os_slash%base%os_slash%gui%os_slash%%BASH_FOR_FILE%" %bash_debug%~ EXACT
END
LAM bash_log

ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EET_bamIndex" BEGIN
	"BIGLOGO" => "0500"
	"TITLE" => "0501"
	"MAPICONS" => "10"
END
ACTION_PHP_EACH table_EET_bamIndex AS file => index BEGIN
	COPY ~EET/base/gui/%file%.bam~ ~%biff_dir%~
		LPF ~UPDATE_PVRZ_INDICES~ RET original_base_index new_base_index END
	ACTION_IF original_base_index >= 0 AND new_base_index >= 0 BEGIN
		ACTION_BASH_FOR ~EET/base/gui~ ~mos%index%.*\.pvrz~ BEGIN
			LAF ~INSTALL_PVRZ~ INT_VAR original_base_index new_base_index STR_VAR source_file = EVAL ~%BASH_FOR_FILESPEC%~ END
		END
	END ELSE BEGIN
		WARN ~Couldn't install PVRZ files~
	END
END

/////                                                  \\\\\
///// INI                                              \\\\\
/////                                                  \\\\\

COPY ~EET/base/ini~ ~override~

/////                                                  \\\\\
///// ITM                                              \\\\\
/////                                                  \\\\\

OUTER_SPRINT ~log~ ~%slash%%slash%log file generated during EET installation~
COPY ~%patch_dir%/itm~ ~override~
	SPRINT ~log~ ~%log%%WNL%%WNL%COPY "%SOURCE_FILESPEC%" "override"~
	PATCH_FOR_EACH ~offset~ IN 0x8 0xc 0x50 0x54 BEGIN //Name - Unidentified, Name - Identified, Description - Unidentified, Description - Identified
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
		END
	END
	READ_LONG 0x64 "abil_off"
	READ_SHORT 0x68 "abil_cnt"
	READ_LONG 0x6a "fx_off"
	READ_SHORT 0x70 "fx_cnt"
	FOR (cnt=0; cnt<"%abil_cnt%"; cnt=cnt+1) BEGIN
		READ_SHORT ("%abil_off%"+0x38*cnt+0x1e) "fx_num"
		SET fx_cnt += fx_num //gauge the total number of effects by summing global effects and the effects of each ability
	END
	FOR (cnt=0; cnt<"%fx_cnt%"; cnt=cnt+1) BEGIN
		READ_SHORT ("%fx_off%"+0x30*cnt) "fx_type"
		PATCH_IF ("%fx_type%" = 103) //Change Name
		OR ("%fx_type%" = 139) //Display String
		OR ("%fx_type%" = 180) //Can't Use Item
		OR ("%fx_type%" = 206) //Protection from Spell
		OR ("%fx_type%" = 253) //Add Map Marker
		OR ("%fx_type%" = 254) //Remove Map Marker
		OR ("%fx_type%" = 267) //Protection from Display Specific String
		OR ("%fx_type%" = 290) //Change Title
		OR ("%fx_type%" = 330) //Float text
		OR ("%fx_type%" = 338) //Disable rest or save
		BEGIN
			SET offset = ("%fx_off%"+0x30*cnt+0x4)
			READ_LONG offset "StrRef"
			PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
				SET str_updated = 0
				PATCH_IF ("%fx_type%" = 267) /*OR ("%fx_type%" = 139)*/ BEGIN
					PHP_EACH "table_traify_strOldstrNew" AS "strOld" => "strNew" BEGIN
						PATCH_IF (StrRef = "%strOld_1%") OR (StrRef = "%strNew%") AND (str_updated = 0) BEGIN
							PATCH_IF ("%strOld_1%" = "%strNew%") OR (StrRef = "%strNew%") BEGIN
								//SAY offset ( AT "StrRef" )
								SPRINT ~log~ ~%log%%WNL%  %slash%%slash%SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%, %strOld%~
							END ELSE BEGIN
								WRITE_LONG offset "%strNew%"
								SPRINT ~log~ ~%log%%WNL%  WRITE_LONG %offset% %strNew% %slash%%slash%opcode=%fx_type%, %strOld%~
							END
							SET str_updated = 1
						END
					END
				END
				PATCH_IF (str_updated = 0) BEGIN
					SAY offset ( AT "StrRef" )
					SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%~
				END
			END
		END ELSE PATCH_IF ("%fx_type%" = 319) BEGIN //Item Usability
			SET offset = ("%fx_off%"+0x30*cnt+0x2c)
			READ_LONG offset "StrRef"
			PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
				SAY offset ( AT "StrRef" )
				SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%~
			END
		END
	END
COPY + ~EET-inlined/blank.txt~ ~EET/temp/itm.tph~
	PATCH_LOG ~%log%~
APPEND_OUTER + ~EET/temp/itm.tph~ ~%log%~

COPY_EXISTING ~BPRNG1.ITM~ ~override~
	LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = "208" END //Minimum HP
BUT_ONLY

COPY ~EET/base/itm/K#TELBG2.ITM~ ~override~
	~EET/base/itm/K#TELTUT.ITM~ ~override~

/////                                                  \\\\\
///// MUS / ACM                                        \\\\\
/////                                                  \\\\\

COPY ~%bgee_dir%/music/bl1.mus~ ~music~
	~%bgee_dir%/music/bl2.mus~ ~music~
	~%bgee_dir%/music/bw1.mus~ ~music~
	~%bgee_dir%/music/cday1.mus~ ~music~
	~%bgee_dir%/music/cday2.mus~ ~music~
	~%bgee_dir%/music/chants.mus~ ~music~
	~%bgee_dir%/music/chapter.mus~ ~music~
	~%bgee_dir%/music/cnite.mus~ ~music~
	~%bgee_dir%/music/dream.mus~ ~music~
	~%bgee_dir%/music/dung1.mus~ ~music~
	~%bgee_dir%/music/dung2.mus~ ~music~
	~%bgee_dir%/music/dung3.mus~ ~music~
	~%bgee_dir%/music/fday.mus~ ~music~
	~%bgee_dir%/music/fnite.mus~ ~music~
	~%bgee_dir%/music/fort.mus~ ~music~
	~%bgee_dir%/music/fort2.mus~ ~music~
	~%bgee_dir%/music/inite.mus~ ~music~
	~%bgee_dir%/music/nadoys.mus~ ~music~
	~%bgee_dir%/music/ngoblin.mus~ ~music~
	~%bgee_dir%/music/oldthm.mus~ ~music~
	~%bgee_dir%/music/pday.mus~ ~music~
	~%bgee_dir%/music/pnite.mus~ ~music~
	~%bgee_dir%/music/rpeaks.mus~ ~music~
	~%bgee_dir%/music/tday1.mus~ ~music~
	~%bgee_dir%/music/tday2.mus~ ~music~
	~%bgee_dir%/music/temple.mus~ ~music~
	~%bgee_dir%/music/tnite.mus~ ~music~

COPY ~%bgee_dir%/music/bc1.mus~ ~music/bc3.mus~
	REPLACE_TEXTUALLY ~BC1~ ~BC3~
COPY ~%bgee_dir%/music/bc2.mus~ ~music/bc4.mus~
	REPLACE_TEXTUALLY ~BC2~ ~BC4~
COPY ~%bgee_dir%/music/bd1.mus~ ~music/bd4.mus~
	REPLACE_TEXTUALLY ~BD1~ ~BD4~
COPY ~%bgee_dir%/music/bd2.mus~ ~music/bd5.mus~
	REPLACE_TEXTUALLY ~BD2~ ~BD5~
COPY ~%bgee_dir%/music/bf1.mus~ ~music/bf3.mus~
	REPLACE_TEXTUALLY ~BF1~ ~BF3~
COPY ~%bgee_dir%/music/bf2.mus~ ~music/bf4.mus~
	REPLACE_TEXTUALLY ~BF2~ ~BF4~
COPY ~%bgee_dir%/music/bp1.mus~ ~music/bp3.mus~
	REPLACE_TEXTUALLY ~BP1~ ~BP3~
COPY ~%bgee_dir%/music/bp2.mus~ ~music/bp4.mus~
	REPLACE_TEXTUALLY ~BP2~ ~BP4~
COPY ~%bgee_dir%/music/theme.mus~ ~music/newthm.mus~
	REPLACE_TEXTUALLY ~THEME~ ~NEWTHM~

MKDIR ~music/bc3~
	~music/bc4~
	~music/bd4~
	~music/bd5~
	~music/bf3~
	~music/bf4~
	~music/bl1~
	~music/bl2~
	~music/bp3~
	~music/bp4~
	~music/bw1~
	~music/cday1~
	~music/cday2~
	~music/chants~
	~music/cnite~
	~music/dream~
	~music/dung1~
	~music/dung2~
	~music/dung3~
	~music/fday~
	~music/fnite~
	~music/fort~
	~music/fort2~
	~music/inite~
	~music/nadoys~
	~music/ngoblin~
	~music/oldthm~
	~music/pday~
	~music/pnite~
	~music/rpeaks~
	~music/tday1~
	~music/tday2~
	~music/temple~
	~music/newthm~
	~music/tnite~

COPY ~%bgee_dir%/music/bl1~ ~music/bl1~
	~%bgee_dir%/music/bl2~ ~music/bl2~
	~%bgee_dir%/music/bw1~ ~music/bw1~
	~%bgee_dir%/music/cday1~ ~music/cday1~
	~%bgee_dir%/music/cday2~ ~music/cday2~
	~%bgee_dir%/music/chants~ ~music/chants~
	~%bgee_dir%/music/cnite~ ~music/cnite~
	~%bgee_dir%/music/dream~ ~music/dream~
	~%bgee_dir%/music/dung1~ ~music/dung1~
	~%bgee_dir%/music/dung2~ ~music/dung2~
	~%bgee_dir%/music/dung3~ ~music/dung3~
	~%bgee_dir%/music/fday~ ~music/fday~
	~%bgee_dir%/music/fnite~ ~music/fnite~
	~%bgee_dir%/music/fort~ ~music/fort~
	~%bgee_dir%/music/fort2~ ~music/fort2~
	~%bgee_dir%/music/inite~ ~music/inite~
	~%bgee_dir%/music/nadoys~ ~music/nadoys~
	~%bgee_dir%/music/ngoblin~ ~music/ngoblin~
	~%bgee_dir%/music/oldthm~ ~music/oldthm~
	~%bgee_dir%/music/pday~ ~music/pday~
	~%bgee_dir%/music/pnite~ ~music/pnite~
	~%bgee_dir%/music/rpeaks~ ~music/rpeaks~
	~%bgee_dir%/music/tday1~ ~music/tday1~
	~%bgee_dir%/music/tday2~ ~music/tday2~
	~%bgee_dir%/music/temple~ ~music/temple~
	~%bgee_dir%/music/tnite~ ~music/tnite~

COPY ~%bgee_dir%/music/bc1/bc1a1.acm~ ~music/bc3/bc3a1.acm~
	~%bgee_dir%/music/bc1/bc1a2.acm~ ~music/bc3/bc3a2.acm~
	~%bgee_dir%/music/bc1/bc1a3.acm~ ~music/bc3/bc3a3.acm~
	~%bgee_dir%/music/bc1/bc1a4.acm~ ~music/bc3/bc3a4.acm~
	~%bgee_dir%/music/bc1/bc1b1.acm~ ~music/bc3/bc3b1.acm~
	~%bgee_dir%/music/bc1/bc1b2.acm~ ~music/bc3/bc3b2.acm~
	~%bgee_dir%/music/bc1/bc1b3.acm~ ~music/bc3/bc3b3.acm~
	~%bgee_dir%/music/bc1/bc1c1.acm~ ~music/bc3/bc3c1.acm~
	~%bgee_dir%/music/bc1/bc1d1.acm~ ~music/bc3/bc3d1.acm~
	~%bgee_dir%/music/bc1/bc1d1a.acm~ ~music/bc3/bc3d1a.acm~
	~%bgee_dir%/music/bc1/bc1d2.acm~ ~music/bc3/bc3d2.acm~
	~%bgee_dir%/music/bc1/bc1d2a.acm~ ~music/bc3/bc3d2a.acm~
	~%bgee_dir%/music/bc1/bc1d3.acm~ ~music/bc3/bc3d3.acm~
	~%bgee_dir%/music/bc1/bc1d4.acm~ ~music/bc3/bc3d4.acm~
	~%bgee_dir%/music/bc1/bc1e1.acm~ ~music/bc3/bc3e1.acm~
	~%bgee_dir%/music/bc1/bc1e2.acm~ ~music/bc3/bc3e2.acm~
	~%bgee_dir%/music/bc1/bc1e3.acm~ ~music/bc3/bc3e3.acm~
	~%bgee_dir%/music/bc1/bc1e4.acm~ ~music/bc3/bc3e4.acm~
	~%bgee_dir%/music/bc1/bc1enda1.acm~ ~music/bc3/bc3enda1.acm~
	~%bgee_dir%/music/bc1/bc1enda4.acm~ ~music/bc3/bc3enda4.acm~
	~%bgee_dir%/music/bc1/bc1endb1.acm~ ~music/bc3/bc3endb1.acm~
	~%bgee_dir%/music/bc1/bc1endb2.acm~ ~music/bc3/bc3endb2.acm~
	~%bgee_dir%/music/bc1/bc1endb3.acm~ ~music/bc3/bc3endb3.acm~
	~%bgee_dir%/music/bc1/bc1endc1.acm~ ~music/bc3/bc3endc1.acm~
	~%bgee_dir%/music/bc1/bc1endd1.acm~ ~music/bc3/bc3endd1.acm~
	~%bgee_dir%/music/bc1/bc1endd2.acm~ ~music/bc3/bc3endd2.acm~
	~%bgee_dir%/music/bc1/bc1endd4.acm~ ~music/bc3/bc3endd4.acm~
	~%bgee_dir%/music/bc1/bc1ende1.acm~ ~music/bc3/bc3ende1.acm~
	~%bgee_dir%/music/bc1/bc1ende2.acm~ ~music/bc3/bc3ende2.acm~
	~%bgee_dir%/music/bc1/bc1ende4.acm~ ~music/bc3/bc3ende4.acm~
	~%bgee_dir%/music/bc1/bc1endf2.acm~ ~music/bc3/bc3endf2.acm~
	~%bgee_dir%/music/bc1/bc1endf4.acm~ ~music/bc3/bc3endf4.acm~
	~%bgee_dir%/music/bc1/bc1endg2.acm~ ~music/bc3/bc3endg2.acm~
	~%bgee_dir%/music/bc1/bc1endg3.acm~ ~music/bc3/bc3endg3.acm~
	~%bgee_dir%/music/bc1/bc1endh2.acm~ ~music/bc3/bc3endh2.acm~
	~%bgee_dir%/music/bc1/bc1endh3.acm~ ~music/bc3/bc3endh3.acm~
	~%bgee_dir%/music/bc1/bc1endh4.acm~ ~music/bc3/bc3endh4.acm~
	~%bgee_dir%/music/bc1/bc1endi1.acm~ ~music/bc3/bc3endi1.acm~
	~%bgee_dir%/music/bc1/bc1f1.acm~ ~music/bc3/bc3f1.acm~
	~%bgee_dir%/music/bc1/bc1f2.acm~ ~music/bc3/bc3f2.acm~
	~%bgee_dir%/music/bc1/bc1f3.acm~ ~music/bc3/bc3f3.acm~
	~%bgee_dir%/music/bc1/bc1f4.acm~ ~music/bc3/bc3f4.acm~
	~%bgee_dir%/music/bc1/bc1g1.acm~ ~music/bc3/bc3g1.acm~
	~%bgee_dir%/music/bc1/bc1g2.acm~ ~music/bc3/bc3g2.acm~
	~%bgee_dir%/music/bc1/bc1g3.acm~ ~music/bc3/bc3g3.acm~
	~%bgee_dir%/music/bc1/bc1h1.acm~ ~music/bc3/bc3h1.acm~
	~%bgee_dir%/music/bc1/bc1h2.acm~ ~music/bc3/bc3h2.acm~
	~%bgee_dir%/music/bc1/bc1h3.acm~ ~music/bc3/bc3h3.acm~
	~%bgee_dir%/music/bc1/bc1h4.acm~ ~music/bc3/bc3h4.acm~
	~%bgee_dir%/music/bc1/bc1i1.acm~ ~music/bc3/bc3i1.acm~
	~%bgee_dir%/music/bc1/bc1i1a.acm~ ~music/bc3/bc3i1a.acm~
	~%bgee_dir%/music/bc1/bc1i2a.acm~ ~music/bc3/bc3i2a.acm~
	
COPY ~%bgee_dir%/music/bc2/bc2a1.acm~ ~music/bc4/bc4a1.acm~
	~%bgee_dir%/music/bc2/bc2a2.acm~ ~music/bc4/bc4a2.acm~
	~%bgee_dir%/music/bc2/bc2a3.acm~ ~music/bc4/bc4a3.acm~
	~%bgee_dir%/music/bc2/bc2b1.acm~ ~music/bc4/bc4b1.acm~
	~%bgee_dir%/music/bc2/bc2b2.acm~ ~music/bc4/bc4b2.acm~
	~%bgee_dir%/music/bc2/bc2c1.acm~ ~music/bc4/bc4c1.acm~
	~%bgee_dir%/music/bc2/bc2c2.acm~ ~music/bc4/bc4c2.acm~
	~%bgee_dir%/music/bc2/bc2d1.acm~ ~music/bc4/bc4d1.acm~
	~%bgee_dir%/music/bc2/bc2d2.acm~ ~music/bc4/bc4d2.acm~
	~%bgee_dir%/music/bc2/bc2e1.acm~ ~music/bc4/bc4e1.acm~
	~%bgee_dir%/music/bc2/bc2e2.acm~ ~music/bc4/bc4e2.acm~
	~%bgee_dir%/music/bc2/bc2enda.acm~ ~music/bc4/bc4enda.acm~
	~%bgee_dir%/music/bc2/bc2endb.acm~ ~music/bc4/bc4endb.acm~
	~%bgee_dir%/music/bc2/bc2endc.acm~ ~music/bc4/bc4endc.acm~
	~%bgee_dir%/music/bc2/bc2f1.acm~ ~music/bc4/bc4f1.acm~
	~%bgee_dir%/music/bc2/bc2f2.acm~ ~music/bc4/bc4f2.acm~
	~%bgee_dir%/music/bc2/bc2g1.acm~ ~music/bc4/bc4g1.acm~
	~%bgee_dir%/music/bc2/bc2g2.acm~ ~music/bc4/bc4g2.acm~
	~%bgee_dir%/music/bc2/bc2g3.acm~ ~music/bc4/bc4g3.acm~
	~%bgee_dir%/music/bc2/bc2h1.acm~ ~music/bc4/bc4h1.acm~
	~%bgee_dir%/music/bc2/bc2h2.acm~ ~music/bc4/bc4h2.acm~
	~%bgee_dir%/music/bc2/bc2h3.acm~ ~music/bc4/bc4h3.acm~
	~%bgee_dir%/music/bc2/bc2h4.acm~ ~music/bc4/bc4h4.acm~
	~%bgee_dir%/music/bc2/bc2i1.acm~ ~music/bc4/bc4i1.acm~
	~%bgee_dir%/music/bc2/bc2i2.acm~ ~music/bc4/bc4i2.acm~
	~%bgee_dir%/music/bc2/bc2i3.acm~ ~music/bc4/bc4i3.acm~
	~%bgee_dir%/music/bc2/bc2i4.acm~ ~music/bc4/bc4i4.acm~
	~%bgee_dir%/music/bc2/bc2j1.acm~ ~music/bc4/bc4j1.acm~
	~%bgee_dir%/music/bc2/bc2j2.acm~ ~music/bc4/bc4j2.acm~
	~%bgee_dir%/music/bc2/bc2j3.acm~ ~music/bc4/bc4j3.acm~
	~%bgee_dir%/music/bc2/bc2j4.acm~ ~music/bc4/bc4j4.acm~
	~%bgee_dir%/music/bc2/bc2k1.acm~ ~music/bc4/bc4k1.acm~
	~%bgee_dir%/music/bc2/bc2k2.acm~ ~music/bc4/bc4k2.acm~
	~%bgee_dir%/music/bc2/bc2k3.acm~ ~music/bc4/bc4k3.acm~
	~%bgee_dir%/music/bc2/bc2xa1.acm~ ~music/bc4/bc4xa1.acm~
	
COPY ~%bgee_dir%/music/bd1/bd1a1.acm~ ~music/bd4/bd4a1.acm~
	~%bgee_dir%/music/bd1/bd1a2.acm~ ~music/bd4/bd4a2.acm~
	~%bgee_dir%/music/bd1/bd1b1.acm~ ~music/bd4/bd4b1.acm~
	~%bgee_dir%/music/bd1/bd1b2.acm~ ~music/bd4/bd4b2.acm~
	~%bgee_dir%/music/bd1/bd1b3.acm~ ~music/bd4/bd4b3.acm~
	~%bgee_dir%/music/bd1/bd1b4.acm~ ~music/bd4/bd4b4.acm~
	~%bgee_dir%/music/bd1/bd1c1.acm~ ~music/bd4/bd4c1.acm~
	~%bgee_dir%/music/bd1/bd1c2.acm~ ~music/bd4/bd4c2.acm~
	~%bgee_dir%/music/bd1/bd1d1.acm~ ~music/bd4/bd4d1.acm~
	~%bgee_dir%/music/bd1/bd1d2.acm~ ~music/bd4/bd4d2.acm~
	~%bgee_dir%/music/bd1/bd1d3.acm~ ~music/bd4/bd4d3.acm~
	~%bgee_dir%/music/bd1/bd1e1.acm~ ~music/bd4/bd4e1.acm~
	~%bgee_dir%/music/bd1/bd1e2.acm~ ~music/bd4/bd4e2.acm~
	~%bgee_dir%/music/bd1/bd1e3.acm~ ~music/bd4/bd4e3.acm~
	~%bgee_dir%/music/bd1/bd1e4.acm~ ~music/bd4/bd4e4.acm~
	~%bgee_dir%/music/bd1/bd1enda.acm~ ~music/bd4/bd4enda.acm~
	~%bgee_dir%/music/bd1/bd1endb.acm~ ~music/bd4/bd4endb.acm~
	~%bgee_dir%/music/bd1/bd1endc.acm~ ~music/bd4/bd4endc.acm~
	~%bgee_dir%/music/bd1/bd1f1.acm~ ~music/bd4/bd4f1.acm~
	~%bgee_dir%/music/bd1/bd1f2.acm~ ~music/bd4/bd4f2.acm~
	~%bgee_dir%/music/bd1/bd1f3.acm~ ~music/bd4/bd4f3.acm~
	~%bgee_dir%/music/bd1/bd1g1.acm~ ~music/bd4/bd4g1.acm~
	~%bgee_dir%/music/bd1/bd1g2.acm~ ~music/bd4/bd4g2.acm~
	~%bgee_dir%/music/bd1/bd1g3a.acm~ ~music/bd4/bd4g3a.acm~
	~%bgee_dir%/music/bd1/bd1g4a.acm~ ~music/bd4/bd4g4a.acm~
	~%bgee_dir%/music/bd1/bd1h1.acm~ ~music/bd4/bd4h1.acm~
	~%bgee_dir%/music/bd1/bd1h2.acm~ ~music/bd4/bd4h2.acm~
	~%bgee_dir%/music/bd1/bd1h3.acm~ ~music/bd4/bd4h3.acm~
	~%bgee_dir%/music/bd1/bd1i12.acm~ ~music/bd4/bd4i12.acm~
	~%bgee_dir%/music/bd1/bd1xa1.acm~ ~music/bd4/bd4xa1.acm~
	
COPY ~%bgee_dir%/music/bd2/bd2a1.acm~ ~music/bd5/bd5a1.acm~
	~%bgee_dir%/music/bd2/bd2a2.acm~ ~music/bd5/bd5a2.acm~
	~%bgee_dir%/music/bd2/bd2a3.acm~ ~music/bd5/bd5a3.acm~
	~%bgee_dir%/music/bd2/bd2b1.acm~ ~music/bd5/bd5b1.acm~
	~%bgee_dir%/music/bd2/bd2b2.acm~ ~music/bd5/bd5b2.acm~
	~%bgee_dir%/music/bd2/bd2b2b.acm~ ~music/bd5/bd5b2b.acm~
	~%bgee_dir%/music/bd2/bd2c1.acm~ ~music/bd5/bd5c1.acm~
	~%bgee_dir%/music/bd2/bd2c2.acm~ ~music/bd5/bd5c2.acm~
	~%bgee_dir%/music/bd2/bd2c3.acm~ ~music/bd5/bd5c3.acm~
	~%bgee_dir%/music/bd2/bd2c4.acm~ ~music/bd5/bd5c4.acm~
	~%bgee_dir%/music/bd2/bd2d1.acm~ ~music/bd5/bd5d1.acm~
	~%bgee_dir%/music/bd2/bd2d2.acm~ ~music/bd5/bd5d2.acm~
	~%bgee_dir%/music/bd2/bd2d3.acm~ ~music/bd5/bd5d3.acm~
	~%bgee_dir%/music/bd2/bd2d4.acm~ ~music/bd5/bd5d4.acm~
	~%bgee_dir%/music/bd2/bd2e1.acm~ ~music/bd5/bd5e1.acm~
	~%bgee_dir%/music/bd2/bd2e2.acm~ ~music/bd5/bd5e2.acm~
	~%bgee_dir%/music/bd2/bd2e3.acm~ ~music/bd5/bd5e3.acm~
	~%bgee_dir%/music/bd2/bd2e4.acm~ ~music/bd5/bd5e4.acm~
	~%bgee_dir%/music/bd2/bd2enda1.acm~ ~music/bd5/bd5enda1.acm~
	~%bgee_dir%/music/bd2/bd2enda2.acm~ ~music/bd5/bd5enda2.acm~
	~%bgee_dir%/music/bd2/bd2endb1.acm~ ~music/bd5/bd5endb1.acm~
	~%bgee_dir%/music/bd2/bd2endb2.acm~ ~music/bd5/bd5endb2.acm~
	~%bgee_dir%/music/bd2/bd2endd1.acm~ ~music/bd5/bd5endd1.acm~
	~%bgee_dir%/music/bd2/bd2ende1.acm~ ~music/bd5/bd5ende1.acm~
	~%bgee_dir%/music/bd2/bd2endg3.acm~ ~music/bd5/bd5endg3.acm~
	~%bgee_dir%/music/bd2/bd2f1.acm~ ~music/bd5/bd5f1.acm~
	~%bgee_dir%/music/bd2/bd2f2.acm~ ~music/bd5/bd5f2.acm~
	~%bgee_dir%/music/bd2/bd2f3.acm~ ~music/bd5/bd5f3.acm~
	~%bgee_dir%/music/bd2/bd2f4.acm~ ~music/bd5/bd5f4.acm~
	~%bgee_dir%/music/bd2/bd2g1a.acm~ ~music/bd5/bd5g1a.acm~
	~%bgee_dir%/music/bd2/bd2g2b.acm~ ~music/bd5/bd5g2b.acm~
	~%bgee_dir%/music/bd2/bd2g3.acm~ ~music/bd5/bd5g3.acm~
	~%bgee_dir%/music/bd2/bd2h2b.acm~ ~music/bd5/bd5h2b.acm~
	
COPY ~%bgee_dir%/music/bf1/bf1a1.acm~ ~music/bf3/bf3a1.acm~
	~%bgee_dir%/music/bf1/bf1a2.acm~ ~music/bf3/bf3a2.acm~
	~%bgee_dir%/music/bf1/bf1a3.acm~ ~music/bf3/bf3a3.acm~
	~%bgee_dir%/music/bf1/bf1b1.acm~ ~music/bf3/bf3b1.acm~
	~%bgee_dir%/music/bf1/bf1b2.acm~ ~music/bf3/bf3b2.acm~
	~%bgee_dir%/music/bf1/bf1c1.acm~ ~music/bf3/bf3c1.acm~
	~%bgee_dir%/music/bf1/bf1d1.acm~ ~music/bf3/bf3d1.acm~
	~%bgee_dir%/music/bf1/bf1d2.acm~ ~music/bf3/bf3d2.acm~
	~%bgee_dir%/music/bf1/bf1e1.acm~ ~music/bf3/bf3e1.acm~
	~%bgee_dir%/music/bf1/bf1e2.acm~ ~music/bf3/bf3e2.acm~
	~%bgee_dir%/music/bf1/bf1enda.acm~ ~music/bf3/bf3enda.acm~
	~%bgee_dir%/music/bf1/bf1endb.acm~ ~music/bf3/bf3endb.acm~
	~%bgee_dir%/music/bf1/bf1endc.acm~ ~music/bf3/bf3endc.acm~
	~%bgee_dir%/music/bf1/bf1endd.acm~ ~music/bf3/bf3endd.acm~
	~%bgee_dir%/music/bf1/bf1ende.acm~ ~music/bf3/bf3ende.acm~
	~%bgee_dir%/music/bf1/bf1endf.acm~ ~music/bf3/bf3endf.acm~
	~%bgee_dir%/music/bf1/bf1endg.acm~ ~music/bf3/bf3endg.acm~
	~%bgee_dir%/music/bf1/bf1f1.acm~ ~music/bf3/bf3f1.acm~
	~%bgee_dir%/music/bf1/bf1f2.acm~ ~music/bf3/bf3f2.acm~
	~%bgee_dir%/music/bf1/bf1f3.acm~ ~music/bf3/bf3f3.acm~
	~%bgee_dir%/music/bf1/bf1g1.acm~ ~music/bf3/bf3g1.acm~
	~%bgee_dir%/music/bf1/bf1g2.acm~ ~music/bf3/bf3g2.acm~
	~%bgee_dir%/music/bf1/bf1g3.acm~ ~music/bf3/bf3g3.acm~
	~%bgee_dir%/music/bf1/bf1h1.acm~ ~music/bf3/bf3h1.acm~
	~%bgee_dir%/music/bf1/bf1h2.acm~ ~music/bf3/bf3h2.acm~
	~%bgee_dir%/music/bf1/bf1i1.acm~ ~music/bf3/bf3i1.acm~
	~%bgee_dir%/music/bf1/bf1i2.acm~ ~music/bf3/bf3i2.acm~
	~%bgee_dir%/music/bf1/bf1i3.acm~ ~music/bf3/bf3i3.acm~
	~%bgee_dir%/music/bf1/bf1j1.acm~ ~music/bf3/bf3j1.acm~
	~%bgee_dir%/music/bf1/bf1j2.acm~ ~music/bf3/bf3j2.acm~
	~%bgee_dir%/music/bf1/bf1j3.acm~ ~music/bf3/bf3j3.acm~
	~%bgee_dir%/music/bf1/bf1j4.acm~ ~music/bf3/bf3j4.acm~
	~%bgee_dir%/music/bf1/bf1j5.acm~ ~music/bf3/bf3j5.acm~
	
COPY ~%bgee_dir%/music/bf2/bf2a1.acm~ ~music/bf4/bf4a1.acm~
	~%bgee_dir%/music/bf2/bf2a2.acm~ ~music/bf4/bf4a2.acm~
	~%bgee_dir%/music/bf2/bf2a3.acm~ ~music/bf4/bf4a3.acm~
	~%bgee_dir%/music/bf2/bf2a4.acm~ ~music/bf4/bf4a4.acm~
	~%bgee_dir%/music/bf2/bf2b1.acm~ ~music/bf4/bf4b1.acm~
	~%bgee_dir%/music/bf2/bf2b2.acm~ ~music/bf4/bf4b2.acm~
	~%bgee_dir%/music/bf2/bf2b3.acm~ ~music/bf4/bf4b3.acm~
	~%bgee_dir%/music/bf2/bf2b4.acm~ ~music/bf4/bf4b4.acm~
	~%bgee_dir%/music/bf2/bf2c1.acm~ ~music/bf4/bf4c1.acm~
	~%bgee_dir%/music/bf2/bf2c2.acm~ ~music/bf4/bf4c2.acm~
	~%bgee_dir%/music/bf2/bf2c3.acm~ ~music/bf4/bf4c3.acm~
	~%bgee_dir%/music/bf2/bf2c4.acm~ ~music/bf4/bf4c4.acm~
	~%bgee_dir%/music/bf2/bf2d1.acm~ ~music/bf4/bf4d1.acm~
	~%bgee_dir%/music/bf2/bf2d2.acm~ ~music/bf4/bf4d2.acm~
	~%bgee_dir%/music/bf2/bf2d3.acm~ ~music/bf4/bf4d3.acm~
	~%bgee_dir%/music/bf2/bf2d4.acm~ ~music/bf4/bf4d4.acm~
	~%bgee_dir%/music/bf2/bf2e1.acm~ ~music/bf4/bf4e1.acm~
	~%bgee_dir%/music/bf2/bf2e2.acm~ ~music/bf4/bf4e2.acm~
	~%bgee_dir%/music/bf2/bf2enda1.acm~ ~music/bf4/bf4enda1.acm~
	~%bgee_dir%/music/bf2/bf2enda2.acm~ ~music/bf4/bf4enda2.acm~
	~%bgee_dir%/music/bf2/bf2enda3.acm~ ~music/bf4/bf4enda3.acm~
	~%bgee_dir%/music/bf2/bf2enda4.acm~ ~music/bf4/bf4enda4.acm~
	~%bgee_dir%/music/bf2/bf2endc1.acm~ ~music/bf4/bf4endc1.acm~
	~%bgee_dir%/music/bf2/bf2endd1.acm~ ~music/bf4/bf4endd1.acm~
	~%bgee_dir%/music/bf2/bf2endd2.acm~ ~music/bf4/bf4endd2.acm~
	~%bgee_dir%/music/bf2/bf2endd4.acm~ ~music/bf4/bf4endd4.acm~
	~%bgee_dir%/music/bf2/bf2endf1.acm~ ~music/bf4/bf4endf1.acm~
	~%bgee_dir%/music/bf2/bf2endf2.acm~ ~music/bf4/bf4endf2.acm~
	~%bgee_dir%/music/bf2/bf2endf3.acm~ ~music/bf4/bf4endf3.acm~
	~%bgee_dir%/music/bf2/bf2endg3.acm~ ~music/bf4/bf4endg3.acm~
	~%bgee_dir%/music/bf2/bf2endg4.acm~ ~music/bf4/bf4endg4.acm~
	~%bgee_dir%/music/bf2/bf2endh4.acm~ ~music/bf4/bf4endh4.acm~
	~%bgee_dir%/music/bf2/bf2f1.acm~ ~music/bf4/bf4f1.acm~
	~%bgee_dir%/music/bf2/bf2f2.acm~ ~music/bf4/bf4f2.acm~
	~%bgee_dir%/music/bf2/bf2f3.acm~ ~music/bf4/bf4f3.acm~
	~%bgee_dir%/music/bf2/bf2f4.acm~ ~music/bf4/bf4f4.acm~
	~%bgee_dir%/music/bf2/bf2g1.acm~ ~music/bf4/bf4g1.acm~
	~%bgee_dir%/music/bf2/bf2g2.acm~ ~music/bf4/bf4g2.acm~
	~%bgee_dir%/music/bf2/bf2g3.acm~ ~music/bf4/bf4g3.acm~
	~%bgee_dir%/music/bf2/bf2g4.acm~ ~music/bf4/bf4g4.acm~
	~%bgee_dir%/music/bf2/bf2h1.acm~ ~music/bf4/bf4h1.acm~
	~%bgee_dir%/music/bf2/bf2h2.acm~ ~music/bf4/bf4h2.acm~
	~%bgee_dir%/music/bf2/bf2h3.acm~ ~music/bf4/bf4h3.acm~
	~%bgee_dir%/music/bf2/bf2h4.acm~ ~music/bf4/bf4h4.acm~
	
COPY ~%bgee_dir%/music/bp1/bp1a1.acm~ ~music/bp3/bp3a1.acm~
	~%bgee_dir%/music/bp1/bp1a2.acm~ ~music/bp3/bp3a2.acm~
	~%bgee_dir%/music/bp1/bp1a3.acm~ ~music/bp3/bp3a3.acm~
	~%bgee_dir%/music/bp1/bp1b1.acm~ ~music/bp3/bp3b1.acm~
	~%bgee_dir%/music/bp1/bp1b2.acm~ ~music/bp3/bp3b2.acm~
	~%bgee_dir%/music/bp1/bp1b3.acm~ ~music/bp3/bp3b3.acm~
	~%bgee_dir%/music/bp1/bp1c1.acm~ ~music/bp3/bp3c1.acm~
	~%bgee_dir%/music/bp1/bp1c2.acm~ ~music/bp3/bp3c2.acm~
	~%bgee_dir%/music/bp1/bp1d1.acm~ ~music/bp3/bp3d1.acm~
	~%bgee_dir%/music/bp1/bp1d2.acm~ ~music/bp3/bp3d2.acm~
	~%bgee_dir%/music/bp1/bp1d3.acm~ ~music/bp3/bp3d3.acm~
	~%bgee_dir%/music/bp1/bp1enda1.acm~ ~music/bp3/bp3enda1.acm~
	~%bgee_dir%/music/bp1/bp1enda2.acm~ ~music/bp3/bp3enda2.acm~
	~%bgee_dir%/music/bp1/bp1endb1.acm~ ~music/bp3/bp3endb1.acm~
	~%bgee_dir%/music/bp1/bp1endb2.acm~ ~music/bp3/bp3endb2.acm~
	~%bgee_dir%/music/bp1/bp1endc2.acm~ ~music/bp3/bp3endc2.acm~
	~%bgee_dir%/music/bp1/bp1endd1.acm~ ~music/bp3/bp3endd1.acm~
	~%bgee_dir%/music/bp1/bp1endd2.acm~ ~music/bp3/bp3endd2.acm~
	~%bgee_dir%/music/bp1/bp1endz1.acm~ ~music/bp3/bp3endz1.acm~
	
COPY ~%bgee_dir%/music/bp2/bp2a1.acm~ ~music/bp4/bp4a1.acm~
	~%bgee_dir%/music/bp2/bp2a2.acm~ ~music/bp4/bp4a2.acm~
	~%bgee_dir%/music/bp2/bp2a3.acm~ ~music/bp4/bp4a3.acm~
	~%bgee_dir%/music/bp2/bp2b1.acm~ ~music/bp4/bp4b1.acm~
	~%bgee_dir%/music/bp2/bp2b2.acm~ ~music/bp4/bp4b2.acm~
	~%bgee_dir%/music/bp2/bp2c1.acm~ ~music/bp4/bp4c1.acm~
	~%bgee_dir%/music/bp2/bp2c2.acm~ ~music/bp4/bp4c2.acm~
	~%bgee_dir%/music/bp2/bp2d1.acm~ ~music/bp4/bp4d1.acm~
	~%bgee_dir%/music/bp2/bp2d2.acm~ ~music/bp4/bp4d2.acm~
	~%bgee_dir%/music/bp2/bp2d3.acm~ ~music/bp4/bp4d3.acm~
	~%bgee_dir%/music/bp2/bp2d4.acm~ ~music/bp4/bp4d4.acm~
	~%bgee_dir%/music/bp2/bp2e1.acm~ ~music/bp4/bp4e1.acm~
	~%bgee_dir%/music/bp2/bp2e2.acm~ ~music/bp4/bp4e2.acm~
	~%bgee_dir%/music/bp2/bp2e3.acm~ ~music/bp4/bp4e3.acm~
	~%bgee_dir%/music/bp2/bp2e4.acm~ ~music/bp4/bp4e4.acm~
	~%bgee_dir%/music/bp2/bp2e5.acm~ ~music/bp4/bp4e5.acm~
	~%bgee_dir%/music/bp2/bp2enda1.acm~ ~music/bp4/bp4enda1.acm~
	~%bgee_dir%/music/bp2/bp2endc1.acm~ ~music/bp4/bp4endc1.acm~
	~%bgee_dir%/music/bp2/bp2endc2.acm~ ~music/bp4/bp4endc2.acm~
	~%bgee_dir%/music/bp2/bp2endd1.acm~ ~music/bp4/bp4endd1.acm~
	~%bgee_dir%/music/bp2/bp2endd2.acm~ ~music/bp4/bp4endd2.acm~
	~%bgee_dir%/music/bp2/bp2endd3.acm~ ~music/bp4/bp4endd3.acm~
	~%bgee_dir%/music/bp2/bp2endf1.acm~ ~music/bp4/bp4endf1.acm~
	~%bgee_dir%/music/bp2/bp2endf4.acm~ ~music/bp4/bp4endf4.acm~
	~%bgee_dir%/music/bp2/bp2endg4.acm~ ~music/bp4/bp4endg4.acm~
	~%bgee_dir%/music/bp2/bp2endh1.acm~ ~music/bp4/bp4endh1.acm~
	~%bgee_dir%/music/bp2/bp2endh3.acm~ ~music/bp4/bp4endh3.acm~
	~%bgee_dir%/music/bp2/bp2endz1.acm~ ~music/bp4/bp4endz1.acm~
	~%bgee_dir%/music/bp2/bp2f1.acm~ ~music/bp4/bp4f1.acm~
	~%bgee_dir%/music/bp2/bp2f2.acm~ ~music/bp4/bp4f2.acm~
	~%bgee_dir%/music/bp2/bp2f3.acm~ ~music/bp4/bp4f3.acm~
	~%bgee_dir%/music/bp2/bp2f4.acm~ ~music/bp4/bp4f4.acm~
	~%bgee_dir%/music/bp2/bp2g1.acm~ ~music/bp4/bp4g1.acm~
	~%bgee_dir%/music/bp2/bp2g2.acm~ ~music/bp4/bp4g2.acm~
	~%bgee_dir%/music/bp2/bp2g3.acm~ ~music/bp4/bp4g3.acm~
	~%bgee_dir%/music/bp2/bp2g4.acm~ ~music/bp4/bp4g4.acm~
	~%bgee_dir%/music/bp2/bp2h1.acm~ ~music/bp4/bp4h1.acm~
	~%bgee_dir%/music/bp2/bp2h2.acm~ ~music/bp4/bp4h2.acm~
	~%bgee_dir%/music/bp2/bp2h3.acm~ ~music/bp4/bp4h3.acm~
	~%bgee_dir%/music/bp2/bp2xa1.acm~ ~music/bp4/bp4xa1.acm~
	~%bgee_dir%/music/bp2/bp2z1.acm~ ~music/bp4/bp4z1.acm~

COPY ~%bgee_dir%/music/theme/themea.acm~ ~music/newthm/newthma.acm~

/////                                                  \\\\\
///// PRO                                              \\\\\
/////                                                  \\\\\

COPY ~EET/temp/pro~ ~override~

/////                                                  \\\\\
///// SPL                                              \\\\\
/////                                                  \\\\\

//Find Familiar related spells
COPY ~EET/base/spl/SPWI123.SPL~ ~override~
	~EET/base/spl/SPCL342.SPL~ ~override~
	~EET/base/spl/K#FAMKIL.SPL~ ~override~

ACTION_FOR_EACH var IN 4 6 9 12 24 BEGIN
	COPY ~EET/base/spl/K#CURE.SPL~ ~override/K#CURE%var%.SPL~
		WRITE_LONG 0x9e "%var%"
	//OUTER_SET value = ((var / 2) * 3)
	//COPY ~EET/base/spl/K#DMG.SPL~ ~override/K#DMG%value%.SPL~
	//	WRITE_LONG 0x9e "%value%"
END

//item shattering related spells
ACTION_FOR_EACH var IN WE AR SH HE BEGIN
	COPY ~EET/base/spl/K#SHAT.SPL~ ~override/K#SHAT%var%.SPL~
		WRITE_ASCIIE 0xae ~K#SHAT%var%~ #8
END

//auto spell removing
COPY ~EET/base/spl/K#REMBHA.SPL~ ~override~
	~EET/base/spl/K#REMSPL.SPL~ ~override~

OUTER_FOR (lvl = 1; lvl <= 4; lvl = lvl + 1) BEGIN
	OUTER_FOR (cnt = 1; cnt <= 9; cnt = cnt + 1) BEGIN
		ACTION_IF (FILE_EXISTS_IN_GAME ~SPWI%lvl%0%cnt%.SPL~) BEGIN
			COPY_EXISTING ~K#REMSPL.SPL~ ~override~
				LPF ADD_SPELL_EFFECT
					INT_VAR
					opcode        = 172 // remove spell
					target        =   2 // preset target
					power         =   1
					timing        =   1 // instant/perm
					resist_dispel =   2
					STR_VAR
					resource = EVAL "SPWI%lvl%0%cnt%"
				END
			BUT_ONLY
		END
	END
	OUTER_FOR (cnt = 10; cnt <= 25; cnt = cnt + 1) BEGIN
		ACTION_IF (FILE_EXISTS_IN_GAME ~SPWI%lvl%%cnt%.SPL~) BEGIN
			COPY_EXISTING ~K#REMSPL.SPL~ ~override~
				LPF ADD_SPELL_EFFECT
					INT_VAR
					opcode        = 172 // remove spell
					target        =   2 // preset target
					power         =   1
					timing        =   1 // instant/perm
					resist_dispel =   2
					STR_VAR
					resource = EVAL "SPWI%lvl%%cnt%"
				END
			BUT_ONLY
		END
	END
END

//transition related spells
COPY ~EET/base/spl/K#DEATH.SPL~ ~override~
	~EET/base/spl/K#DISPEL.SPL~ ~override~
	~EET/base/spl/K#FREEA.SPL~ ~override~
	~EET/base/spl/K#LIVE.SPL~ ~override~
	~EET/base/spl/K#SILENC.SPL~ ~override~

//assign BG2 portraits on transition
ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EET_portraitID" BEGIN
	"NEDWIN" => "EDW"
	"NIMOEN" => "IMO"
	"NJAHEIR" => "JAH"
	"NMINSC" => "MIN"
	"NVICON" => "VIC"
END
ACTION_PHP_EACH table_EET_portraitID AS portrait => ID BEGIN
	COPY ~EET/base/spl/K#PORTR.SPL~ ~override/K#POR%ID%.SPL~
	WRITE_ASCIIE ~0xae~ ~%portrait%S~ #8 // Small portrait
	WRITE_ASCIIE ~0xde~ ~%portrait%M~ #8 // Medium portrait
END

//ADD_SPELL "%patch_dir%/spl/SPCL101.SPL" 4 1 "DARK_GIFT"//appends also to spell selection screen during character generation which should not be the case
APPEND ~SPELL.IDS~ ~4101 DARK_GIFT~ UNLESS ~DARK_GIFT~
/*ACTION_IF (NOT FILE_EXISTS_IN_GAME ~add_spell.ids~) BEGIN
	COPY ~EET-inlined/blank.txt~ ~override/add_spell.ids~
END
APPEND ~add_spell.IDS~ ~4101 DARK_GIFT~ UNLESS ~DARK_GIFT~//information for the weidu unistall*/

OUTER_SPRINT ~log~ ~%slash%%slash%log file generated during EET installation~
COPY ~%patch_dir%/spl~ ~override~
	SPRINT ~log~ ~%log%%WNL%%WNL%COPY "%SOURCE_FILESPEC%" "override"~
	PATCH_FOR_EACH ~offset~ IN 0x8 0xc 0x50 0x54 BEGIN //Name - Unidentified, Name - Identified, Description - Unidentified, Description - Identified
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
		END
	END
	READ_LONG 0x64 "abil_off"
	READ_SHORT 0x68 "abil_cnt"
	READ_LONG 0x6a "fx_off"
	READ_SHORT 0x70 "fx_cnt"
	FOR (cnt=0; cnt<"%abil_cnt%"; cnt=cnt+1) BEGIN
		READ_SHORT ("%abil_off%"+0x28*cnt+0x1e) "fx_num"
		SET fx_cnt += fx_num //gauge the total number of effects by summing global effects and the effects of each ability
	END
	FOR (cnt=0; cnt<"%fx_cnt%"; cnt=cnt+1) BEGIN
		READ_SHORT ("%fx_off%"+0x30*cnt) "fx_type"
		PATCH_IF ("%fx_type%" = 103) //Change Name
		OR ("%fx_type%" = 139) //Display String
		OR ("%fx_type%" = 180) //Can't Use Item
		OR ("%fx_type%" = 206) //Protection from Spell
		OR ("%fx_type%" = 253) //Add Map Marker
		OR ("%fx_type%" = 254) //Remove Map Marker
		OR ("%fx_type%" = 267) //Protection from Display Specific String
		OR ("%fx_type%" = 290) //Change Title
		OR ("%fx_type%" = 330) //Float text
		OR ("%fx_type%" = 338) //Disable rest or save
		BEGIN
			SET offset = ("%fx_off%"+0x30*cnt+0x4)
			READ_LONG offset "StrRef"
			PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
				SET str_updated = 0
				PATCH_IF ("%fx_type%" = 267) /*OR ("%fx_type%" = 139)*/ BEGIN
					PHP_EACH "table_traify_strOldstrNew" AS "strOld" => "strNew" BEGIN
						PATCH_IF (StrRef = "%strOld_1%") OR (StrRef = "%strNew%") AND (str_updated = 0) BEGIN
							PATCH_IF ("%strOld_1%" = "%strNew%") OR (StrRef = "%strNew%") BEGIN
								//SAY offset ( AT "StrRef" )
								SPRINT ~log~ ~%log%%WNL%  %slash%%slash%SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%, %strOld%~
							END ELSE BEGIN
								WRITE_LONG offset "%strNew%"
								SPRINT ~log~ ~%log%%WNL%  WRITE_LONG %offset% %strNew% %slash%%slash%opcode=%fx_type%, %strOld%~
							END
							SET str_updated = 1
						END
					END
				END
				PATCH_IF (str_updated = 0) BEGIN
					SAY offset ( AT "StrRef" )
					SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%~
				END
			END
		END ELSE PATCH_IF ("%fx_type%" = 319) BEGIN //Item Usability
			SET offset = ("%fx_off%"+0x30*cnt+0x2c)
			READ_LONG offset "StrRef"
			PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
				SAY offset ( AT "StrRef" )
				SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef% %slash%%slash%opcode=%fx_type%~
			END
		END
	END
COPY + ~EET-inlined/blank.txt~ ~EET/temp/spl.tph~
	PATCH_LOG ~%log%~
APPEND_OUTER + ~EET/temp/spl.tph~ ~%log%~

/////                                                  \\\\\
///// STO                                              \\\\\
/////                                                  \\\\\

OUTER_SPRINT ~log~ ~%slash%%slash%log file generated during EET installation~
COPY ~%patch_dir%/sto~ ~override~
	SPRINT ~log~ ~%log%%WNL%%WNL%COPY "%SOURCE_FILESPEC%" "override"~
	SET offset = 0xc //Name
	READ_LONG offset "StrRef"
	PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
		SAY offset ( AT "StrRef" )
		SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
	END
	READ_LONG 0x4c "drink_off"
	READ_SHORT 0x50 "drink_cnt"
	FOR (cnt=0; cnt<"%drink_cnt%"; cnt=cnt+1) BEGIN
		SET offset = ("%drink_off%"+0x14*cnt+0x8) //Drink name
		READ_LONG offset "StrRef"
		PATCH_IF ((StrRef > 0) AND (StrRef <= StrRef_cutoff)) BEGIN
			SAY offset ( AT "StrRef" )
			SPRINT ~log~ ~%log%%WNL%  SAY %offset% @%StrRef%~
		END
	END
COPY + ~EET-inlined/blank.txt~ ~EET/temp/sto.tph~
	PATCH_LOG ~%log%~
APPEND_OUTER + ~EET/temp/sto.tph~ ~%log%~

/////                                                  \\\\\
///// VVC                                              \\\\\
/////                                                  \\\\\

COPY ~%patch_dir%/vvc~ ~override~

/////                                                  \\\\\
///// WAV                                              \\\\\
/////                                                  \\\\\

//Textscreen music
ACTION_DEFINE_ARRAY textscreenInputSpeArray BEGIN ~EET/temp/wav/INTRO01F.wav~ ~EET/temp/wav/INTRO02F.wav~ ~EET/temp/wav/INTRO03.wav~ ~EET/temp/wav/INTROADD.wav~ END
LAF ~textscreen_music~
	INT_VAR
	mus_fadeEnd = 3
	mus_delayEnd = 1
	spe_volumeFloat = 5 //speech volume set to 1.5
	STR_VAR
	input_spe_array = textscreenInputSpeArray
	input_mus = EVAL "EET%os_slash%base%os_slash%gui%os_slash%CDAY1A.ogg"
	output = EVAL "EET%os_slash%temp%os_slash%wav%os_slash%INTRO_.wav"
END
ACTION_CLEAR_ARRAY textscreenInputSpeArray

ACTION_DEFINE_ARRAY textscreenInputSpeArray BEGIN ~EET/temp/wav/CHAP00A.wav~ ~EET/temp/wav/CHAP00B.wav~ ~EET/temp/wav/CHAP00C.wav~ END
LAF ~textscreen_music~
	INT_VAR
	mus_fadeEnd = 3
	mus_delayEnd = 1
	spe_volumeFloat = 5 //speech volume set to 1.5
	STR_VAR
	input_spe_array = textscreenInputSpeArray
	input_mus = EVAL "EET%os_slash%base%os_slash%gui%os_slash%DREAMA.ogg"
	output = EVAL "EET%os_slash%temp%os_slash%wav%os_slash%CHAP00_.wav"
END
ACTION_CLEAR_ARRAY textscreenInputSpeArray

//other chapters and dreams
ACTION_FOR_EACH file IN CHAPTER1 CHAPTER2 CHAPTER3 CHAPTER4 CHAPTER5 CHAPTER6 DREAM1G DREAM1E DREAM2G DREAM2E DREAM3G DREAM3E DREAM4G DREAM4E DREAM5G DREAM5E DREAM6G DREAM6E BEGIN
	ACTION_DEFINE_ARRAY textscreenInputSpeArray BEGIN ~EET/temp/wav/%file%.wav~ END
	LAF ~textscreen_music~
		INT_VAR
		mus_fadeEnd = 3
		mus_delayEnd = 1
		spe_volumeFloat = 5 //speech volume set to 1.5
		STR_VAR
		input_spe_array = textscreenInputSpeArray
		input_mus = EVAL "EET%os_slash%base%os_slash%gui%os_slash%DREAMA.ogg"
		output = EVAL "EET%os_slash%temp%os_slash%wav%os_slash%%file%.wav"
	END
	ACTION_CLEAR_ARRAY textscreenInputSpeArray
END

// Soundsets
// copy only if soundset is available in BG:EE (English and German have 2 new DLC soundsets)

MKDIR ~lang/%LANGUAGE_BG2%/sounds~

ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EET_oldNameNewName" BEGIN
	"MAINF03" => "BGMainFa"
	"MAINF08" => "BGMainFb"
	"MAINF09" => "BGMainFc"
	"MAINF10" => "BGMainFd"
	"MAINF11" => "BGMainFe"
	"MAINF17" => "BGMainFf"
	"MAINF18" => "BGMainFg"
	"MAINF19" => "BGMainFh"
	"MAINF20" => "BGMainFi"
	"MAINF21" => "BGMainFj"
	"MAINF22" => "BGMainFk"
	"MAINF38" => "BGMainFl"
	"MAINF39" => "BGMainFm"
	"MAINM03" => "BGMainMa"
	"MAINM08" => "BGMainMb"
	"MAINM09" => "BGMainMc"
	"MAINM10" => "BGMainMd"
	"MAINM11" => "BGMainMe"
	"MAINM17" => "BGMainMf"
	"MAINM18" => "BGMainMg"
	"MAINM19" => "BGMainMh"
	"MAINM20" => "BGMainMi"
	"MAINM21" => "BGMainMj"
	"MAINM22" => "BGMainMk"
	"MAINM38" => "BGMainMl"
	"MAINM39" => "BGMainMm"
END
ACTION_IF (FILE_EXISTS ~EET/temp/wav/mainf03.wav~) BEGIN
	ACTION_PHP_EACH table_EET_oldNameNewName AS oldName => newName BEGIN
		COPY ~EET/temp/wav/%oldName%.wav~ ~lang/%LANGUAGE_BG2%/sounds/%newName%.wav~
	END
END ELSE ACTION_IF (FILE_EXISTS ~%bgee_dir%/lang/%LANGUAGE%/sounds/mainf03.wav~) BEGIN
	ACTION_PHP_EACH table_EET_oldNameNewName AS oldName => newName BEGIN
		COPY ~%bgee_dir%/lang/%LANGUAGE%/sounds/%oldName%.wav~ ~lang/%LANGUAGE_BG2%/sounds/%newName%.wav~
	END
END

OUTER_FOR (cnt = 1; cnt <= 6; cnt = cnt + 1) BEGIN
	ACTION_IF (FILE_EXISTS ~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%a.wav~) BEGIN
		COPY ~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%a.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%a.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%b.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%b.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%c.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%c.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%d.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%d.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%e.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%e.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%f.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%f.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%g.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%g.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%h.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%h.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%i.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%i.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%j.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%j.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%k.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%k.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%l.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%l.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Female%cnt%m.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGFeml%cnt%m.wav~
	END
	ACTION_IF (FILE_EXISTS ~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%a.wav~) BEGIN
		COPY ~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%a.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%a.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%b.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%b.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%c.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%c.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%d.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%d.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%e.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%e.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%f.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%f.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%g.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%g.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%h.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%h.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%i.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%i.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%j.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%j.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%k.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%k.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%l.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%l.wav~
			~%bgee_dir%/lang/%LANGUAGE%/sounds/Male%cnt%m.wav~ ~lang/%LANGUAGE_BG2%/sounds/BGMale%cnt%m.wav~
	END
END

/////                                                  \\\\\
///// WBM                                              \\\\\
/////                                                  \\\\\

MKDIR ~lang/%LANGUAGE_BG2%/movies~

//resolve BG2 movies conflicts

//renamed (using name from the vanilla game) to prevent from playing in Start Screen
//COPY_LARGE used instead of MOVE to prevent file deletion upon uninstalling
//disabled - EXE is patched instead
/*ACTION_IF (FILE_EXISTS ~lang/%LANGUAGE_BG2%/movies/INTRO15F.wbm~) BEGIN
	COPY_LARGE ~lang/%LANGUAGE_BG2%/movies/INTRO15F.wbm~ ~lang/%LANGUAGE_BG2%/movies/BG2INTRO.wbm~
END ELSE BEGIN
	COPY_LARGE ~movies/INTRO15F.wbm~ ~lang/%LANGUAGE_BG2%/movies/BG2INTRO.wbm~
END*/

//renamed to bypass hardcoded day/niteday movies when area has 'city' and 'extended nights' flags day/night movies are now triggered via area scripts
//currently disabled due to bug in Multiplayer mode, probably related to this: http://forum.baldursgate.com/discussion/comment/191406
/*ACTION_IF (FILE_EXISTS ~lang/%LANGUAGE_BG2%/movies/DAYNITE.wbm~)
AND (FILE_EXISTS ~lang/%LANGUAGE_BG2%/movies/NITEDAY.wbm~) BEGIN
	MOVE ~lang/%LANGUAGE_BG2%/movies/DAYNITE.wbm~ ~lang/%LANGUAGE_BG2%/movies/DAYNITE_.wbm~
	MOVE ~lang/%LANGUAGE_BG2%/movies/NITEDAY.wbm~ ~lang/%LANGUAGE_BG2%/movies/NITEDAY_.wbm~
END
MOVE ~movies/DAYNITE.wbm~ ~movies/DAYNITE_.wbm~
MOVE ~movies/NITEDAY.wbm~ ~movies/NITEDAY_.wbm~*/

//copy movies from BG:EE

//renamed due to name conflict
ACTION_IF (FILE_EXISTS ~%bgee_dir%/lang/%LANGUAGE%/movies/BLACKPIT.wbm~) BEGIN
	COPY_LARGE ~%bgee_dir%/lang/%LANGUAGE%/movies/BLACKPIT.wbm~ ~lang/%LANGUAGE_BG2%/movies/BLACKPI_.wbm~
END ELSE BEGIN
	COPY_LARGE ~%bgee_dir%/lang/en_US/movies/BLACKPIT.wbm~ ~lang/%LANGUAGE_BG2%/movies/BLACKPI_.wbm~
END

//copy BG1 movies converted to HD
//ENDCRDIT.wbm => ENDCRDI_.wbm
COPY_LARGE ~EET/base/movies~ ~lang/%LANGUAGE_BG2%/movies~

//copy BG1 movies with voice over
//INTRO.wbm => INTROBG1.wbm //name must have the same length as hardcoded INTRO15F
ACTION_IF (FILE_EXISTS ~%tra_path%/%LANGUAGE%/movies/BEREGOST.wbm~)
AND (FILE_EXISTS ~%tra_path%/%LANGUAGE%/movies/INTROBG1.wbm~)
AND (FILE_EXISTS ~%tra_path%/%LANGUAGE%/movies/NASHKELL.wbm~)
AND (FILE_EXISTS ~%tra_path%/%LANGUAGE%/movies/WYVERN.wbm~) BEGIN
	COPY_LARGE ~%tra_path%/%LANGUAGE%/movies~ ~lang/%LANGUAGE_BG2%/movies~
END ELSE BEGIN
	COPY_LARGE ~%tra_path%/en_US/movies~ ~lang/%LANGUAGE_BG2%/movies~
END

//update Baldur.ini
ACTION_IF (FILE_EXISTS ~%USER_DIRECTORY%/Baldur.ini~) BEGIN
	COPY ~%USER_DIRECTORY%/Baldur.ini~ ~%USER_DIRECTORY%~
		COUNT_REGEXP_INSTANCES ~'Text'~ num_matches
		PATCH_IF (num_matches = 0) BEGIN
			REPLACE_TEXTUALLY ~INSERT INTO options ROWS (~ ~INSERT INTO options ROWS (
	'Language',	'Text',	'%LANGUAGE_BG2%',~
		END ELSE BEGIN
			REPLACE_TEXTUALLY ~'Language',[ 	]+'Text',[ 	]+'.*'~ ~'Language',	'Text',	'%LANGUAGE_BG2%'~
		END
		DEFINE_ASSOCIATIVE_ARRAY table_EET_ini BEGIN
			"Full Screen" => "Window"//set only for now due to unknown problem with TIS/WED
			"Scale UI" => "Graphics"//set only for now due to unknown problem with TIS/WED
			"Cheats" => "Game Options"
			"Wild Surge Keys" => "Game Options"
			"Logging On" => "Program Options"
			"Debug Mode" => "Program Options"
			"WOTC" => "MOVIES"
			"TSRLOGO" => "MOVIES"
			"INTRO15F" => "MOVIES"
			"INTROBG1" => "MOVIES"
			"INFELOGO" => "MOVIES"
			"CREDITS_" => "MOVIES"
			"BWDRAGON" => "MOVIES"
			"BLACKPI_" => "MOVIES"
			"BILOGO" => "MOVIES"
			"BG4LOGO" => "MOVIES"
		END
		PHP_EACH "table_EET_ini" AS "option" => "row" BEGIN
			COUNT_REGEXP_INSTANCES ~'%option%'~ num_matches
			PATCH_IF (num_matches = 0) BEGIN
				REPLACE_TEXTUALLY ~INSERT INTO options ROWS (~ ~INSERT INTO options ROWS (
	'%row%',	'%option%',	'1',~
			END
		END
	BUT_ONLY
END ELSE BEGIN
<<<<<<<< EET-inlined/Baldur.ini
CREATE TABLE options (
	section string,
	name string,
	value string
);
INSERT INTO options ROWS (
	'Window',	'Full Screen',	'1',//set only for now due to unknown problem with TIS/WED
	'Graphics',	'Scale UI',	'1',//set only for now due to unknown problem with TIS/WED
	'MOVIES',	'BG4LOGO',	'1',
	'MOVIES',	'BILOGO',	'1',
	'MOVIES',	'BLACKPI_',	'1',
	'MOVIES',	'BWDRAGON',	'1',
	'MOVIES',	'CREDITS_',	'1',
	'MOVIES',	'INFELOGO',	'1',
	'MOVIES',	'INTRO15F',	'1',
	'MOVIES',	'INTROBG1',	'1',
	'MOVIES',	'TSRLOGO',	'1',
	'MOVIES',	'WOTC',	'1',
	'Program Options',	'Debug Mode',	'1',
	'Program Options',	'Logging On',	'1',
	'Game Options',	'Wild Surge Keys',	'1',
	'Game Options',	'Cheats',	'1',
	'Language',	'Text',	'%LANGUAGE_BG2%'
);
>>>>>>>>
	COPY ~EET-inlined/Baldur.ini~ ~%USER_DIRECTORY%/Baldur.ini~ EVALUATE_BUFFER
END

/////                                                  \\\\\
///// WED                                              \\\\\
/////                                                  \\\\\

COPY ~%patch_dir%/wed~ ~override~

/////                                                  \\\\\
///// WMP                                              \\\\\
/////                                                  \\\\\

MKDIR ~Worldmap~

COPY_EXISTING ~xnewarea.2da~ ~override~
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~^.*ar3000.*$~ ~~
IF ~ar3000~
BUT_ONLY

//icons_array
ACTION_DEFINE_ASSOCIATIVE_ARRAY "wmp_bg2_icons_array" BEGIN
	OH4000 , 29 => 106
	OH4100 , 32 => 108
	OH5100 , 34 => 110
	OH6000 , 33 => 109
	OH6100 , 31 => 107
	OH6200 , 31 => 107
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY "wmp_tob_icons_array" BEGIN
	AR4000 , 6 => 91
	AR3000 , 5 => 90
	AR5000 , 8 => 93
	AR5500 , 14 => 99
	AR5203 , 7 => 92
	AR5202 , 9 => 94
	AR5200 , 10 => 95
	AR6300 , 15 => 100
	AR6400 , 13 => 98
	AR6000 , 16 => 101
	AR6100 , 12 => 97
	OH4200 , 18 => 111
	OH6400 , 11 => 96
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY "wmp_bg2_2da_links_array" BEGIN
	AR3000 => XL3000
END

//dump wmp
COPY - ~%patch_dir%/worldmap.wmp~ ~%patch_dir%~
	LPF ~dump_wmp~
	INT_VAR
		//move_x = 373 //areas file is provided externally
		skip_areas = 1
	STR_VAR
		output_links = "EET/temp/map_bgee_links.tbl"
	END

COPY_EXISTING - ~worldmap.wmp~ ~override~
	LPF ~dump_wmp~
	INT_VAR
		move_y = 953
		compare_bams = 1
	STR_VAR
		2da_links_array = "wmp_bg2_2da_links_array"
		icons_array = "wmp_bg2_icons_array"
		output_areas = "EET/temp/map_bg2_areas.tbl"
		output_links = "EET/temp/map_bg2_links.tbl"
		output_strings = "EET/temp/map_bg2_trans.tra"
	END

COPY_EXISTING - ~worldm25.wmp~ ~override~
	LPF ~dump_wmp~
	INT_VAR
		move_x = 893
		move_y = 1681
		compare_bams = 1
	STR_VAR
		icons_array = "wmp_tob_icons_array"
		output_areas = "EET/temp/map_tob_areas.tbl"
		output_links = "EET/temp/map_tob_links.tbl"
		output_strings = "EET/temp/map_tob_trans.tra"
	END

//merge worldmap files
COPY + ~EET/tbl/map_mods_areas.tbl~ ~EET/temp/map_bgee_areas.tbl~
	~EET/lang/%LANGUAGE%/map_mods_trans.tra~ ~EET/temp/map_bgee_trans.tra~
COPY + ~EET/temp/map_bgee_links.tbl~ ~EET/temp~
	APPEND_FILE ~EET/tbl/map_mods_links.tbl~

ACTION_FOR_EACH file IN areas.tbl links.tbl trans.tra BEGIN
	//BP-BGT Worldmap
	ACTION_IF (FILE_EXISTS ~%bgee_dir%/Worldmap/map_mods_%file%~) BEGIN
		COPY + ~%bgee_dir%/Worldmap/map_mods_%file%~ ~EET/temp~
			FOR (i=1; i<="%resref_are_total%"; i=i+1) BEGIN
				SPRINT name_source "resref_are_source%i%"
				SPRINT name_dest "resref_are_dest%i%"
				SPRINT resref_index_source EVALUATE_BUFFER ~%%name_source%%~
				SPRINT resref_index_dest EVALUATE_BUFFER ~%%name_dest%%~
				REPLACE_TEXTUALLY ~%resref_index_source%~ ~%resref_index_dest%~
			END
	END
	ACTION_IF (FILE_EXISTS ~Worldmap/map_mods_%file%~) BEGIN
		COPY ~Worldmap/map_mods_%file%~ ~Worldmap~
			APPEND_FILE ~EET/temp/map_bgee_%file%~
			PATCH_IF (FILE_EXISTS ~EET/temp/map_mods_%file%.tbl~) BEGIN
				APPEND_FILE ~EET/temp/map_mods_%file%.tbl~
			END
	END ELSE BEGIN
		COPY ~EET/temp/map_bgee_%file%~ ~Worldmap/map_mods_%file%~
			PATCH_IF (FILE_EXISTS ~EET/temp/map_mods_%file%.tbl~) BEGIN
				APPEND_FILE ~EET/temp/map_mods_%file%.tbl~
			END
	END
	//EET worldmap
	ACTION_IF (~%file%~ STRING_EQUAL_CASE ~areas.tbl~) BEGIN
		COPY + ~EET/tbl/map_eet_areas.tbl~ ~EET/temp~
			REPLACE_EVALUATE CASE_INSENSITIVE ~^\([A-Z0-9#_]+[ 	]+[A-Z0-9#_]+[ 	]+[A-Z0-9#_]+[ 	]+[0-9]+[ 	]+[0-9]+[ 	]+\)\([0-9\-]+\)~ BEGIN
				SET MATCH2 = MATCH2 + 373
			END ~%MATCH1%%MATCH2%~
			APPEND_FILE ~EET/temp/map_bg2_areas.tbl~
			APPEND_FILE ~EET/temp/map_tob_areas.tbl~
	END ELSE BEGIN
		COPY + ~Worldmap/map_mods_%file%~ ~EET/temp/map_eet_%file%~
			APPEND_FILE ~EET/temp/map_bg2_%file%~
			APPEND_FILE ~EET/temp/map_tob_%file%~
	END
END

COPY - ~EET/temp/map_eet_areas.tbl~ ~EET/temp~
	LPF fix_table END
	LPM read_area_array_from_2da

COPY - ~EET/temp/map_eet_links.tbl~ ~EET/temp~
    LPF fix_table END
    LPM read_link_array_from_2da

COPY - ~EET/temp/map_eet_trans.tra~ ~EET/temp~
    LPF fix_table END
    LPM read_trans_array_from_2da

LAM merge_in_translations

INCLUDE ~EET/lib/build_worldmap.tpa~

COPY_EXISTING ~worldmap.wmp~ ~override~
	READ_LONG 0xc wmap_off
	WRITE_ASCII wmap_off ~WORLDMAP~ #8
	WRITE_LONG wmap_off + 0x8 ~1550~
	WRITE_LONG wmap_off + 0xc ~2160~
	WRITE_ASCII wmap_off + 0x30 ~MAPICONS~ #8

/////                                                  \\\\\
///// Fixpack                                          \\\\\
/////                                                  \\\\\

INCLUDE ~EET/lib/bgee_fixpack.tph~

/////                                                  \\\\\
///// Exe patching                                     \\\\\
/////                                                  \\\\\

COPY ~Baldur.exe~ ~Baldur.exe~
	REPLACE_TEXTUALLY ~INTRO15F~ ~INTROBG1~ //changes hardcoded movie reference
BUT_ONLY

/////                                                  \\\\\
///// Biffing                                          \\\\\
/////                                                  \\\\\

// Biff area graphic resources, wav files and IWD animations
ACTION_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[Bb]~ = 1) BEGIN //b not dected in args-list
	PRINT ~Biffing graphic resources, wav files and IWD animations~
	MAKE_BIFF ~eetTU00~ BEGIN ~EET/temp/biff/eetTU00~ ~^.*$~ END
	MAKE_BIFF ~eetOH2~ BEGIN ~EET/temp/biff/eetOH2~ ~^.*$~ END
	MAKE_BIFF ~eetOH3~ BEGIN ~EET/temp/biff/eetOH3~ ~^.*$~ END
	MAKE_BIFF ~eetOH9~ BEGIN ~EET/temp/biff/eetOH9~ ~^.*$~ END
	MAKE_BIFF ~eetAR00~ BEGIN ~EET/temp/biff/eetAR00~ ~^.*$~ END
	MAKE_BIFF ~eetAR01~ BEGIN ~EET/temp/biff/eetAR01~ ~^.*$~ END
	MAKE_BIFF ~eetAR02~ BEGIN ~EET/temp/biff/eetAR02~ ~^.*$~ END
	MAKE_BIFF ~eetAR03~ BEGIN ~EET/temp/biff/eetAR03~ ~^.*$~ END
	MAKE_BIFF ~eetAR04~ BEGIN ~EET/temp/biff/eetAR04~ ~^.*$~ END
	MAKE_BIFF ~eetAR05~ BEGIN ~EET/temp/biff/eetAR05~ ~^.*$~ END
	MAKE_BIFF ~eetAR06~ BEGIN ~EET/temp/biff/eetAR06~ ~^.*$~ END
	MAKE_BIFF ~eetAR07~ BEGIN ~EET/temp/biff/eetAR07~ ~^.*$~ END
	MAKE_BIFF ~eetAR08~ BEGIN ~EET/temp/biff/eetAR08~ ~^.*$~ END
	MAKE_BIFF ~eetAR09~ BEGIN ~EET/temp/biff/eetAR09~ ~^.*$~ END
	MAKE_BIFF ~eetAR10~ BEGIN ~EET/temp/biff/eetAR10~ ~^.*$~ END
	MAKE_BIFF ~eetAR11~ BEGIN ~EET/temp/biff/eetAR11~ ~^.*$~ END
	MAKE_BIFF ~eetAR12~ BEGIN ~EET/temp/biff/eetAR12~ ~^.*$~ END
	MAKE_BIFF ~eetAR13~ BEGIN ~EET/temp/biff/eetAR13~ ~^.*$~ END
	MAKE_BIFF ~eetAR14~ BEGIN ~EET/temp/biff/eetAR14~ ~^.*$~ END
	MAKE_BIFF ~eetAR15~ BEGIN ~EET/temp/biff/eetAR15~ ~^.*$~ END
	MAKE_BIFF ~eetAR16~ BEGIN ~EET/temp/biff/eetAR16~ ~^.*$~ END
	MAKE_BIFF ~eetAR17~ BEGIN ~EET/temp/biff/eetAR17~ ~^.*$~ END
	MAKE_BIFF ~eetAR18~ BEGIN ~EET/temp/biff/eetAR18~ ~^.*$~ END
	MAKE_BIFF ~eetAR19~ BEGIN ~EET/temp/biff/eetAR19~ ~^.*$~ END
	MAKE_BIFF ~eetAR20~ BEGIN ~EET/temp/biff/eetAR20~ ~^.*$~ END
	MAKE_BIFF ~eetAR21~ BEGIN ~EET/temp/biff/eetAR21~ ~^.*$~ END
	MAKE_BIFF ~eetAR22~ BEGIN ~EET/temp/biff/eetAR22~ ~^.*$~ END
	MAKE_BIFF ~eetAR23~ BEGIN ~EET/temp/biff/eetAR23~ ~^.*$~ END
	MAKE_BIFF ~eetAR24~ BEGIN ~EET/temp/biff/eetAR24~ ~^.*$~ END
	MAKE_BIFF ~eetAR26~ BEGIN ~EET/temp/biff/eetAR26~ ~^.*$~ END
	MAKE_BIFF ~eetAR27~ BEGIN ~EET/temp/biff/eetAR27~ ~^.*$~ END
	MAKE_BIFF ~eetAR28~ BEGIN ~EET/temp/biff/eetAR28~ ~^.*$~ END
	MAKE_BIFF ~eetAR29~ BEGIN ~EET/temp/biff/eetAR29~ ~^.*$~ END
	MAKE_BIFF ~eetAR30~ BEGIN ~EET/temp/biff/eetAR30~ ~^.*$~ END
	MAKE_BIFF ~eetAR31~ BEGIN ~EET/temp/biff/eetAR31~ ~^.*$~ END
	MAKE_BIFF ~eetAR32~ BEGIN ~EET/temp/biff/eetAR32~ ~^.*$~ END
	MAKE_BIFF ~eetAR33~ BEGIN ~EET/temp/biff/eetAR33~ ~^.*$~ END
	MAKE_BIFF ~eetAR34~ BEGIN ~EET/temp/biff/eetAR34~ ~^.*$~ END
	MAKE_BIFF ~eetAR35~ BEGIN ~EET/temp/biff/eetAR35~ ~^.*$~ END
	MAKE_BIFF ~eetAR36~ BEGIN ~EET/temp/biff/eetAR36~ ~^.*$~ END
	MAKE_BIFF ~eetAR37~ BEGIN ~EET/temp/biff/eetAR37~ ~^.*$~ END
	MAKE_BIFF ~eetAR38~ BEGIN ~EET/temp/biff/eetAR38~ ~^.*$~ END
	MAKE_BIFF ~eetAR39~ BEGIN ~EET/temp/biff/eetAR39~ ~^.*$~ END
	MAKE_BIFF ~eetAR40~ BEGIN ~EET/temp/biff/eetAR40~ ~^.*$~ END
	MAKE_BIFF ~eetAR41~ BEGIN ~EET/temp/biff/eetAR41~ ~^.*$~ END
	MAKE_BIFF ~eetAR42~ BEGIN ~EET/temp/biff/eetAR42~ ~^.*$~ END
	MAKE_BIFF ~eetAR43~ BEGIN ~EET/temp/biff/eetAR43~ ~^.*$~ END
	MAKE_BIFF ~eetAR44~ BEGIN ~EET/temp/biff/eetAR44~ ~^.*$~ END
	MAKE_BIFF ~eetAR45~ BEGIN ~EET/temp/biff/eetAR45~ ~^.*$~ END
	MAKE_BIFF ~eetAR46~ BEGIN ~EET/temp/biff/eetAR46~ ~^.*$~ END
	MAKE_BIFF ~eetAR47~ BEGIN ~EET/temp/biff/eetAR47~ ~^.*$~ END
	MAKE_BIFF ~eetAR48~ BEGIN ~EET/temp/biff/eetAR48~ ~^.*$~ END
	MAKE_BIFF ~eetAR49~ BEGIN ~EET/temp/biff/eetAR49~ ~^.*$~ END
	MAKE_BIFF ~eetAR50~ BEGIN ~EET/temp/biff/eetAR50~ ~^.*$~ END
	MAKE_BIFF ~eetAR51~ BEGIN ~EET/temp/biff/eetAR51~ ~^.*$~ END
	MAKE_BIFF ~eetAR52~ BEGIN ~EET/temp/biff/eetAR52~ ~^.*$~ END
	MAKE_BIFF ~eetAR53~ BEGIN ~EET/temp/biff/eetAR53~ ~^.*$~ END
	MAKE_BIFF ~eetAR54~ BEGIN ~EET/temp/biff/eetAR54~ ~^.*$~ END
	MAKE_BIFF ~eetAR55~ BEGIN ~EET/temp/biff/eetAR55~ ~^.*$~ END
	MAKE_BIFF ~eetAR56~ BEGIN ~EET/temp/biff/eetAR56~ ~^.*$~ END
	MAKE_BIFF ~eetAR57~ BEGIN ~EET/temp/biff/eetAR57~ ~^.*$~ END
	MAKE_BIFF ~eetAR58~ BEGIN ~EET/temp/biff/eetAR58~ ~^.*$~ END
	MAKE_BIFF ~eetAR59~ BEGIN ~EET/temp/biff/eetAR59~ ~^.*$~ END
	MAKE_BIFF ~eetAR60~ BEGIN ~EET/temp/biff/eetAR60~ ~^.*$~ END
	MAKE_BIFF ~eetAR61~ BEGIN ~EET/temp/biff/eetAR61~ ~^.*$~ END
	MAKE_BIFF ~eetWAV~ BEGIN ~EET/temp/wav~ ~^.*$~ END
	MAKE_BIFF ~eetBAM~ BEGIN ~EET/temp/bam~ ~^.*$~ END
	MAKE_BIFF ~eetIWD~ BEGIN ~EET/base/animation~ ~^.*$~ END
	//OUTER_SET currentTotal = 0
	//OUTER_SET currentFile = 0
	//MKDIR ~EET/temp/biff/eetMis0~ //already created
	OUTER_SET biff_temp = currentFile
	OUTER_WHILE (biff_temp > 0) BEGIN
		OUTER_SET biff_temp = biff_temp - 1
		MAKE_BIFF ~eetMis%biff_temp%~ BEGIN ~EET/temp/biff/eetMis%biff_temp%~ ~^.*$~ END
	END
	ACTION_BASH_FOR ~EET/temp/biff~ ~^.*$~ BEGIN
		ACTION_IF ( BASH_FOR_SIZE + currentTotal > 30000000 /* 30M */ && currentTotal > 0 ) BEGIN
			MAKE_BIFF ~eetMis%currentFile%~ BEGIN ~EET/temp/biff/eetMis%currentFile%~ ~^.*$~ END
			OUTER_SET currentFile = currentFile + 1
			OUTER_SET currentTotal = 0
			MKDIR ~EET/temp/biff/eetMis%currentFile%~
		END
		MOVE + ~%BASH_FOR_FILESPEC%~ ~EET/temp/biff/eetMis%currentFile%~
		OUTER_SET currentTotal = currentTotal + BASH_FOR_SIZE
	END
	ACTION_IF ( currentTotal > 0 ) BEGIN
		MAKE_BIFF ~eetMis%currentFile%~ BEGIN ~EET/temp/biff/eetMis%currentFile%~ ~^.*$~ END
	END
END ELSE BEGIN
	PRINT ~Copying area graphic resources, wav files and IWD animations to override. This will take a while...~
	COPY_LARGE ~EET/temp/biff/eetTU00~ ~override~
		~EET/temp/biff/eetOH2~ ~override~
		~EET/temp/biff/eetOH3~ ~override~
		~EET/temp/biff/eetOH9~ ~override~
		~EET/temp/biff/eetAR00~ ~override~
		~EET/temp/biff/eetAR01~ ~override~
		~EET/temp/biff/eetAR02~ ~override~
		~EET/temp/biff/eetAR03~ ~override~
		~EET/temp/biff/eetAR04~ ~override~
		~EET/temp/biff/eetAR05~ ~override~
		~EET/temp/biff/eetAR06~ ~override~
		~EET/temp/biff/eetAR07~ ~override~
		~EET/temp/biff/eetAR08~ ~override~
		~EET/temp/biff/eetAR09~ ~override~
		~EET/temp/biff/eetAR10~ ~override~
		~EET/temp/biff/eetAR11~ ~override~
		~EET/temp/biff/eetAR12~ ~override~
		~EET/temp/biff/eetAR13~ ~override~
		~EET/temp/biff/eetAR14~ ~override~
		~EET/temp/biff/eetAR15~ ~override~
		~EET/temp/biff/eetAR16~ ~override~
		~EET/temp/biff/eetAR17~ ~override~
		~EET/temp/biff/eetAR18~ ~override~
		~EET/temp/biff/eetAR19~ ~override~
		~EET/temp/biff/eetAR20~ ~override~
		~EET/temp/biff/eetAR21~ ~override~
		~EET/temp/biff/eetAR22~ ~override~
		~EET/temp/biff/eetAR23~ ~override~
		~EET/temp/biff/eetAR24~ ~override~
		~EET/temp/biff/eetAR26~ ~override~
		~EET/temp/biff/eetAR27~ ~override~
		~EET/temp/biff/eetAR28~ ~override~
		~EET/temp/biff/eetAR29~ ~override~
		~EET/temp/biff/eetAR30~ ~override~
		~EET/temp/biff/eetAR31~ ~override~
		~EET/temp/biff/eetAR32~ ~override~
		~EET/temp/biff/eetAR33~ ~override~
		~EET/temp/biff/eetAR34~ ~override~
		~EET/temp/biff/eetAR35~ ~override~
		~EET/temp/biff/eetAR36~ ~override~
		~EET/temp/biff/eetAR37~ ~override~
		~EET/temp/biff/eetAR38~ ~override~
		~EET/temp/biff/eetAR39~ ~override~
		~EET/temp/biff/eetAR40~ ~override~
		~EET/temp/biff/eetAR41~ ~override~
		~EET/temp/biff/eetAR42~ ~override~
		~EET/temp/biff/eetAR43~ ~override~
		~EET/temp/biff/eetAR44~ ~override~
		~EET/temp/biff/eetAR45~ ~override~
		~EET/temp/biff/eetAR46~ ~override~
		~EET/temp/biff/eetAR47~ ~override~
		~EET/temp/biff/eetAR48~ ~override~
		~EET/temp/biff/eetAR49~ ~override~
		~EET/temp/biff/eetAR50~ ~override~
		~EET/temp/biff/eetAR51~ ~override~
		~EET/temp/biff/eetAR52~ ~override~
		~EET/temp/biff/eetAR53~ ~override~
		~EET/temp/biff/eetAR54~ ~override~
		~EET/temp/biff/eetAR55~ ~override~
		~EET/temp/biff/eetAR56~ ~override~
		~EET/temp/biff/eetAR57~ ~override~
		~EET/temp/biff/eetAR58~ ~override~
		~EET/temp/biff/eetAR59~ ~override~
		~EET/temp/biff/eetAR60~ ~override~
		~EET/temp/biff/eetAR61~ ~override~
		~EET/temp/wav~ ~override~
		~EET/temp/bam~ ~override~
		~EET/base/animation~ ~override~
		~EET/temp/biff~ ~override~
		~EET/temp/biff/eetMis0~ ~override~
	OUTER_SET biff_temp = currentFile
	OUTER_WHILE (biff_temp > 0) BEGIN
		COPY_LARGE ~EET/temp/biff/eetMis%biff_temp%~ ~override~
		OUTER_SET biff_temp = biff_temp - 1
	END
END

/////                                                  \\\\\
///// Finalize installation                            \\\\\
/////                                                  \\\\\

COPY_EXISTING sw1h01.itm ~override/EET.flag~
PRINT ~'EET.flag' dummy file is used by weidu to recognize the installation as EET~
PRINT ~To install mods without EET argument in GAME_IS command you need to temporarily move it away from override directory~

ACTION_IF (FILE_EXISTS ~EET/docs/[%LANGUAGE%]readme.htm~) BEGIN
	AT_INTERACTIVE_EXIT ~VIEW EET%os_slash%docs%os_slash%[%LANGUAGE%]readme.htm~
END ELSE BEGIN
	AT_INTERACTIVE_EXIT ~VIEW EET%os_slash%docs%os_slash%[en_US]readme.htm~
END
