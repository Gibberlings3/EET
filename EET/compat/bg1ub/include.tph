//keep in mind that there is also compatibility code in lib/prep_ITM.tph for following resources: AX1H07.ITM, MAGE02.ITM

//Compatibility with BG1UB "Creature Corrections" component

ACTION_IF (FILE_EXISTS ~WeiDU-BGSoD.log~) AND (FILE_CONTAINS ~WeiDU-BGSoD.log~ ~BG1UB\.TP2. #[0-9]+ #16 ~) BEGIN
	COPY_EXISTING ~PLYOGRE.CRE~ ~override~ // Ogre (OGRE animation)
		PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
			WRITE_BYTE 0x237 1                  // Sex: Male
			WRITE_BYTE 0x275 4                  // Gender: Neither
		END
	BUT_ONLY
END

//Compatibility with BG1UB "Item Corrections and Restorations" component

ACTION_IF (FILE_EXISTS ~WeiDU-BGSoD.log~) AND (FILE_CONTAINS ~WeiDU-BGSoD.log~ ~BG1UB\.TP2. #[0-9]+ #22 ~) BEGIN
	COPY_EXISTING ~misc76.itm~ ~override~ // Dream Potion
		WRITE_ASCIIE 0x3a "imisc76" #8
		WRITE_ASCIIE 0x58 "cpotn13" #8
	BUT_ONLY

	COPY_EXISTING ~misc77.itm~ ~override~ // Skull Of Kereph
		WRITE_ASCIIE 0x3a "imisc77" #8
	BUT_ONLY

	COPY_EXISTING ~SQUIRP.ITM~ ~override~ // Restores the Squirrel Paw icon (devSin)
		PATCH_IF (SOURCE_SIZE > 0x72) BEGIN
			WRITE_ASCIIE 0x3a ~ISQUIRL~ #8
			READ_SHORT 0x68 "num_abl"
			PATCH_IF ("%num_abl%" > 0x0) BEGIN
				READ_LONG 0x64 "abl_offset"
				WRITE_ASCIIE "%abl_offset%" + 0x4 ~ISQUIRL~ #8
			END
		END
	BUT_ONLY

	COPY_EXISTING ~SPIN109.SPL~ ~override~ // Restores the unused Lay on Hands spell icon (devSin)
		PATCH_IF (SOURCE_SIZE > 0x72) BEGIN
			READ_LONG 0x64 ablOff
			FOR (READ_SHORT 0x68 numAbl; numAbl > 0x0; numAbl -= 0x1) BEGIN
				WRITE_ASCII ablOff + 0x4 ~SPIN109~ #8
				ablOff += 0x28
			END
		END
	BUT_ONLY
END
