BACKUP ~EET_Tweaks/backup~
AUTHOR ~K4thos (swit)~
VERSION ~beta 0.1~
README ~EET_Tweaks/docs/[%LANGUAGE%]readme.htm~ ~EET_Tweaks/docs/[en_US]readme.htm~
//ASK_EVERY_COMPONENT
//AUTO_TRA ~EET_Tweaks/lang/%s~//%~

ALWAYS
	INCLUDE ~EET_Tweaks/lib/always.tph~
END

LANGUAGE ~English~
	~en_US~
	~EET_Tweaks/lang/en_US/prompts.tra~
	~EET_Tweaks/lang/en_US/setup.tra~
LANGUAGE ~Polski (Polish)~
	~pl_PL~
	~EET_Tweaks/lang/pl_PL/prompts.tra~
	~EET_Tweaks/lang/pl_PL/setup.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Teleport spell                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Teleport spell~
//DESIGNATED 0
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

LOAD_TRA ~EET_Tweaks/lang/%LANGUAGE%/%COMPONENT_NUMBER%.tra~

COMPILE ~EET_Tweaks/%COMPONENT_NUMBER%/K#TELSPL.d~

//modify both scripts and dialogue
<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#TELCUT.baf
IF
  True()
THEN
  RESPONSE #100
    CutSceneId(Player1)
    CreateVisualEffectObject("SPDIMNDR",Player1)
    CreateVisualEffectObject("SPDIMNDR",Player2)
    CreateVisualEffectObject("SPDIMNDR",Player3)
    CreateVisualEffectObject("SPDIMNDR",Player4)
    CreateVisualEffectObject("SPDIMNDR",Player5)
    CreateVisualEffectObject("SPDIMNDR",Player6)
    Wait(3)
    FadeToColor([20.0],0)
    Wait(1)
    LeaveAreaLUAPanic("%area%","",[%Xcoordinate%.%Ycoordinate%],%Orientation%)
    LeaveAreaLUA("%area%","",[%Xcoordinate%.%Ycoordinate%],%Orientation%)
    ActionOverride(Player2,LeaveAreaLUA("%area%","",[%Xcoordinate%.%Ycoordinate%],%Orientation%))
    ActionOverride(Player3,LeaveAreaLUA("%area%","",[%Xcoordinate%.%Ycoordinate%],%Orientation%))
    ActionOverride(Player4,LeaveAreaLUA("%area%","",[%Xcoordinate%.%Ycoordinate%],%Orientation%))
    ActionOverride(Player5,LeaveAreaLUA("%area%","",[%Xcoordinate%.%Ycoordinate%],%Orientation%))
    ActionOverride(Player6,LeaveAreaLUA("%area%","",[%Xcoordinate%.%Ycoordinate%],%Orientation%))
    MultiPlayerSync()
    Wait(1)
    FadeFromColor([20.0],0)
    CreateVisualEffectObject("SPDIMNDR",Myself)
    ActionOverride(Player2,CreateVisualEffectObject("SPDIMNDR",Myself))
    ActionOverride(Player3,CreateVisualEffectObject("SPDIMNDR",Myself))
    ActionOverride(Player4,CreateVisualEffectObject("SPDIMNDR",Myself))
    ActionOverride(Player5,CreateVisualEffectObject("SPDIMNDR",Myself))
    ActionOverride(Player6,CreateVisualEffectObject("SPDIMNDR",Myself))
    Wait(3)
    EndCutSceneMode()
END
>>>>>>>>

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/area-et.baf
IF
  Global("K#BeenIn%area%","GLOBAL",0)
  //AreaCheck("%area%")
THEN
  RESPONSE #100
    SetGlobal("K#BeenIn%area%","GLOBAL",1)
    Continue()
END
>>>>>>>>

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#TELSPL-eb.d
EXTEND_BOTTOM K#TELSPL 0
	IF ~OR(2) Global("K#BeenIn%area%","GLOBAL",1) Global("K#DebugMode","GLOBAL",1)~ THEN REPLY ~%string%~ DO ~ClearAllActions()
StartCutScene("K#%area%")
DestroySelf()~ EXIT
END
>>>>>>>>

COPY - ~EET_Tweaks/%COMPONENT_NUMBER%/EET-teleport.tbl~ ~EET_Tweaks/%COMPONENT_NUMBER%/EET-teleport.tbl~
	COUNT_2DA_ROWS 4 cntrow
	FOR (cnt = 1; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
		SET update = 0
		READ_2DA_ENTRY cnt 0 4 "area"
		READ_2DA_ENTRY cnt 1 4 "Xcoordinate"
		READ_2DA_ENTRY cnt 2 4 "Ycoordinate"
		READ_2DA_ENTRY cnt 3 4 "Orientation"
		INNER_ACTION BEGIN
			ACTION_IF (("%area%" STRING_EQUAL_CASE "%area_last%")=0) BEGIN
				EXTEND_TOP ~%area%.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/area-et.baf~ EVALUATE_BUFFER
				COPY - ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#TELCUT.baf~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#%area%.baf~
				COMPILE ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#%area%.baf~ EVALUATE_BUFFER
				COPY - ~%tra_path%/%LANGUAGE%/areas.tra~ ~%tra_path%/%LANGUAGE%/areas.tra~
					REPLACE_TEXTUALLY ~%slash%%slash%.*~ ~~
					COUNT_2DA_ROWS 3 cntrow2
					FOR (cnt2 = 0; cnt2 < "%cntrow2%"; cnt2 = cnt2 + 1) BEGIN
						READ_2DA_ENTRY cnt2 0 3 "compareWith"
						READ_2DA_ENTRY cnt2 2 3 "string"
						PATCH_IF (("@%area%" STRING_EQUAL_CASE "%compareWith%")=1) BEGIN
							INNER_PATCH_SAVE string "%string%" BEGIN
								REPLACE_TEXTUALLY "###" " "
								REPLACE_TEXTUALLY ~"~ ~~ //"
								REPLACE_TEXTUALLY "~" ""
							END
							SET update = 1
							SET cnt2 = cntrow2
						END
					END
				ACTION_IF ("%update%" = 1) BEGIN 
					COMPILE ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#TELSPL-eb.d~ EVALUATE_BUFFER
				END ELSE BEGIN
					WARN ~Translation for %area% used in K#TELSPL.DLG not found.~
				END
			END
		END
		SPRINT ~area_last~ ~%area%~
	END

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/K#TELSPL.SPL~ ~override~
	SAY 0x8 @2000000
	SAY 0x50 @2000001

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/K#TELSPL.ITM~ ~override~
	SAY 0xc @2000000
	SAY 0x54 @2000001

COPY_EXISTING ~25Spell.sto~ ~override~
	ADD_STORE_ITEM "K#TELSPL" #1 #0 #0 ~IDENTIFIED~ #2

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Consistent NPC appearance                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Consistent NPC appearance~
//DESIGNATED 1
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //BG:EE
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //BG2:EE
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Which game should be used as a portraits and colours base for NPCs that are joinable in both BG:EE and BG2:EE?
 1] BG1
 2] BG2~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

INCLUDE ~EET_TWEAKS/lib/%COMPONENT_NUMBER%.tph~

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Consistent NPC voices                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Consistent NPC voices~
//DESIGNATED 2
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~
REQUIRE_PREDICATE ("%LANGUAGE%" STRING_EQUAL_CASE ~pl_PL~) ~This component is only for Polish language (unless someone will suggest changes for other languages in future).~

LOAD_TRA ~EET_Tweaks/lang/%LANGUAGE%/%COMPONENT_NUMBER%.tra~

INCLUDE ~EET_TWEAKS/lib/%COMPONENT_NUMBER%_%LANGUAGE%.tph~ //no point in bloating the main file, especially if there will be more languages added in future

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Additional XP CAP for SoA portion of the game    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Additional XP CAP for SoA portion of the game~
//DESIGNATED 3
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //2,500,000
		OUTER_SET value = 2500000
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //2,950,000
		OUTER_SET value = 2950000
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 1) BEGIN
			PRINT ~Type in the integer value for XP CAP:~
			ACTION_READLN ~value~
		END
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~What value should be used for BG2:EE SoA XP CAP?
 1] 2,500,000 - BG2:ToB starting XP for NPC
 2] 2,950,000 - BG2 without expansion XP CAP
 3] Custom value~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/baldur-eb.baf
IF
  GlobalLT("ENDOFBG2","GLOBAL",2)
  XPGT(Player%number%,%value%)
  !AreaCheck("OH8000")
  !AreaCheck("OH9310")
  Global("OHB_BP2_CAPTURE","GLOBAL",0)
  Global("BPINTRO","GLOBAL",0)
THEN
  RESPONSE #100
    ChangeStat(Player%number%,XP,%value%,SET)
    Continue()
END
>>>>>>>>

ACTION_FOR_EACH number IN 1 2 3 4 5 6 BEGIN
	EXTEND_BOTTOM ~BALDUR.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/baldur-eb.baf~ EVALUATE_BUFFER
END

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust XP CAP for BG1 portion of the game        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust XP CAP for BG1 portion of the game~
//DESIGNATED 4
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //89,000
		OUTER_SET value = 89000
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //161,000
		OUTER_SET value = 161000
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //Disable
		OUTER_SET value = 100000000
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 1) BEGIN
			PRINT ~Type in the integer value for XP CAP:~
			ACTION_READLN ~value~
		END
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~What value should be used for BG:EE XP CAP?
 1]  89,000 - BG2:EE starting XP
 2] 161,000 - BG:EE XP CAP (default)
 3] Disable XP CAP (set to 100,000,000)
 4] Custom value~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING ~BALDUR.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		PATCH_FOR_EACH number IN 1 2 3 4 5 6 BEGIN
			SPRINT textToReplace ~GlobalLT("ENDOFBG1","GLOBAL",2)[%newline%]*XPGT(Player%number%,[0-9]+)[%newline%]*!AreaCheck("OH8000")[%newline%]*!AreaCheck("OH9310")[%newline%]*Global("OHB_BP2_CAPTURE","GLOBAL",0)[%newline%]*Global("BPINTRO","GLOBAL",0)[%newline%]*THEN[%newline%]*RESPONSE #100[%newline%]*ChangeStat(Player%number%,XP,[0-9]+,SET)~
			COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
			PATCH_IF (num_matches > 0) BEGIN
				REPLACE_TEXTUALLY ~%textToReplace%~ ~GlobalLT("ENDOFBG1","GLOBAL",2)
  XPGT(Player%number%,%value%)
  !AreaCheck("OH8000")
  !AreaCheck("OH9310")
  Global("OHB_BP2_CAPTURE","GLOBAL",0)
  Global("BPINTRO","GLOBAL",0)
THEN
  RESPONSE #100
    ChangeStat(Player%number%,XP,%value%,SET)~
			END ELSE BEGIN
				PATCH_WARN ~Warning: could not find EET XP CAP data in %SOURCE_FILESPEC%~
			END
		END
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust total XP CAP                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust total XP CAP~
//DESIGNATED 5
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ ~This component is not compatible with your game.~
FORBID_COMPONENT ~bg2_tweaks/setup-bg2_tweaks.tp2~ 2090 ~Component already installed via BG2 Tweaks~
FORBID_COMPONENT ~bg2_tweaks/setup-bg2_tweaks.tp2~ 2091 ~Component already installed via BG2 Tweaks~
FORBID_COMPONENT ~bg2_tweaks/setup-bg2_tweaks.tp2~ 2092 ~Component already installed via BG2 Tweaks~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //2,950,000
		OUTER_SET value = 2950000
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //8,000,000
		OUTER_SET value = 8000000
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //Disable
		OUTER_SET value = "-1"
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 10) BEGIN
			PRINT ~Type in the integer value for XP CAP:~
			ACTION_READLN ~value~
		END
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~What value should be used for BG2:EE SoA XP CAP?
 1] 2,950,000 - BG2 without expansion XP CAP
 2] 8,000,000 - BG2:EE XP CAP (default)
 3] Disable XP CAP
 4] Custom value~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING ~XPCAP.2DA~ ~override~
	REPLACE_TEXTUALLY ~[1-9][0-9]+~ ~%value%~ EVALUATE_BUFFER //numbers >=10 only, otherwise, it changes 2DA as well
	BUT_ONLY
COPY_EXISTING ~STARTARE.2DA~ ~override~
	REPLACE_TEXTUALLY ~START_XP_CAP[ 	]+[-0-9]+~ ~%value%~ EVALUATE_BUFFER
	BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust XP for Traps, Spells and Lockpicking      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust XP for Traps, Spells and Lockpicking~
//DESIGNATED 6
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt ca iwd_in_bg2 bgee bg2ee iwdee eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //BG1 (default)
		OUTER_SPRINT DISARM_TRAP ~DISARM_TRAP   10   10   10   10   10   17   17   17   17   17   27   27   27   27   27   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32   32~
		OUTER_SPRINT PICK_LOCK   ~PICK_LOCK     25   25   25   25   25   40   40   40   40   40   95   95   95   95   95   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15   15~
		OUTER_SPRINT LEARN_SPELL ~LEARN_SPELL   10   20   30   40   50   60   70   80   90   0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0~
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //BG2
		OUTER_SPRINT DISARM_TRAP ~DISARM_TRAP   1000 1000 1000 1000 1000 1750 1750 1750 1750 1750 2750 2750 2750 2750 2750 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250 3250~
		OUTER_SPRINT PICK_LOCK   ~PICK_LOCK     250  250  250  250  250  400  400  400  400  400  950  950  950  950  950  1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550 1550~
		OUTER_SPRINT LEARN_SPELL ~LEARN_SPELL   1000 2000 3000 4000 5000 6000 7000 8000 9000 0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0~
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //Vanilla friendly progressive
		OUTER_SPRINT DISARM_TRAP ~DISARM_TRAP   10   10   10   10   10   17   17   17   17   17   300  350  450  550  650  750  850  950  1050 1150 1250 1350 1450 1550 1650 1750 1850 1950 2050 2150 2250 2450 2550 2650 2750 2850 2950 3050 3150 3250~
		OUTER_SPRINT PICK_LOCK   ~PICK_LOCK     25   25   25   25   25   40   40   40   40   40   100  150  200  250  300  350  400  450  500  550  600  650  700  750  800  850  900  950  1000 1050 1100 1150 1200 1250 1300 1350 1400 1450 1500 1550~
		OUTER_SPRINT LEARN_SPELL ~LEARN_SPELL   10   20   30   40   250  500  1000 2000 4000 0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0    0~
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //Disable
		OUTER_SET value = 0
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 5) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 0) BEGIN
			PRINT ~Type in the integer percentage value:~
			ACTION_READLN ~value~
		END
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~How would you like to do adjust XP for Disarming Traps, Lockpicking and Spell learning?
 1] Use XP values from BG1 (default)
 2] Use XP values from BG2
 3] Vanilla friendly progressive
 4] Disable XP for disarming traps, lockpicking, spell learning
 5] Custom value~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING ~XPBONUS.2DA~ ~override~
	PATCH_IF (button >= 4) BEGIN
		COUNT_2DA_COLS "cntcol"
		COUNT_2DA_ROWS "%cntcol%" "cntrows"
		FOR (cnt = 0; cnt < "%cntrows%"; cnt = cnt + 1) BEGIN
			FOR (cnt2 = 1; cnt2 < "%cntcol%"; cnt2 = cnt2 + 1) BEGIN
				READ_2DA_ENTRY cnt cnt2 "%cntcol%" "XP_value"
				PATCH_IF ("%XP_value%" > 0) BEGIN
					SET XP_value = ("%XP_value%" * "%value%" / 100)
					PATCH_IF ("%XP_value%" < 1) BEGIN
						SET XP_value = 1
					END
					SET_2DA_ENTRY cnt cnt2 cntcol ~%XP_value%~
				END
			END
		END
	END ELSE BEGIN
		REPLACE_TEXTUALLY ~DISARM_TRAP.+$~ ~%DISARM_TRAP%~
		REPLACE_TEXTUALLY ~PICK_LOCK.+$~   ~%PICK_LOCK%~
		REPLACE_TEXTUALLY ~LEARN_SPELL.+$~ ~%LEARN_SPELL%~
	END
	PRETTY_PRINT_2DA
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust XP for killing creatures                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust XP for killing creatures~
//DESIGNATED 7
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt ca iwd_in_bg2 bgee bg2ee iwdee eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //150%
		OUTER_SET value = 150
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //75%
		OUTER_SET value = 75
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //50%
		OUTER_SET value = 50
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //25%
		OUTER_SET value = 25
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 5) BEGIN //10%
		OUTER_SET value = 10
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 6) BEGIN //Disable
		OUTER_SET value = 0
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 7) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 0) BEGIN
			PRINT ~Type in the integer percentage value:~
			ACTION_READLN ~value~
		END
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Adjust XP for killing creatures:
 1] Increase to 150%
 2] Decrease to 75%
 3] Decrease to 50%
 4] Decrease to 25%
 5] Decrease to 10%
 6] Disable XP for killing creatures
 7] Custom value~//%~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING_REGEXP GLOB ~.+\.CRE~ ~override~
	READ_LONG 0x14 "XP_value"
	PATCH_IF ("%XP_value%" > 0) BEGIN
		SET print = "%XP_value%"
		PATCH_IF ("%value%" > 0) BEGIN
			SET XP_value = ("%XP_value%" * "%value%" / 100)
			PATCH_IF ("%XP_value%" < 1) BEGIN
				SET XP_value = 1
			END
			WRITE_LONG 0x14 "%XP_value%"
			PATCH_PRINT ~Patching %SOURCE_FILE%: %print% => %XP_value%~
		END ELSE BEGIN
			WRITE_LONG 0x14 0
			PATCH_PRINT ~Patching %SOURCE_FILE%: %print% => 0~
		END
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust XP for quests                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust XP for quests~
//DESIGNATED 8
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt ca iwd_in_bg2 bgee bg2ee iwdee eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //150%
		OUTER_SET value = 150
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //75%
		OUTER_SET value = 75
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //50%
		OUTER_SET value = 50
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //25%
		OUTER_SET value = 25
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 5) BEGIN //10%
		OUTER_SET value = 10
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 6) BEGIN //Disable
		OUTER_SET value = 0
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 7) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 0) BEGIN
			PRINT ~Type in the integer percentage value:~
			ACTION_READLN ~value~
		END
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Adjust XP for quests:
 1] Increase to 150%
 2] Decrease to 75%
 3] Decrease to 50%
 4] Decrease to 25%
 5] Decrease to 10%
 6] Disable XP for quests
 7] Custom value~//%~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING ~XPLIST.2da~ ~override~
	COUNT_2DA_COLS "cntcol"
	COUNT_2DA_ROWS "%cntcol%" "cntrows"
	FOR (cnt = 0; cnt < "%cntrows%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 "%cntcol%" "name"
		PATCH_PRINT ~Analysing '%name%' XPLIST.2da row~
		FOR (cnt2 = 2; cnt2 < "%cntcol%"; cnt2 = cnt2 + 1) BEGIN
			READ_2DA_ENTRY cnt cnt2 "%cntcol%" "XP_value"
			SET add_prefix = 0
			SPRINT print "%XP_value%"
			PATCH_IF NOT (IS_AN_INT %XP_value%) BEGIN
				INNER_PATCH_SAVE ~XP_value~ ~%XP_value%~ BEGIN
					REPLACE_TEXTUALLY ~P_~ ~~
				END
				SET add_prefix = 1
			END
			PATCH_IF ("%XP_value%" > 0) BEGIN
				SET XP_value = ("%XP_value%" * "%value%" / 100)
				PATCH_IF ("%XP_value%" < 1) BEGIN
					SET XP_value = 1
				END
				PATCH_IF ("%add_prefix%" = 1) BEGIN
					INNER_PATCH_SAVE ~XP_value~ ~%XP_value%~ BEGIN
						REPLACE_TEXTUALLY ~^~ ~P_~
					END
				END
				SET_2DA_ENTRY cnt cnt2 cntcol ~%XP_value%~
				PATCH_PRINT ~Patching XPLIST.2da (row%cnt%, col%cnt2%): %print% => %XP_value%~
			END
		END
	END
	PRETTY_PRINT_2DA
BUT_ONLY

ACTION_FOR_EACH ext IN BCS DLG BEGIN
	COPY_EXISTING_REGEXP GLOB ~.+\.%ext%~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_EVALUATE CASE_INSENSITIVE ~AddexperienceParty(\([1-9][0-9]*\))~ BEGIN
				SET "XP_value" = "%MATCH1%"
				PATCH_IF ("%XP_value%" > 0) BEGIN
					SET XP_value = ("%XP_value%" * "%value%" / 100)
					PATCH_IF ("%XP_value%" < 1) BEGIN
						SET XP_value = 1
					END
					PATCH_PRINT ~Patching %SOURCE_FILE%: %MATCH1% => %XP_value%~
				END
			END
			~AddexperienceParty(%XP_value%)~
			REPLACE_EVALUATE CASE_INSENSITIVE ~AddXPObject(\(.*\),\([1-9][0-9]*\))~ BEGIN
				SET "XP_value" = "%MATCH2%"
				PATCH_IF ("%XP_value%" > 0) BEGIN
					SET XP_value = ("%XP_value%" * "%value%" / 100)
					PATCH_IF ("%XP_value%" < 1) BEGIN
						SET XP_value = 1
					END
					PATCH_PRINT ~Patching %SOURCE_FILE%: %MATCH2% => %XP_value%~
				END
			END
			~AddXPObject(%MATCH1%,%XP_value%)~
			REPLACE_EVALUATE CASE_INSENSITIVE ~ChangeStat(\(.*\),XP,\([1-9][0-9]*\),ADD)~ BEGIN
				SET "XP_value" = "%MATCH2%"
				PATCH_IF ("%XP_value%" > 0) BEGIN
					SET XP_value = ("%XP_value%" * "%value%" / 100)
					PATCH_IF ("%XP_value%" < 1) BEGIN
						SET XP_value = 1
					END
					PATCH_PRINT ~Patching %SOURCE_FILE%: %MATCH2% => %XP_value%~
				END
			END
			~ChangeStat(%MATCH1%,XP,%XP_value%,ADD)~
			REPLACE_TEXTUALLY ~\(Allegiance(.*,\)ANYONE)~ ~\10)~//workaround for weidu compiler bug
		END
	BUT_ONLY
END

COPY_EXISTING_REGEXP GLOB ~.+\.SPL~ ~override~
	READ_LONG 0x64 "abil_off" ELSE 0
	READ_SHORT 0x68 "abil_num" ELSE 0
	READ_LONG 0x6a "fx_off" ELSE 0
	FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
		READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
		READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
		FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
			SET "offset" = ("%fx_off%" + (0x30 * ("%abil_fx_idx%" + "%index2%")))
			READ_SHORT "%offset%" "opcode"
			PATCH_IF ("%opcode%" = 104) BEGIN //XP bonus
				READ_LONG ("%offset%" + 0x4) "XP_value" //Value
				READ_LONG ("%offset%" + 0x8) "XP_type" //Modifier type
				PATCH_IF ((XP_value > 0) AND (XP_type = 0)) BEGIN
					SET print = "%XP_value%"
					SET XP_value = ("%XP_value%" * "%value%" / 100)
					PATCH_IF ("%XP_value%" < 1) BEGIN
						SET XP_value = 1
					END
					WRITE_LONG ("%offset%" + 0x4) "%XP_value%"
					PATCH_PRINT ~Patching %SOURCE_FILE%: %print% => %XP_value%~
				END
			END
		END
	END
	READ_SHORT 0x70 "fx_num" ELSE 0
	FOR (index3 = 0 ; index3 < fx_num ; index3 = index3 + 1) BEGIN
		SET "offset" = ("%fx_off%" + (0x30 * "%index3%"))
		READ_SHORT "%offset%" "opcode"
		PATCH_IF ("%opcode%" = 104) BEGIN //XP bonus
			READ_LONG ("%offset%" + 0x4) "XP_value" //Value
			READ_LONG ("%offset%" + 0x8) "XP_type" //Modifier type
			PATCH_IF ((XP_value > 0) AND (XP_type = 0)) BEGIN
				SET print = "%XP_value%"
				SET XP_value = ("%XP_value%" * "%value%" / 100)
				PATCH_IF ("%XP_value%" < 1) BEGIN
					SET XP_value = 1
				END
				WRITE_LONG ("%offset%" + 0x4) "%XP_value%"
				PATCH_PRINT ~Patching %SOURCE_FILE%: %print% => %XP_value%~
			END
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.+\.ITM~ ~override~
	READ_LONG 0x64 "abil_off" ELSE 0
	READ_SHORT 0x68 "abil_num" ELSE 0
	READ_LONG 0x6a "fx_off" ELSE 0
	FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
		READ_SHORT ("%abil_off%" + 0x1e + (0x38 * "%index%")) "abil_fx_num"
		READ_SHORT ("%abil_off%" + 0x20 + (0x38 * "%index%")) "abil_fx_idx"
		FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
			SET "offset" = ("%fx_off%" + (0x30 * ("%abil_fx_idx%" + "%index2%")))
			READ_SHORT "%offset%" "opcode"
			PATCH_IF ("%opcode%" = 104) BEGIN //XP bonus
				READ_LONG ("%offset%" + 0x4) "XP_value" //Value
				READ_LONG ("%offset%" + 0x8) "XP_type" //Modifier type
				PATCH_IF ((XP_value > 0) AND (XP_type = 0)) BEGIN
					SET print = "%XP_value%"
					SET XP_value = ("%XP_value%" * "%value%" / 100)
					PATCH_IF ("%XP_value%" < 1) BEGIN
						SET XP_value = 1
					END
					WRITE_LONG ("%offset%" + 0x4) "%XP_value%"
					PATCH_PRINT ~Patching %SOURCE_FILE%: %print% => %XP_value%~
				END
			END
		END
	END
	READ_SHORT 0x70 "fx_num" ELSE 0
	FOR (index3 = 0 ; index3 < fx_num ; index3 = index3 + 1) BEGIN
		SET "offset" = ("%fx_off%" + (0x30 * "%index3%"))
		READ_SHORT "%offset%" "opcode"
		PATCH_IF ("%opcode%" = 104) BEGIN //XP bonus
			READ_LONG ("%offset%" + 0x4) "XP_value" //Value
			READ_LONG ("%offset%" + 0x8) "XP_type" //Modifier type
			PATCH_IF ((XP_value > 0) AND (XP_type = 0)) BEGIN
				SET print = "%XP_value%"
				SET XP_value = ("%XP_value%" * "%value%" / 100)
				PATCH_IF ("%XP_value%" < 1) BEGIN
					SET XP_value = 1
				END
				WRITE_LONG ("%offset%" + 0x4) "%XP_value%"
				PATCH_PRINT ~Patching %SOURCE_FILE%: %print% => %XP_value%~
			END
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.+\.EFF~ ~override~
	READ_LONG 0x10 "opcode"
	PATCH_IF ("%opcode%" = 104) BEGIN //XP bonus
		READ_LONG 0x1c "XP_value" //Value
		READ_LONG 0x20 "XP_type" //Modifier type
		PATCH_IF ((XP_value > 0) AND (XP_type = 0)) BEGIN
			SET print = "%XP_value%"
			SET XP_value = ("%XP_value%" * "%value%" / 100)
			PATCH_IF ("%XP_value%" < 1) BEGIN
				SET XP_value = 1
			END
			WRITE_LONG 0x4 "%XP_value%"
			PATCH_PRINT ~Patching %SOURCE_FILE%: %print% => %XP_value%~
		END
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Party XP for quests distributed individually     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Party XP for quests distributed individually~
//DESIGNATED 9
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt ca iwd_in_bg2 bgee bg2ee iwdee eet~ ~This component is not compatible with your game.~

COPY_EXISTING ~XPLIST.2da~ ~override~
	COUNT_2DA_COLS "cntcol"
	COUNT_2DA_ROWS "%cntcol%" "cntrows"
	FOR (cnt = 0; cnt < "%cntrows%"; cnt = cnt + 1) BEGIN
		READ_2DA_ENTRY cnt 0 "%cntcol%" "name"
		PATCH_PRINT ~Analysing '%name%' XPLIST.2da row~
		FOR (cnt2 = 2; cnt2 < "%cntcol%"; cnt2 = cnt2 + 1) BEGIN
			READ_2DA_ENTRY cnt cnt2 "%cntcol%" "XP_value"
			PATCH_IF NOT (IS_AN_INT %XP_value%) BEGIN
				SPRINT print "%XP_value%"
				INNER_PATCH_SAVE ~XP_value~ ~%XP_value%~ BEGIN
					REPLACE_TEXTUALLY ~P_~ ~~
				END
				PATCH_IF ("%XP_value%" > 0) BEGIN
					SET XP_value = ("%XP_value%" / 6)
					PATCH_IF ("%XP_value%" < 1) BEGIN
						SET XP_value = 1
					END
					SET_2DA_ENTRY cnt cnt2 cntcol ~%XP_value%~
					PATCH_PRINT ~Patching XPLIST.2da (row%cnt%, col%cnt2%): %print% => %XP_value%~
				END
			END
		END
	END
	PRETTY_PRINT_2DA
BUT_ONLY

ACTION_FOR_EACH ext IN BCS DLG BEGIN
	COPY_EXISTING_REGEXP GLOB ~.+\.%ext%~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_EVALUATE CASE_INSENSITIVE ~AddexperienceParty(\([1-9][0-9]*\))~ BEGIN
				SET "XP_value" = "%MATCH1%"
				PATCH_IF ("%XP_value%" > 0) BEGIN
					SET XP_value = ("%XP_value%" / 6)
					PATCH_IF ("%XP_value%" < 1) BEGIN
						SET XP_value = 1
					END
					PATCH_PRINT ~Patching %SOURCE_FILE%: %MATCH1% => %XP_value% (for Player1-6)~
				END
			END
			~AddXPObject(Player1,%XP_value%)
    AddXPObject(Player2,%XP_value%)
    AddXPObject(Player3,%XP_value%)
    AddXPObject(Player4,%XP_value%)
    AddXPObject(Player5,%XP_value%)
    AddXPObject(Player6,%XP_value%)~
			REPLACE_TEXTUALLY ~\(Allegiance(.*,\)ANYONE)~ ~\10)~//workaround for weidu compiler bug
		END
	BUT_ONLY
END

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// 3E Thief Sneak Attack                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~3E Thief Sneak Attack~
//DESIGNATED 10
REQUIRE_PREDICATE GAME_IS ~eet bg2ee~ ~This component is not compatible with your game.~

LOAD_TRA ~EET_Tweaks/lang/%LANGUAGE%/%COMPONENT_NUMBER%.tra~

ACTION_IF (FILE_EXISTS ~%USER_DIRECTORY%/Baldur.ini~) BEGIN
	COPY ~%USER_DIRECTORY%/Baldur.ini~ ~%USER_DIRECTORY%~
		COUNT_REGEXP_INSTANCES ~'3E Thief Sneak Attack'~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~\('3E Thief Sneak Attack',[ 	]+\)'.'~ ~\1'1'~
		END ELSE BEGIN
			REPLACE_TEXTUALLY ~INSERT INTO options ROWS (~ ~INSERT INTO options ROWS (
	'Game Options',	'3E Thief Sneak Attack',	'1',~
		END
	BUT_ONLY
END ELSE BEGIN
<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/Baldur.ini
CREATE TABLE options (
	section string,
	name string,
	value string
);
INSERT INTO options ROWS (
	'Game Options',	'3E Thief Sneak Attack',	'1'
);
>>>>>>>>
	COPY ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/Baldur.ini~ ~%USER_DIRECTORY%/Baldur.ini~ EVALUATE_BUFFER
END

ACTION_BASH_FOR ~EET_Tweaks/%COMPONENT_NUMBER%~ ~.*\.2DA$~ BEGIN
	ACTION_IF NOT FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~ BEGIN
		COPY ~%BASH_FOR_FILESPEC%~ ~override~
	END
END

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/BACKSTAB.SPL~ ~override~

STRING_SET ~3042~ @0 //change "The backstab seems to have failed." to "Successful Sneak Attack"
//STRING_SET ~~ @1 //failed sneak attack message
STRING_SET ~9561~ @2 //Thief kit description
STRING_SET ~25211~ @3 //Stalker kit description
STRING_SET ~25213~ @4 //Assassin kit description
STRING_SET ~25216~ @5 //Swashbuckler kit description
STRING_SET ~74298~ @6 //Shadowdancer kit description

//10013 //Your weapon is unsuitable for backstab.
//12128 //Backstab Damage
//16471 //Backstab Double Damage
//16472 //Backstab Triple Damage
//16473 //Backstab Quadruple Damage
//16474 //Backstab Quintuple Damage
//16475 //Backstab Sextuple Damage
//74094 //Backstab Septuple Damage
//74095 //Backstab Octuple Damage
//74096 //Backstab Nonuple Damage

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust FPS and fix audio skipping in cutscenes   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust FPS and fix audio skipping in cutscenes~
//DESIGNATED 11
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt ca iwd_in_bg2 bgee bg2ee iwdee eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //60
		OUTER_SET value = 60
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //45
		OUTER_SET value = 45
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% <= 30) OR (%value% > 60) BEGIN
			PRINT ~Type in the FPS vakue (30+ up to 60):~
			ACTION_READLN ~value~
		END
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Choose Maximum Frame Rate:
 1] 60 FPS
 2] 45 FPS
 3] Custom value~//%~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

ACTION_IF (FILE_EXISTS ~%USER_DIRECTORY%/Baldur.ini~) BEGIN
	COPY ~%USER_DIRECTORY%/Baldur.ini~ ~%USER_DIRECTORY%~
		PATCH_IF ENGINE_IS ~bgee bg2ee iwdee~ BEGIN
			COUNT_REGEXP_INSTANCES ~'Maximum Frame Rate'~ num_matches
			PATCH_IF (num_matches > 0) BEGIN
				REPLACE_TEXTUALLY ~\('Maximum Frame Rate',[ 	]+\)'[0-9]+'~ ~\1'%value%'~
			END ELSE BEGIN
				REPLACE_TEXTUALLY ~INSERT INTO options ROWS (~ ~INSERT INTO options ROWS (
		'Program Options',	'Maximum Frame Rate',	'%value%',~
			END
		END ELSE BEGIN
			//patch ini file for non EE games
		END
	BUT_ONLY
END ELSE ACTION_IF ENGINE_IS ~bgee bg2ee iwdee~ BEGIN
<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/Baldur.ini
CREATE TABLE options (
	section string,
	name string,
	value string
);
INSERT INTO options ROWS (
	'Program Options',	'Maximum Frame Rate',	'%value%'
);
>>>>>>>>
	COPY ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/Baldur.ini~ ~%USER_DIRECTORY%/Baldur.ini~ EVALUATE_BUFFER
END ELSE BEGIN
	//create ini file for non EE games
END

//additional delay. Unused, but can be set for languages that have many problems with cutting sounds even in 30 FPS because they are longer than English ones
/*ACTION_IF ("%LANGUAGE%" STRING_EQUAL_CASE ~pl_PL~) BEGIN
	OUTER_SET lang_val = 10
END*/

//Below code will update timers depending on the chosen FPS and should fix most of the issues for English audio
//updates only Wait and SmallWait 1 line below DisplayStringX actions, so it shouldn't slow the game in other places
COPY_EXISTING_REGEXP GLOB ~^.+\.BCS$~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(DisplayString.+[%newline%]+\)\(.*\)Wait(\([0-9]+\))\(.*\)~ BEGIN
			SET time = "%MATCH3%"
			SPRINT new_MATCH2 "%MATCH2%"
			PATCH_IF "%MATCH2%" STRING_MATCHES_REGEXP ".*Small$" = 0 BEGIN
				SET time = ("%time%" * "%value%" / 30)
				/*PATCH_IF VARIABLE_IS_SET "%lang_val%" BEGIN
					SET lang_add = ("%time%" / "%lang_val%")
					SET time = ("%time%" + "%lang_add%")
				END*/
				INNER_PATCH_SAVE ~new_MATCH2~ ~%new_MATCH2%~ BEGIN
					REPLACE_TEXTUALLY ~Small$~ ~~
				END
			END ELSE BEGIN
				SET time = ("%time%" * 15 * "%value%" / 30)
				/*PATCH_IF VARIABLE_IS_SET "%lang_val%" BEGIN
					SET lang_add = ("%time%" / "%lang_val%")
					SET time = ("%time%" + "%lang_add%")
				END*/
			END
			PATCH_IF ("%time%" < 1) BEGIN
				SET time = 1
			END
			PATCH_PRINT ~Patching %SOURCE_FILE%: %MATCH1%%MATCH2%Wait(%MATCH3%)%MATCH4% => %new_MATCH2%SmallWait(%time%)%MATCH4%~
		END
		~%MATCH1%%new_MATCH2%SmallWait(%time%)%MATCH4%~
		REPLACE_TEXTUALLY ~\(Allegiance(.*,\)ANYONE)~ ~\10)~//workaround for weidu compiler bug
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// More bandit scalps                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~More bandit scalps~
//DESIGNATED 12
REQUIRE_PREDICATE GAME_IS ~bgee eet~ ~This component is not compatible with your game.~
FORBID_COMPONENT ~Setup-BGTTweak.tp2~ 1400 ~Component already installed via BGT Tweaks~

ACTION_FOR_EACH cre IN britik credus hakt raiken taugos tersus teven vax venkt zal BEGIN
	COPY_EXISTING ~%cre%.cre~ ~override~
		READ_LONG 0x2b8 itemslot_off
		READ_LONG 0x2bc item_off
		READ_LONG 0x2c0 item_num
		SET item_found = 0
		FOR (i = 0; i < %item_num%; i +=1) BEGIN
			READ_ASCII (%item_off% + %i% * 0x14) item_name
			PATCH_IF (("%item_name%" STRING_COMPARE_REGEXP "MISC86.*")=0) BEGIN
				FOR (j = 0; j < 37; j += 1) BEGIN
					READ_SHORT (%itemslot_off% + %j% * 0x2) itemslot_idx
					PATCH_IF %itemslot_idx% = %i% BEGIN
						SET item_found = 1
					END
				END
			END
		END
		PATCH_IF item_found = 0 BEGIN
			ADD_CRE_ITEM ~MISC86_~ #0 #0 #0 ~UNSTEALABLE~ ~INV~
		END
	BUT_ONLY
END

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Put Sword of Chaos +2 in Sarevok's inventory     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Put Sword of Chaos +2 in Sarevok's inventory~
//DESIGNATED 13
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~
FORBID_COMPONENT ~Setup-BGTTweak.tp2~ 2700 ~Component already installed via BGT Tweaks~

//check if Sarevok already has the sword
COPY_EXISTING ~sarevo.cre~ ~override/sarevo.cre~
	READ_LONG 0x2b8 itemslot_off
	READ_LONG 0x2bc item_off
	READ_LONG 0x2c0 item_num
	SET item_found = 0
	FOR (i = 0; i < %item_num%; i +=1) BEGIN
		READ_ASCII (%item_off% + %i% * 0x14) item_name
		PATCH_IF ("%item_name%" STRING_EQUAL_CASE "SW2H16") BEGIN
			FOR (j = 0; j < 37; j += 1) BEGIN
				READ_SHORT (%itemslot_off% + %j% * 0x2) itemslot_idx
				PATCH_IF %itemslot_idx% = %i% BEGIN
					SET item_found = 1
				END
			END
		END
	END
	PATCH_IF item_found = 0 BEGIN
		ADD_CRE_ITEM ~SW2H16~ #0 #0 #0 ~UNSTEALABLE~ ~INV~
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Protagonist can die                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Protagonist can die~
//DESIGNATED 14
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet~ ~This component is not compatible with your game.~

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
	READ_BYTE 0x14 "flags"
	WRITE_BYTE 0x14 ("%flags%" BOR 0b00010000)
BUT_ONLY

//Ironman Mode detection scrapped for now
/*ACTION_IF MOD_IS_INSTALLED ~EET_Tweaks/EET_Tweaks.tp2~ ~15~ BEGIN
	OUTER_SPRINT save_eval ~SaveGame(0)~
END ELSE BEGIN
	OUTER_SPRINT save_eval ~~
END*/

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/baldur-et.baf
IF
  OR(3)
    NumInPartyAlive(0)
    StateCheck(Player1,STATE_EXPLODING_DEATH)
    StateCheck(Player1,STATE_FROZEN_DEATH)
THEN
  RESPONSE #100
	//%save_eval%
    GameOver(19377)
END
>>>>>>>>

ACTION_FOR_EACH script IN BALDUR BALDUR25 BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%script%.BCS~ BEGIN
		EXTEND_TOP ~%script%.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/baldur-et.baf~ EVALUATE_BUFFER
	END
END

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Disable item shattering for enemies              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Disable item shattering for enemies~
//DESIGNATED 15
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#SHAT-et.baf
IF
  !InParty(LastSummonerOf(Myself))
THEN
  RESPONSE #100
    DestroySelf()
END
>>>>>>>>

ACTION_FOR_EACH file IN K#SHATWE K#SHATSH K#SHATAR K#SHATHE BEGIN
	EXTEND_TOP ~%file%.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#SHAT-et.baf~
END

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust BG1 weapon shattering chance              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust BG1 weapon shattering~
//DESIGNATED 16
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //1%
		OUTER_SET value = 10
		OUTER_SET response = 90
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //0.5%
		OUTER_SET value = 5
		OUTER_SET response = 95
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //0.2%
		OUTER_SET value = 2
		OUTER_SET response = 98
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //0.1%
		OUTER_SET value = 1
		OUTER_SET response = 99
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 5) BEGIN //Disable
		OUTER_SET value = 0
		OUTER_SET response = 100
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 6) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 0) OR (%value% > 100) BEGIN
			PRINT ~Type in the integer value from 0 to 100 (it will be divided by 10, for example: 1 = 0.1%, and 100 = 10%)~
			ACTION_READLN ~value~
		END
		OUTER_SET response = (100 - value)
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Adjust shattering chance:
 1] 1% (default BG1 value)
 2] 0.5% (default EET value)
 3] 0.2%
 4] 0.1%
 5] Disable weapon shattering
 6] Custom value~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING ~K#SHATWE.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~RESPONSE #\([0-9]+\)[%newline%]*DestroySelf()[%newline%]*RESPONSE #\([0-9]+\)~ BEGIN
			PATCH_IF ("%MATCH1%" != "%value%") BEGIN
				PATCH_PRINT ~Patching %SOURCE_FILE%: Shattering chance = (%MATCH1% / 10) => (%value% / 10)~
			END ELSE BEGIN
				PATCH_PRINT ~No changes needed. Shattering chance already set to (%value% / 10)~
			END
		END
		~RESPONSE #%response%
    DestroySelf()
  RESPONSE #%value%~
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust BG1 shield shattering chance              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust BG1 shield shattering~
//DESIGNATED 17
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //1%
		OUTER_SET value = 10
		OUTER_SET response = 90
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //0.5%
		OUTER_SET value = 5
		OUTER_SET response = 95
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //0.2%
		OUTER_SET value = 2
		OUTER_SET response = 98
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //0.1%
		OUTER_SET value = 1
		OUTER_SET response = 99
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 5) BEGIN //Disable
		OUTER_SET value = 0
		OUTER_SET response = 100
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 6) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 0) OR (%value% > 100) BEGIN
			PRINT ~Type in the integer value from 0 to 100 (it will be divided by 10, for example: 1 = 0.1%, and 100 = 10%)~
			ACTION_READLN ~value~
		END
		OUTER_SET response = (100 - value)
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Adjust shattering chance:
 1] 1%
 2] 0.5%
 3] 0.2% (default EET value)
 4] 0.1%
 5] Disable shield shattering
 6] Custom value~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING ~K#SHATSH.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~RESPONSE #\([0-9]+\)[%newline%]*DestroySelf()[%newline%]*RESPONSE #\([0-9]+\)~ BEGIN
			PATCH_IF ("%MATCH1%" != "%value%") BEGIN
				PATCH_PRINT ~Patching %SOURCE_FILE%: Shattering chance = (%MATCH1% / 10) => (%value% / 10)~
			END ELSE BEGIN
				PATCH_PRINT ~No changes needed. Shattering chance already set to (%value% / 10)~
			END
		END
		~RESPONSE #%response%
    DestroySelf()
  RESPONSE #%value%~
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust BG1 armor shattering chance               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust BG1 armor shattering~
//DESIGNATED 18
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //1%
		OUTER_SET value = 10
		OUTER_SET response = 90
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //0.5%
		OUTER_SET value = 5
		OUTER_SET response = 95
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //0.2%
		OUTER_SET value = 2
		OUTER_SET response = 98
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //0.1%
		OUTER_SET value = 1
		OUTER_SET response = 99
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 5) BEGIN //Disable
		OUTER_SET value = 0
		OUTER_SET response = 100
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 6) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 0) OR (%value% > 100) BEGIN
			PRINT ~Type in the integer value from 0 to 100 (it will be divided by 10, for example: 1 = 0.1%, and 100 = 10%)~
			ACTION_READLN ~value~
		END
		OUTER_SET response = (100 - value)
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Adjust shattering chance:
 1] 1%
 2] 0.5%
 3] 0.2% (default EET value)
 4] 0.1%
 5] Disable armor shattering
 6] Custom value~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING ~K#SHATAR.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~RESPONSE #\([0-9]+\)[%newline%]*DestroySelf()[%newline%]*RESPONSE #\([0-9]+\)~ BEGIN
			PATCH_IF ("%MATCH1%" != "%value%") BEGIN
				PATCH_PRINT ~Patching %SOURCE_FILE%: Shattering chance = (%MATCH1% / 10) => (%value% / 10)~
			END ELSE BEGIN
				PATCH_PRINT ~No changes needed. Shattering chance already set to (%value% / 10)~
			END
		END
		~RESPONSE #%response%
    DestroySelf()
  RESPONSE #%value%~
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust BG1 helmet shattering chance              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust BG1 helmet shattering~
//DESIGNATED 19
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //1%
		OUTER_SET value = 10
		OUTER_SET response = 90
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //0.5%
		OUTER_SET value = 5
		OUTER_SET response = 95
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //0.2%
		OUTER_SET value = 2
		OUTER_SET response = 98
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 4) BEGIN //0.1%
		OUTER_SET value = 1
		OUTER_SET response = 99
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 5) BEGIN //Disable
		OUTER_SET value = 0
		OUTER_SET response = 100
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 6) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 0) OR (%value% > 100) BEGIN
			PRINT ~Type in the integer value from 0 to 100 (it will be divided by 10, for example: 1 = 0.1%, and 100 = 10%)~
			ACTION_READLN ~value~
		END
		OUTER_SET response = (100 - value)
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Adjust shattering chance:
 1] 1%
 2] 0.5%
 3] 0.2%
 4] 0.1% (default EET value)
 5] Disable helmet shattering
 6] Custom value~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

COPY_EXISTING ~K#SHATHE.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~RESPONSE #\([0-9]+\)[%newline%]*DestroySelf()[%newline%]*RESPONSE #\([0-9]+\)~ BEGIN
			PATCH_IF ("%MATCH1%" != "%value%") BEGIN
				PATCH_PRINT ~Patching %SOURCE_FILE%: Shattering chance = (%MATCH1% / 10) => (%value% / 10)~
			END ELSE BEGIN
				PATCH_PRINT ~No changes needed. Shattering chance already set to (%value% / 10)~
			END
		END
		~RESPONSE #%response%
    DestroySelf()
  RESPONSE #%value%~
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Disable hostile reaction after charm             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Disable hostile reaction after charm~
//DESIGNATED 20
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt ca iwd_in_bg2 bgee bg2ee iwdee eet~ ~This component is not compatible with your game.~
FORBID_COMPONENT ~Setup-BGTTweak.tp2~ 2300 ~Component already installed via BGT Tweaks~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
	~^.+\.spl$~ ~override~
	PATCH_IF (("%SOURCE_RES%" STRING_EQUAL_CASE "spwi939")=0) AND ("%SOURCE_SIZE%" > 0x71) BEGIN
		READ_LONG 0x64 abil_off
		READ_SHORT 0x68 abil_num
		READ_LONG 0x6a fx_off
		PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.spl$" = 0) BEGIN
			SET "abil_length" = 0x28
		END ELSE BEGIN
			SET "abil_length" = 0x38
		END
		FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
			READ_SHORT ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "abil_fx_num"
			READ_SHORT ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) "abil_fx_idx"
			FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
				READ_SHORT ("%fx_off%" + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
				PATCH_IF ("%opcode%" = 5) BEGIN // Charm Specific Creature
					READ_BYTE ("%fx_off%" + 0x8 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "charm_type"
					PATCH_IF ("%charm_type%" = 3) BEGIN
						WRITE_BYTE ("%fx_off%" + 0x8 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) 2
					END ELSE PATCH_IF ("%charm_type%" = 1) BEGIN
						WRITE_BYTE ("%fx_off%" + 0x8 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) 0
					END
				END
			END
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.eff$~ ~override~
	PATCH_IF ("%SOURCE_SIZE%" > 0x109) BEGIN
		READ_SHORT 0x10 "opcode"
		PATCH_IF "%opcode%" = 5 BEGIN // Charm Specific Creature
			READ_BYTE 0x20 "charm_type"
			PATCH_IF ("%charm_type%" = 3) BEGIN
				WRITE_BYTE 0x20 2
			END ELSE PATCH_IF ("%charm_type%" = 1) BEGIN
				WRITE_BYTE 0x20 0
			END
		END
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust familiar death consequences               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Adjust familiar death consequences~
//DESIGNATED 21
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~

OUTER_WHILE (valid_answer = 0) BEGIN
	ACTION_IF (button = 1) BEGIN //Constitution loss for 1 week + blocked familiar summoning for 1 week
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 2) BEGIN //Constitution loss for 1 week
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button = 3) BEGIN //Disable Constitution loss
		OUTER_SET valid_answer = 1
	END ELSE ACTION_IF (button > 3) AND (button < 6) BEGIN //Custom value
		OUTER_WHILE NOT (IS_AN_INT %value%) OR (%value% < 0) BEGIN
			PRINT ~Type in the integer value that represents how many days should penalties last~
			ACTION_READLN ~value~
		END
		OUTER_SET time = (value * 7200)//7200 = 1 day
		OUTER_SET logCustom = 1
		OUTER_SET valid_answer = 1
	END ELSE BEGIN
		PRINT ~Adjust familiar death consequences (permanent 1 point Constitution loss by default):
 1] Constitution loss for 1 week + blocked familiar summoning for 1 week
 2] Constitution loss for 1 week
 3] Disable Constitution loss
 4] Option 1 custom time
 5] Option 2 custom time~
		ACTION_READLN ~button~
		ACTION_IF NOT (IS_AN_INT %button%) BEGIN
			OUTER_SET button = 0
		END
	END
END

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#FAMSUM-et.baf
IF
  GlobalTimerNotExpired("K#TimerFamiliar","GLOBAL")
THEN
  RESPONSE #100
    //DisplayStringNoName(Myself,)
	VerbalConstant(Player1,SPELL_DISRUPTED)
    DestroySelf()
END
>>>>>>>>

COPY_EXISTING ~K#FAMKIL.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		PATCH_IF (button = 1) BEGIN //Constitution loss for 1 week + blocked familiar summoning for 1 week
			REPLACE_TEXTUALLY ~ActionOverride(Player1,DisplayString(Myself,21004))~ ~SetGlobalTimer("K#TimerFamiliar","GLOBAL",50400)~
			REPLACE_TEXTUALLY ~ChangeStat(Player1,CON,-1,ADD)~ ~ActionOverride(Player1,ApplySpellRES("K#FAMCON",Myself))~
			INNER_ACTION BEGIN
				COPY ~EET_Tweaks/%COMPONENT_NUMBER%/K#FAMCON.SPL~ ~override~
				EXTEND_TOP ~K#FAMSUM.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#FAMSUM-et.baf~
			END
		END ELSE PATCH_IF (button = 2) BEGIN //Constitution loss for 1 week
			REPLACE_TEXTUALLY ~ActionOverride(Player1,DisplayString(Myself,21004))~ ~~
			REPLACE_TEXTUALLY ~ChangeStat(Player1,CON,-1,ADD)~ ~ActionOverride(Player1,ApplySpellRES("K#FAMCON",Myself))~
			INNER_ACTION BEGIN
				COPY ~EET_Tweaks/%COMPONENT_NUMBER%/K#FAMCON.SPL~ ~override~
			END
		END ELSE PATCH_IF (button = 3) BEGIN //Disable Constitution loss
			REPLACE_TEXTUALLY ~ActionOverride(Player1,DisplayString(Myself,21004))~ ~~
			REPLACE_TEXTUALLY ~ChangeStat(Player1,CON,-1,ADD)~ ~~
		END ELSE PATCH_IF (button = 4) BEGIN //Option 1 custom time
			REPLACE_TEXTUALLY ~ActionOverride(Player1,DisplayString(Myself,21004))~ ~SetGlobalTimer("K#TimerFamiliar","GLOBAL",%time%)~
			REPLACE_TEXTUALLY ~ChangeStat(Player1,CON,-1,ADD)~ ~ActionOverride(Player1,ApplySpellRES("K#FAMCON",Myself))~
			INNER_ACTION BEGIN
				COPY ~EET_Tweaks/%COMPONENT_NUMBER%/K#FAMCON.SPL~ ~override~
					WRITE_LONG 0xa8 ~%time%~
				EXTEND_TOP ~K#FAMSUM.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/K#FAMSUM-et.baf~
			END
		END ELSE PATCH_IF (button = 5) BEGIN //Option 2 custom time
			REPLACE_TEXTUALLY ~ActionOverride(Player1,DisplayString(Myself,21004))~ ~~
			REPLACE_TEXTUALLY ~ChangeStat(Player1,CON,-1,ADD)~ ~ActionOverride(Player1,ApplySpellRES("K#FAMCON",Myself))~
			INNER_ACTION BEGIN
				COPY ~EET_Tweaks/%COMPONENT_NUMBER%/K#FAMCON.SPL~ ~override~
					WRITE_LONG 0xa8 ~%time%~
			END
		END
	END
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reserved slot for future tweak                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Reserved slot for future tweak~
//DESIGNATED 22
REQUIRE_PREDICATE GAME_IS ~eet~ ~This component is not compatible with your game.~
DEPRECATED ~Nothing to see here for now~

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD:EE soundsets                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~IWD:EE soundsets~
//DESIGNATED 23
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ ~This component is not compatible with your game.~

INCLUDE ~EET_Tweaks/lib/iwdee_dir.tph~

ACTION_IF FILE_EXISTS ~%iwdee_dir%/weidu.conf~ BEGIN
	COPY - ~%iwdee_dir%/weidu.conf~ ~%iwdee_dir%/weidu.conf~
		READ_2DA_ENTRY 0 2 3 "LANGUAGE_IWDEE"
END

ACTION_IF (FILE_EXISTS ~%iwdee_dir%/lang/%LANGUAGE%/sounds/FEMALE1_.wav~) BEGIN
	OUTER_SPRINT LANGUAGE_IWDEE ~%LANGUAGE%~
END ELSE ACTION_IF (VARIABLE_IS_SET ~LANGUAGE_IWDEE~) AND (FILE_EXISTS ~%iwdee_dir%/lang/%LANGUAGE_IWDEE%/sounds/FEMALE1_.wav~) BEGIN
	//dummy check
END ELSE BEGIN
	OUTER_SPRINT LANGUAGE_IWDEE ~en_us~
END

PRINT ~LANGUAGE_IWDEE = %LANGUAGE_IWDEE%~

COPY - ~weidu.conf~ ~weidu.conf~
	READ_2DA_ENTRY 0 2 3 "LANGUAGE_BG2"
PRINT ~LANGUAGE_BG2 = %LANGUAGE_BG2%~

//extraction of TLK from IWD:EE

PRINT ~~
PRINT ~Extracting texts for translation from IWD:EE...~

ACTION_IF FILE_EXISTS ~%iwdee_dir%/lang/%LANGUAGE_IWDEE%/dialog.tlk~ BEGIN
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --game "%iwdee_dir%" --use-lang "%LANGUAGE_IWDEE%" --traify-tlk --out "EET_Tweaks/iwdee.tra"~
END ELSE BEGIN
	FAIL ~TLK for language: %LANGUAGE_IWDEE% not found in %iwdee_dir%~
END

LOAD_TRA ~EET_Tweaks/iwdee.tra~

//copy soundsets

MKDIR ~lang/%LANGUAGE_BG2%/sounds~

ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EETT_oldNameNewName" BEGIN
	~FEMALE1~ , ~40380~ => ~IWFeml1~
	~FEMALE2~ , ~40381~ => ~IWFeml2~
	~FEMALE3~ , ~40382~ => ~IWFeml3~
	~FFIGHT1~ , ~40375~ => ~FFight1~
	~FFIGHT2~ , ~40376~ => ~FFight2~
	~FFIGHT3~ , ~40377~ => ~FFight3~
	~FFIGHT4~ , ~40378~ => ~FFight4~
	~FFIGHT5~ , ~40379~ => ~FFight5~
	~FMAGE1~ , ~40383~ => ~FMage1~
	~FMAGE2~ , ~40384~ => ~FMage2~
	~FMAGE3~ , ~40385~ => ~FMage3~
	~FMAGE4~ , ~40386~ => ~FMage4~
	~FMAGE5~ , ~40387~ => ~FMage5~
	~FTHIEF1~ , ~40388~ => ~FThief1~
	~FTHIEF2~ , ~40389~ => ~FThief2~
	~MALE1~ , ~40390~ => ~IWMale1~
	~MALE2~ , ~40391~ => ~IWMale2~
	~MALE3~ , ~40392~ => ~IWMale3~
	~MFIGHT1~ , ~40393~ => ~MFight1~
	~MFIGHT2~ , ~40394~ => ~MFight2~
	~MFIGHT3~ , ~40395~ => ~MFight3~
	~MFIGHT4~ , ~40396~ => ~MFight4~
	~MFIGHT5~ , ~40397~ => ~MFight5~
	~MMAGE1~ , ~40398~ => ~MMage1~
	~MMAGE2~ , ~40399~ => ~MMage2~
	~MMAGE3~ , ~40400~ => ~MMage3~
	~MMAGE4~ , ~40401~ => ~MMage4~
	~MMAGE5~ , ~40402~ => ~MMage5~
	~MTHIEF1~ , ~40403~ => ~MThief1~
	~MTHIEF2~ , ~40404~ => ~MThief2~
END

APPEND ~BGEE.SQL~ ~
INSERT INTO filenames_stringrefs ROWS (
);~ UNLESS ~filenames_stringrefs~

ACTION_PHP_EACH "table_EETT_oldNameNewName" AS "oldName" => "newName" BEGIN
	ACTION_BASH_FOR ~%iwdee_dir%/lang/%LANGUAGE_IWDEE%/sounds~ ~^%oldName%.+\.WAV$~ BEGIN
		OUTER_SPRINT filename ~%BASH_FOR_RES%~
		OUTER_INNER_PATCH_SAVE ~filename~ ~%filename%~ BEGIN
			REPLACE_TEXTUALLY ~%oldName%~ ~%newName%~
		END
		COPY ~%BASH_FOR_FILESPEC%~ ~lang/%LANGUAGE_BG2%/sounds/%filename%.WAV~
	END
	COPY_EXISTING ~BGEE.SQL~ ~override~
		REPLACE_TEXTUALLY ~\(INSERT INTO filenames_stringrefs ROWS (\)~ ~\1
	'%newName%', 999%oldName_1%, 2,~
		REPLACE ~999%oldName_1%~ ( AT "oldName_1" )
	BUT_ONLY
END

//subtitles

ACTION_IF (FILE_EXISTS ~%iwdee_dir%/override/CHARSND.2DA~) BEGIN
	COPY ~%iwdee_dir%/override/CHARSND.2DA~ ~EET_Tweaks~
END ELSE BEGIN
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --game "%iwdee_dir%" --out "EET_Tweaks/temp" --biff-get-rest "CHARSND.2DA"~
END

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/CHARSND.txt
%index2%
>>>>>>>>

COPY - ~EET_Tweaks/temp/CHARSND.2DA~ ~EET_Tweaks~
	COUNT_2DA_COLS "cntcol"
	COUNT_2DA_ROWS (cntcol - 1) "cntrow"
	READ_2DA_ENTRIES_NOW table_charsnd (cntcol - 1)
	INNER_ACTION BEGIN
		COPY_EXISTING ~CHARSND.2DA~ ~override~
			PRETTY_PRINT_2DA
			COUNT_2DA_COLS "cntcol2"
			COUNT_2DA_ROWS cntcol2 "cntrow2"
			FOR (cnt1 = 0 ; cnt1 < cntcol2 - 1 ; cnt1 = cnt1 + 1) BEGIN
				INNER_ACTION BEGIN
					COPY - ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/CHARSND.txt~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/CHARSND.txt~
						REPLACE_TEXTUALLY ~\(index2.+\)~ ~\1 -1~
				END
			END
		BUT_ONLY
		OUTER_FOR (cnt2 = 2 ; cnt2 < cntcol - 1 ; cnt2 = cnt2 + 1) BEGIN
			COPY_EXISTING ~CHARSND.2DA~ ~override~//need to be copied each time to reload data for FILE_CONTAINS_EVALUATED
				READ_2DA_ENTRY_FORMER table_charsnd 0 cnt2 "name"
				PHP_EACH "table_EETT_oldNameNewName" AS "oldName" => "newName" BEGIN
					PATCH_IF ~%name%~ STRING_EQUAL_CASE ~%oldName%~ BEGIN
						SPRINT name ~%newName%~
					END
				END
				PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~CHARSND.2DA~ ~^%name%~)) BEGIN
					PATCH_PRINT ~Patching CHARSND.2DA: %name% subtitles~
					REPLACE_TEXTUALLY ~^\([ ]+MALE .+\)$~ ~\1 %name%~
					FOR (cnt3 = 0 ; cnt3 < cntrow2 ; cnt3 = cnt3 + 1) BEGIN
						READ_2DA_ENTRY cnt3 0 cntcol2 "index1"
						PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~EET_Tweaks/temp/CHARSND.2DA~ ~^%index1%~)) BEGIN
							REPLACE_TEXTUALLY ~^\(%index1% .+\)$~ ~\1 -1~
						END
					END
					FOR (cnt4 = 1; cnt4 < cntrow ; cnt4 = cnt4 + 1) BEGIN
						READ_2DA_ENTRY_FORMER table_charsnd cnt4 0 "index2"
						READ_2DA_ENTRY_FORMER table_charsnd cnt4 (cnt2 + 1) "string"
						PATCH_IF (NOT FILE_CONTAINS_EVALUATED (~CHARSND.2DA~ ~^%index2%~)) BEGIN
							APPEND_FILE ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/CHARSND.txt~ EVALUATE_BUFFER
						END
						PATCH_IF (string > 0) BEGIN
							REPLACE_TEXTUALLY ~^\(%index2% .+\)$~ ~\1 999%string%~
							REPLACE ~999%string%~ ( AT "string" )
						END ELSE BEGIN
							REPLACE_TEXTUALLY ~^\(%index2% .+\)$~ ~\1 %string%~
						END
					END
				END
			BUT_ONLY
		END
		COPY_EXISTING ~CHARSND.2DA~ ~override~
			PRETTY_PRINT_2DA
		BUT_ONLY
	END

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD:EE portraits                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~IWD:EE portraits~
//DESIGNATED 24
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ ~This component is not compatible with your game.~

INCLUDE ~EET_Tweaks/lib/iwdee_dir.tph~

MKDIR ~EET_Tweaks/temp/bmp~

ACTION_FOR_EACH file IN DMC_ DMF2_ DMF_ DMT_ EMC_ EMF2_ EMF_ EMT_ EMW_ GMT_ GMW_ HAMF_ HAMT_ HEMF_ HEMT_ HEMW_ HMB2_ HMB_ HMC2_ HMC3_ HMC4_ HMC_ HMF2_ HMF3_ HMF4_ HMF5_ HMF6_ HMF7_ HMF_ HMW2_ HMW3_ HMW4_ HMW_ 2MBAR1_ 2MHUM1_ 2MORC1_ DFF_ EFC_ EFF_ EFW2_ EFW_ GFW_ HAFF_ HAFT_ HEFB_ HEFC_ HEFF_ HEFW_ HFD_ HFF2_ HFF_ HFT2_ HFT_ HFW2_ HFW3_ HFW_ 2FBAR1_ 2FHUM4_ 2FORC1_ BEGIN
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --game "%iwdee_dir%" --out "EET_Tweaks/temp/bmp" --biff-get-rest "^%file%.\.bmp$"~
END

ACTION_BASH_FOR ~EET_Tweaks/temp/bmp~ ~^.+\.bmp$~ BEGIN
	OUTER_SPRINT filename ~%BASH_FOR_RES%~
	/*OUTER_INNER_PATCH_SAVE filename "%filename%" BEGIN
		REPLACE_TEXTUALLY "_G$" "_L"
	END*/
	ACTION_IF NOT FILE_EXISTS_IN_GAME ~%filename%.BMP~ BEGIN
		COPY ~%BASH_FOR_DIRECTORY%/%BASH_FOR_RES%.BMP~ ~override/%filename%.BMP~
	END ELSE BEGIN
		WARN ~%filename%.BMP already exists in game. Possible conflict with added portrait.~
	END
END

APPEND ~bgee.sql~ ~
INSERT INTO portraits ROWS
(
	'DMC_', 1,
	'DMF2_', 1,
	'DMF_', 1,
	'DMT_', 1,
	'EMC_', 1,
	'EMF2_', 1,
	'EMF_', 1,
	'EMT_', 1,
	'EMW_', 1,
	'GMT_', 1,
	'GMW_', 1,
	'HAMF_', 1,
	'HAMT_', 1,
	'HEMF_', 1,
	'HEMT_', 1,
	'HEMW_', 1,
	'HMB2_', 1,
	'HMB_', 1,
	'HMC2_', 1,
	'HMC3_', 1,
	'HMC4_', 1,
	'HMC_', 1,
	'HMF2_', 1,
	'HMF3_', 1,
	'HMF4_', 1,
	'HMF5_', 1,
	'HMF6_', 1,
	'HMF7_', 1,
	'HMF_', 1,
	'HMW2_', 1,
	'HMW3_', 1,
	'HMW4_', 1,
	'HMW_', 1,
    '2MBAR1_', 1,
    '2MHUM1_', 1,
    '2MORC1_', 1,
	'DFF_', 2,
	'EFC_', 2,
	'EFF_', 2,
	'EFW2_', 2,
	'EFW_', 2,
	'GFW_', 2,
	'HAFF_', 2,
	'HAFT_', 2,
	'HEFB_', 2,
	'HEFC_', 2,
	'HEFF_', 2,
	'HEFW_', 2,
	'HFD_', 2,
	'HFF2_', 2,
	'HFF_', 2,
	'HFT2_', 2,
	'HFT_', 2,
	'HFW2_', 2,
	'HFW3_', 2,
	'HFW_', 2,
	'2FBAR1_', 2,
	'2FHUM4_', 2,
	'2FORC1_', 2,
);~
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add Priest of Tyr kit for Priests                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Add Priest of Tyr kit for Priests~
//DESIGNATED 25
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ ~This component is not compatible with your game.~

LOAD_TRA ~EET_Tweaks/lang/%LANGUAGE%/%COMPONENT_NUMBER%.tra~

ACTION_IF GAME_IS ~bgee~ BEGIN
	OUTER_SET kit_biography = 15884
	OUTER_SET kit_briefdesc = 31255
END ELSE ACTION_IF GAME_IS ~bg2ee~ BEGIN
	OUTER_SET kit_biography = ~-1~
	OUTER_SET kit_briefdesc = 102495
END ELSE BEGIN //eet
	OUTER_SET kit_biography = 15884
	OUTER_SET kit_briefdesc = 102495
END

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN //add only missing data (kit is already there in patch 1.3, just not enabled for player)
<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/KITTABLE.2DA
%kit_number% 40
>>>>>>>>
	COPY_EXISTING ~KITLIST.2DA~ ~override~
		PRETTY_PRINT_2DA
		//KITLIST.2DA										   ROWNAME          LOWER            MIXED            HELP             ABILITIES        PROFICIENCY      UNUSABLE         CLASS            KITIDS
		REPLACE_TEXTUALLY ~^40[ ]+OHTYR .+$~ ~40               OHTYR            25000000         25000001         25000002         OHTYR            60               0x00004000       3~//             0x00004028
		REPLACE ~25000000~ @25000000
		REPLACE ~25000001~ @25000001
		REPLACE ~25000002~ @25000002
	BUT_ONLY
	COPY_EXISTING ~CLASTEXT.2DA~ ~override~
		PRETTY_PRINT_2DA
		REPLACE_TEXTUALLY ~^OHTYR .+$~ ~~
	ACTION_FOR_EACH file IN K_C_H K_C_D K_C_G K_C_E K_C_HE K_C_HL K_C_HO BEGIN
		COPY_EXISTING ~%file%.2DA~ ~override~
			PRETTY_PRINT_2DA
			COUNT_2DA_ROWS 2 cntrow
			READ_2DA_ENTRY (cntrow - 1) 0 2 "kit_number"
			SET kit_number = kit_number + 1
			PATCH_PRINT ~kit_number = %kit_number%~
			APPEND_FILE ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/KITTABLE.2DA~ EVALUATE_BUFFER
		BUT_ONLY
	END
END ELSE BEGIN //bgee
	ADD_KIT ~OHTYR~
		//CLASWEAP.2DA       SMALL_SWORD         LARGE_SWORD         BLUNT               MISSILE             BOW                 SPIKED              AXE                 SPEAR
		~OHTYR               0                   0                   1                   1                   0                   1                   0                   0~
		//WEAPPROF.2DA       LARGE_SWORD     SMALL_SWORD     BOW     SPEAR     BLUNT     SPIKED     AXE     MISSILE     BASTARDSWORD     LONGSWORD     SHORTSWORD     AXE     TWOHANDEDSWORD     KATANA     SCIMITARWAKISASHININJATO     DAGGER     WARHAMMER     CLUB     SPEAR     HALBERD     FLAILMORNINGSTAR     MACE     QUARTERSTAFF     CROSSBOW     LONGBOW     SHORTBOW     DART     SLING     2HANDED     SWORDANDSHIELD     SINGLEWEAPON     2WEAPON     EXTRA2     EXTRA3     EXTRA4     EXTRA5     EXTRA6     EXTRA7     EXTRA8     EXTRA9     EXTRA10     EXTRA11     EXTRA12     EXTRA13     EXTRA14     EXTRA15     EXTRA16     EXTRA17     EXTRA18     EXTRA19     EXTRA20
		~OHTYR               0               0               0       0         1         1          0       1           0                0             0              0       0                  0          0                            0          1             1        0         0           1                    1        1                0            0           0            0        1         1           1                  1                1           0          0          0          0          0          0          0          0          0           0           0           0           0           0           0           0           0           0           0~
		//ABCLASRQ.2DA       MIN_STR             MIN_DEX             MIN_CON             MIN_INT             MIN_WIS             MIN_CHR
		~OHTYR               0                   0                   0                   0                   9                   0~
		//ABCLSMOD.2DA       MOD_STR             MOD_DEX             MOD_CON             MOD_INT             MOD_WIS             MOD_CHR
		~OHTYR               0                   0                   0                   0                   0                   0~
		//ABDCDSRQ.2DA    MIN_STR          MIN_DEX          MIN_CON          MIN_INT          MIN_WIS          MIN_CHR
		~OHTYR            0                0                0                0                17               0~
		//ABDCSCRQ.2DA    MIN_STR          MIN_DEX          MIN_CON          MIN_INT          MIN_WIS          MIN_CHR
		~OHTYR            0                0                0                0                15               0~
		//ALIGNMNT.2DA       L_G                 L_N                 L_E                 N_G                 N_N                 N_E                 C_G                 C_N                 C_E
		~OHTYR               1                   1                   0                   1                   0                   0                   0                   0                   0~
		//DUALCLAS.2DA       FIGHTER             CLERIC              MAGE                THIEF               DRUID               RANGER
		~OHTYR               1                   0                   1                   1                   0                   1~
		//path to CLAB-type file
		~EET_Tweaks/%COMPONENT_NUMBER%/OHTYR.2DA~
		//KITTABLE.2DA       HUMAN               DWARF               GNOME               ELF                 HALF_ELF            HALFLING            HALFORC
		~                    K_C_H               K_C_D               K_C_G               K_C_E               K_C_HE              K_C_HL              K_C_HO~
		//KITLIST.2DA    UNUSABLE         CLASS
		~                0x00004000       3~
		//LUABBR.2DA     ABBREV
		~                Cl0~
		//25STWEAP.2DA   ARMOR     SHIELD     HELM     BAG     RING1     RING2     CLOAK     BOOTS     AMULET     BRACERS     BELT     AMMO1     AMMO2     AMMO3     MISC1     MISC2     MISC3     MISC4     MISC5     WEAPON1
		~                CHAN09    *          HELM07   BAG20   RING06    RING31    *         BOOT01    AMUL19     BRAC16      BELT06   AROW11,40 BULL03,40 BOLT06,40 POTN52,5  POTN04,2  POTN14,5  HAMM07    SW1H27    STAF08~
		//KITLIST.2DA    LOWER
		SAY              @25000000
		//KITLIST.2DA    MIXED
		SAY              @25000001
		//KITLIST.2DA    HELP
		SAY              @25000002
END

INCLUDE ~EET_Tweaks/lib/fl#add_kit_ee.tpa~
LAF ~fl#add_kit_ee~
	INT_VAR
	//CLASTEXT.2DA       BIOGRAPHY
		biography     = ~kit_biography~
	//CLASTEXT.2DA       BRIEFDESC
		briefdesc     = ~kit_briefdesc~
	//CLASTEXT.2DA       FALLEN
		fallen        = ~0~
	//CLASTEXT.2DA       FALLEN_NOTICE
		fallen_notice = ~-1~
	STR_VAR
	//internal kit name
		kit_name      = ~OHTYR~
	//BACKSTAB.2DA       0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
		//backstab    = ~~
	//CLSWPBON.2DA       GETS_PROF_APR       UNARMED_DIVISOR     ZERO_SKILL_THAC0
		clswpbon      = ~0                   0                   3~
	//NUMWSLOT.2DA       SLOTS
		numwslot      = ~2~
	//THIEFSKL.2DA       START_POINTS       LEVEL_POINTS
		//thiefskl    = ~~
	//TRAPLIMT.2DA       LIMIT
		//traplimt    = ~~
	//CLASCOLR.2DA       METAL     MINOR_CLOTH     MAIN_CLOTH     LEATHER     ARMOR
		clascolr      = ~105       118             125            98          93~
	//CLASISKL.2DA       PICK_POCKETS     OPEN_LOCKS     FIND_TRAPS     MOVE_SILENTLY     HIDE_IN_SHADOWS     DETECT_ILLUSION     SET_TRAPS
		clasiskl      = ~0                0              0              0                 0                   0                   0~
	//THIEFSCL.2DA       PICK_POCKETS     OPEN_LOCKS     FIND_TRAPS     MOVE_SILENTLY     HIDE_IN_SHADOWS     DETECT_ILLUSION     SET_TRAPS     STEALTH)
		thiefscl      = ~0                0              0              0                 0                   0                   0             0~
	//HPCLASS.2DA        TABLE
		hpclass       = ~HPPRS~
	//CLSRCREQ.2DA       HUMAN               ELF                 HALF_ELF            DWARF               HALFLING            GNOME               HALFORC
		clsrcreq      = ~1                   1                   1                   1                   1                   1                   1~
	//CLASTHAC.2DA       BONUS
		clasthac      = ~0~
	//SNEAKATT.2DA       0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
		//sneakatt    = ~~
	//CRIPPSTR.2DA       0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
		//crippstr    = ~~
END

ACTION_FOR_EACH file IN CLASWEAP WEAPPROF ABCLASRQ ABCLSMOD ABDCDSRQ ABDCSCRQ ALIGNMNT DUALCLAS KITTABLE KITLIST LUABBR 25STWEAP CLASTEXT BACKSTAB CLSWPBON NUMWSLOT THIEFSKL TRAPLIMT CLASCOLR CLASISKL THIEFSCL HPCLASS CLSRCREQ CLASTHAC SNEAKATT CRIPPSTR K_C_H K_C_D K_C_G K_C_E K_C_HE K_C_HL K_C_HO BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2DA~ BEGIN
		COPY_EXISTING ~%file%.2DA~ ~override~
			PRETTY_PRINT_2DA
		BUT_ONLY
	END
END

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/ohtyr1.spl~ ~override~
	SAY 0x8 @25000003
	SAY 0x50 @25000004
	SET strref = RESOLVE_STR_REF (@25000005)
	WRITE_LONG 0x9e strref
	
COPY ~EET_Tweaks/%COMPONENT_NUMBER%/ohtyr2.spl~ ~override~
	SAY 0x8 @25000006
	SAY 0xc @25000006
	SAY 0x50 @25000007
	SAY 0x54 @25000007

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/OHTYRHS.spl~ ~override~

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/OHTYRHS.itm~ ~override~
	SAY 0xc @25000008
	SAY 0x54 @25000009

ACTION_FOR_EACH ext IN BAM VVC WAV BEGIN
	ACTION_BASH_FOR ~EET_Tweaks/%COMPONENT_NUMBER%~ ~.*\.%ext%$~ BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~ BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override~
		END
	END
END

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/OHTYR.2DA~ ~override~

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add Priest of Tempus kit for Priests             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Add Priest of Tempus kit for Priests~
//DESIGNATED 26
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ ~This component is not compatible with your game.~

LOAD_TRA ~EET_Tweaks/lang/%LANGUAGE%/%COMPONENT_NUMBER%.tra~

ACTION_IF GAME_IS ~bgee~ BEGIN
	OUTER_SET kit_biography = 15884
	OUTER_SET kit_briefdesc = 31255
END ELSE ACTION_IF GAME_IS ~bg2ee~ BEGIN
	OUTER_SET kit_biography = ~-1~
	OUTER_SET kit_briefdesc = 102495
END ELSE BEGIN //eet
	OUTER_SET kit_biography = 15884
	OUTER_SET kit_briefdesc = 102495
END

ADD_KIT ~OHTEMPUS~
	//CLASWEAP.2DA       SMALL_SWORD         LARGE_SWORD         BLUNT               MISSILE             BOW                 SPIKED              AXE                 SPEAR
	~OHTEMPUS            0                   0                   1                   1                   0                   1                   0                   0~
	//WEAPPROF.2DA       LARGE_SWORD     SMALL_SWORD     BOW     SPEAR     BLUNT     SPIKED     AXE     MISSILE     BASTARDSWORD     LONGSWORD     SHORTSWORD     AXE     TWOHANDEDSWORD     KATANA     SCIMITARWAKISASHININJATO     DAGGER     WARHAMMER     CLUB     SPEAR     HALBERD     FLAILMORNINGSTAR     MACE     QUARTERSTAFF     CROSSBOW     LONGBOW     SHORTBOW     DART     SLING     2HANDED     SWORDANDSHIELD     SINGLEWEAPON     2WEAPON     EXTRA2     EXTRA3     EXTRA4     EXTRA5     EXTRA6     EXTRA7     EXTRA8     EXTRA9     EXTRA10     EXTRA11     EXTRA12     EXTRA13     EXTRA14     EXTRA15     EXTRA16     EXTRA17     EXTRA18     EXTRA19     EXTRA20
	~OHTEMPUS            0               0               0       0         1         1          0       1           0                0             0              0       0                  0          0                            0          1             1        0         0           1                    1        1                0            0           0            0        1         1           1                  1                1           0          0          0          0          0          0          0          0          0           0           0           0           0           0           0           0           0           0           0~
	//ABCLASRQ.2DA       MIN_STR             MIN_DEX             MIN_CON             MIN_INT             MIN_WIS             MIN_CHR
	~OHTEMPUS            0                   0                   0                   0                   9                   0~
	//ABCLSMOD.2DA       MOD_STR             MOD_DEX             MOD_CON             MOD_INT             MOD_WIS             MOD_CHR
	~OHTEMPUS            0                   0                   0                   0                   0                   0~
	//ABDCDSRQ.2DA    MIN_STR          MIN_DEX          MIN_CON          MIN_INT          MIN_WIS          MIN_CHR
	~OHTEMPUS         0                0                0                0                17               0~
	//ABDCSCRQ.2DA    MIN_STR          MIN_DEX          MIN_CON          MIN_INT          MIN_WIS          MIN_CHR
	~OHTEMPUS         0                0                0                0                15               0~
	//ALIGNMNT.2DA       L_G                 L_N                 L_E                 N_G                 N_N                 N_E                 C_G                 C_N                 C_E
	~OHTEMPUS            0                   0                   0                   0                   1                   0                   1                   1                   1~
	//DUALCLAS.2DA       FIGHTER             CLERIC              MAGE                THIEF               DRUID               RANGER
	~OHTEMPUS            1                   0                   1                   1                   0                   1~
	//path to CLAB-type file
	~EET_Tweaks/%COMPONENT_NUMBER%/OHTEMPUS.2DA~
	//KITTABLE.2DA       HUMAN               DWARF               GNOME               ELF                 HALF_ELF            HALFLING            HALFORC
	~K_C_H               K_C_D               K_C_G               K_C_E               K_C_HE              K_C_HL              K_C_HO~
	//KITLIST.2DA    UNUSABLE         CLASS
	~                0x00004000       3~
	//LUABBR.2DA     ABBREV
	~                Cl0~
	//25STWEAP.2DA   ARMOR     SHIELD     HELM     BAG     RING1     RING2     CLOAK     BOOTS     AMULET     BRACERS     BELT     AMMO1     AMMO2     AMMO3     MISC1     MISC2     MISC3     MISC4     MISC5     WEAPON1
	~                CHAN09    *          HELM07   BAG20   RING06    RING31    *         BOOT01    AMUL19     BRAC16      BELT06   AROW11,40 BULL03,40 BOLT06,40 POTN52,5  POTN04,2  POTN14,5  HAMM07    SW1H27    STAF08~
	//KITLIST.2DA    LOWER
	SAY              @26000000
	//KITLIST.2DA    MIXED
	SAY              @26000001
	//KITLIST.2DA    HELP
	SAY              @26000002

INCLUDE ~EET_Tweaks/lib/fl#add_kit_ee.tpa~
LAF ~fl#add_kit_ee~
	INT_VAR
	//CLASTEXT.2DA       BIOGRAPHY
		biography     = ~kit_biography~
	//CLASTEXT.2DA       BRIEFDESC
		briefdesc     = ~kit_briefdesc~
	//CLASTEXT.2DA       FALLEN
		fallen        = ~0~
	//CLASTEXT.2DA       FALLEN_NOTICE
		fallen_notice = ~-1~
	STR_VAR
	//internal kit name
		kit_name      = ~OHTEMPUS~
	//BACKSTAB.2DA       0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
		//backstab    = ~~
	//CLSWPBON.2DA       GETS_PROF_APR       UNARMED_DIVISOR     ZERO_SKILL_THAC0
		clswpbon      = ~0                   0                   3~
	//NUMWSLOT.2DA       SLOTS
		numwslot      = ~2~
	//THIEFSKL.2DA       START_POINTS       LEVEL_POINTS
		//thiefskl    = ~~
	//TRAPLIMT.2DA       LIMIT
		//traplimt    = ~~
	//CLASCOLR.2DA       METAL     MINOR_CLOTH     MAIN_CLOTH     LEATHER     ARMOR
		clascolr      = ~80        126             127            93          97~
	//CLASISKL.2DA       PICK_POCKETS     OPEN_LOCKS     FIND_TRAPS     MOVE_SILENTLY     HIDE_IN_SHADOWS     DETECT_ILLUSION     SET_TRAPS
		clasiskl      = ~0                0              0              0                 0                   0                   0~
	//THIEFSCL.2DA       PICK_POCKETS     OPEN_LOCKS     FIND_TRAPS     MOVE_SILENTLY     HIDE_IN_SHADOWS     DETECT_ILLUSION     SET_TRAPS     STEALTH)
		thiefscl      = ~0                0              0              0                 0                   0                   0             0~
	//HPCLASS.2DA        TABLE
		hpclass       = ~HPPRS~
	//CLSRCREQ.2DA       HUMAN               ELF                 HALF_ELF            DWARF               HALFLING            GNOME               HALFORC
		clsrcreq      = ~1                   1                   1                   1                   1                   1                   1~
	//CLASTHAC.2DA       BONUS
		clasthac      = ~0~
	//SNEAKATT.2DA       0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
		//sneakatt    = ~~
	//CRIPPSTR.2DA       0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
		//crippstr    = ~~
END

ACTION_FOR_EACH file IN CLASWEAP WEAPPROF ABCLASRQ ABCLSMOD ABDCDSRQ ABDCSCRQ ALIGNMNT DUALCLAS KITTABLE KITLIST LUABBR 25STWEAP CLASTEXT BACKSTAB CLSWPBON NUMWSLOT THIEFSKL TRAPLIMT CLASCOLR CLASISKL THIEFSCL HPCLASS CLSRCREQ CLASTHAC SNEAKATT CRIPPSTR K_C_H K_C_D K_C_G K_C_E K_C_HE K_C_HL K_C_HO BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ BEGIN
		COPY_EXISTING ~%file%.2da~ ~override~
			PRETTY_PRINT_2DA
		BUT_ONLY
	END
END

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/ohtmps1.spl~ ~override~
	SAY 0x8 @26000003
	SAY 0x50 @26000004

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/ohtmps2.spl~ ~override~
	SAY 0x8 @26000005
	SAY 0x50 @26000006

ADD_PROJECTILE ~EET_Tweaks/%COMPONENT_NUMBER%/ohvrpo.PRO~
COPY ~EET_Tweaks/%COMPONENT_NUMBER%/ohtmps2d.spl~ ~override~
	PATCH_FOR_EACH offset IN 0x1ce 0x4ce 0x7ce 0xace 0xdce BEGIN
		SAY offset @26000007
	END
	PATCH_FOR_EACH offset IN 0x98 0xc0 0xe8 0x110 0x138 BEGIN
		WRITE_SHORT offset ohvrpo
	END

ADD_PROJECTILE ~EET_Tweaks/%COMPONENT_NUMBER%/ohvrnp.PRO~
COPY ~EET_Tweaks/%COMPONENT_NUMBER%/ohtmps2e.spl~ ~override~
	PATCH_FOR_EACH offset IN 0x1ce 0x4ce 0x7ce 0xace 0xdce BEGIN
		SAY offset @26000008
	END
	PATCH_FOR_EACH offset IN 0x98 0xc0 0xe8 0x110 0x138 BEGIN
		WRITE_SHORT offset ohvrnp
	END

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/OHTMPSHS.spl~ ~override~

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/OHTMPSHS.itm~ ~override~
	SAY 0xc @25000009
	SAY 0x54 @25000010

ACTION_FOR_EACH ext IN BAM VVC WAV BEGIN
	ACTION_BASH_FOR ~EET_Tweaks/%COMPONENT_NUMBER%~ ~.*\.%ext%$~ BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~ BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override~
		END
	END
END

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/OHTEMPUS.2DA~ ~override~

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add racial enemies from IWD                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Add racial enemies from IWD~
//DESIGNATED 27
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ ~This component is not compatible with your game.~

LOAD_TRA ~EET_Tweaks/lang/%LANGUAGE%/%COMPONENT_NUMBER%.tra~

ACTION_DEFINE_ASSOCIATIVE_ARRAY "table_EETT_strrefIDS" BEGIN
	~CADAVEROUS_UNDEAD~ , ~27000000~ , ~27000001~ => ~108~
	~GIANTS~ , ~27000002~ , ~27000003~ => ~142~
	~GOBLINS~ , ~27000004~ , ~27000005~ => ~161~
	~LIZARD_MEN~ , ~27000006~ , ~27000007~ => ~162~
	~ORCS~ , ~27000008~ , ~27000009~ => ~143~
	~SALAMANDERS~ , ~27000010~ , ~27000011~ => ~169~
	~SKELETAL_UNDEAD~ , ~27000012~ , ~27000013~ => ~115~
	~SPECTRAL_UNDEAD~ , ~27000014~ , ~27000015~ => ~175~
	~SPIDERS~ , ~27000016~ , ~27000017~ => ~116~
	~TROLLS~ , ~27000018~ , ~27000019~ => ~129~
	~UMBER_HULKS~ , ~27000020~ , ~27000021~ => ~130~
	~YUAN_TI~ , ~27000022~ , ~27000023~ => ~154~
END

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/HATERACE.2DA
%strref% %strref_1% %IDS% %strref_2%
>>>>>>>>

COPY_EXISTING ~HATERACE.2DA~ ~override~
	PRETTY_PRINT_2DA
	COUNT_2DA_ROWS 4 "cntrow"
	PHP_EACH "table_EETT_strrefIDS" AS "strref" => "IDS" BEGIN
		SET noupdate = 0
		FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
			READ_2DA_ENTRY cnt 2 4 "compareWith"
			PATCH_IF ("%IDS%" STRING_EQUAL_CASE "%compareWith%") BEGIN
				SET_2DA_ENTRY cnt 0 4 ~%strref%~
				SET_2DA_ENTRY cnt 1 4 ~%strref_1%~
				SET_2DA_ENTRY cnt 3 4 ~%strref_2%~
				SET noupdate = 1
			END
		END
		PATCH_IF (noupdate = 0) BEGIN
			APPEND_FILE ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/HATERACE.2DA~ EVALUATE_BUFFER
		END
		REPLACE ~%strref_1%~ ( AT "strref_1" )
		REPLACE ~%strref_2%~ ( AT "strref_2" )
	END
	PRETTY_PRINT_2DA
BUT_ONLY

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove junk from global scripts                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Remove junk from global scripts~
//DESIGNATED 28
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ ~This component is not compatible with your game.~

<<<<<<<< EET_Tweaks-inlined/K#DROWIT-et.baf
IF
  HasItem("%MATCH1%",%MATCH2%)
THEN
  RESPONSE #100
    TakeItemReplace("DWDUST","%MATCH1%",%MATCH2%)
END
>>>>>>>>

COPY - ~EET_Tweaks-inlined/blank.txt~ ~EET_Tweaks-inlined/blank.baf~

COMPILE ~EET_Tweaks/%COMPONENT_NUMBER%/K#HLYSYM.baf~
	~EET_Tweaks/%COMPONENT_NUMBER%/K#DROWIT.baf~
	~EET_Tweaks/%COMPONENT_NUMBER%/K#LILARC.baf~

COPY_EXISTING ~BALDUR.BCS~ ~override~
	~BALDUR25.BCS~ ~override~
	REPLACE_BCS_BLOCK ~EET_Tweaks/%COMPONENT_NUMBER%/HolySymbols.baf~ ~EET_Tweaks-inlined/blank.baf~
	REPLACE_BCS_BLOCK ~EET_Tweaks/%COMPONENT_NUMBER%/Lilarcor.baf~ ~EET_Tweaks-inlined/blank.baf~
	PATCH_IF ~%SOURCE_RES%~ STRING_EQUAL_CASE ~BALDUR~ BEGIN
		//Unused test script blocks
		REPLACE_BCS_BLOCK ~EET_Tweaks/%COMPONENT_NUMBER%/Unused.baf~ ~EET_Tweaks-inlined/blank.baf~
		//Malchior Harpells appearance
		PATCH_IF FILE_EXISTS_IN_GAME ~cdt03160.g3~ BEGIN //BG2 Tweaks "Keep Drizzt's Loot, Disable Malchor Harpell" component detected
			REPLACE_BCS_BLOCK ~EET_Tweaks/%COMPONENT_NUMBER%/compat/bg2tweaks/Harpell.baf~ ~EET_Tweaks-inlined/blank.baf~
		END ELSE BEGIN
			REPLACE_BCS_BLOCK ~EET_Tweaks/%COMPONENT_NUMBER%/Harpell.baf~ ~EET_Tweaks-inlined/blank.baf~
		END
	END ELSE BEGIN
		//Unused Familiar script blocks with wrong DV
		REPLACE_BCS_BLOCK ~EET_Tweaks/%COMPONENT_NUMBER%/Familiar.baf~ ~EET_Tweaks-inlined/blank.baf~
	END
	DECOMPILE_AND_PATCH BEGIN
		//Lilarcor (talking sword)
		COUNT_REGEXP_INSTANCES ~DisplayStringHeadOwner("sw2h14",20580)~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			PATCH_WARN ~Warning: DisplayStringHeadOwner("sw2h14",20580) still found in %SOURCE_FILESPEC% after patching. Some other mod may have changed Lilarcor script blocks. Consider uninstalling this component if you can't analyse it yourself and/or send %SOURCE_FILESPEC% and weidu.log to me.~
		END
		//Cleric's Holy Symbols
		PATCH_FOR_EACH match IN BELT14 BELT13 BELT12 BEGIN
			COUNT_REGEXP_INSTANCES ~TransformItem("CDHLYSYM","%match%")~ num_matches
			PATCH_IF (num_matches > 0) BEGIN
				PATCH_WARN ~Warning: %match% still found %num_matches% times in %SOURCE_FILESPEC% after patching. Some other mod may have changed Cleric's Holy Symbols script blocks. Consider uninstalling this component if you can't analyse it yourself and/or send %SOURCE_FILESPEC% and weidu.log to me.~
			END
		END
		PATCH_IF ~%SOURCE_RES%~ STRING_EQUAL_CASE ~BALDUR25~ BEGIN
			//Drow items changing to dust on surface
			SPRINT textToReplace ~IF[%newline%]*Global("oh_drow_items_safe","GLOBAL",0)[%newline%]*HasItem("\(.+\)",\(Player.\))[%newline%]*THEN[%newline%]*RESPONSE #100[%newline%]*TakeItemReplace("DWDUST",".+",Player.)[%newline%]*END~
			COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
			PATCH_PRINT ~Patching: %num_matches% matches wiped out from %SOURCE_FILESPEC%~
			PATCH_IF (num_matches < 84) BEGIN
				PATCH_WARN ~Warning: could not find %textToReplace% in %SOURCE_FILESPEC% at least 84 times. Some other mod may have changed these blocks. Consider uninstalling this component if you can't analyse it yourself and/or send %SOURCE_FILESPEC% and weidu.log to me.~
			END
			REPLACE_EVALUATE CASE_INSENSITIVE ~%textToReplace%~ BEGIN
				INNER_ACTION BEGIN
					EXTEND_TOP ~K#DROWIT.BCS~ ~EET_Tweaks-inlined/K#DROWIT-et.baf~ EVALUATE_BUFFER
				END
			END ~~
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.BCS$~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~SetGlobal("oh_drow_items_safe","GLOBAL",0)~ BEGIN
			PATCH_PRINT ~Drow items invisible creature spawning added to %SOURCE_FILESPEC%~
		END ~SetGlobal("oh_drow_items_safe","GLOBAL",0)
    CreateCreatureObject("K#DROWIT",Player1,0,0,0)~
		REPLACE_TEXTUALLY ~\(Allegiance(.*,\)ANYONE)~ ~\10)~//workaround for weidu compiler bug
	END
BUT_ONLY

ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdt03160.g3~ BEGIN //BG2 Tweaks "Keep Drizzt's Loot, Disable Malchor Harpell" component not detected
	EXTEND_BOTTOM ~AR0800.BCS~ ~EET_Tweaks/%COMPONENT_NUMBER%/Harpell.baf~
END

COPY_EXISTING ~CDHLYSYM.SPL~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 /*Use EFF File*/ target = 1 power = 4 parameter1 = 0 parameter2 = 2 timing = 1 resist_dispel = 2 STR_VAR resource = "K#HLYSYM" END
BUT_ONLY

COPY_EXISTING ~SW2H14.ITM~ ~override~
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 272 /*Use EFF file on condition*/ target = 1 parameter1 = 6 parameter2 = 3 timing = 2 STR_VAR resource = "K#LILARC" END
BUT_ONLY

COPY ~EET_Tweaks/%COMPONENT_NUMBER%/K#HLYSYM.CRE~ ~override~
	~EET_Tweaks/%COMPONENT_NUMBER%/K#HLYSYM.EFF~ ~override~
	~EET_Tweaks/%COMPONENT_NUMBER%/K#DROWIT.CRE~ ~override~
	~EET_Tweaks/%COMPONENT_NUMBER%/K#DROWIT.EFF~ ~override~
	~EET_Tweaks/%COMPONENT_NUMBER%/K#DROWIT.SPL~ ~override~
	~EET_Tweaks/%COMPONENT_NUMBER%/K#LILARC.CRE~ ~override~
	~EET_Tweaks/%COMPONENT_NUMBER%/K#LILARC.EFF~ ~override~

//Leftover BG:EE ARE scripts code
ACTION_FOR_EACH file IN AR0100 AR0101 AR0103 AR0106 AR0108 AR0111 AR0112 AR0114 AR0115 AR0119 AR0121 AR0123 AR0125 AR0128 AR0130 AR0137 AR0138 AR0145 AR0146 AR0149 AR0153 AR0608 AR0612 AR1803 AR2301 AR2609 AR2611 AR2626 AR3300 AR3304 AR3352 AR5400 AR5405 BEGIN
	ACTION_IF NOT FILE_EXISTS_IN_GAME ~%file%.ARE~ BEGIN //just to be sure that other mod didn't add these areas
		COPY - ~EET_Tweaks-inlined/blank.txt~ ~EET_Tweaks-inlined/%file%.baf~
		COMPILE ~EET_Tweaks-inlined/%file%.baf~
	END
END

//Leftover BG:EE ARE scripts code used in scripts that are also used in BG2:EE
ACTION_FOR_EACH file IN AR0514 AR2001 BEGIN
	COPY_EXISTING ~%file%.BCS~ ~override~
		REPLACE_BCS_BLOCK ~EET_Tweaks/%COMPONENT_NUMBER%/%file%.baf~ ~EET_Tweaks-inlined/blank.baf~
	BUT_ONLY
END

LAM ~EET_Tweaks.log~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Restore TotSC textscreens                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Restore TotSC textscreens~
//DESIGNATED 29
REQUIRE_PREDICATE GAME_IS ~bgee eet~ ~This component is not compatible with your game.~

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/AR1000-et.baf
IF
  Global("MissionPackSave","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("MissionPackSave","GLOBAL",1)
    TextScreen("toscst")
	Continue()
END
>>>>>>>>

COPY_EXISTING ~BALDUR.BCS~ ~override~
	REPLACE_TEXTUALLY ~\(SetGlobal("FinishedExpansion","GLOBAL",1)\)~ ~\1
    TextScreen("toscend")~
BUT_ONLY

ACTION_IF GAME_IS ~bgee~ BEGIN
	EXTEND_TOP ~AR1000.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/AR1000-et.baf~
	COPY_EXISTING ~TOSCST.2DA~ ~override~
		REPLACE_TEXTUALLY ~\(REPUTATION[	 ]+-1[	 ]+\)\([0-9]+\)~ ~\1 24105~
		PRETTY_PRINT_2DA
	BUT_ONLY
END ELSE BEGIN
	EXTEND_TOP ~BG1000.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/AR1000-et.baf~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Components below are not finished                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Ironman Mode                                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~Ironman Mode~
//DESIGNATED 100
REQUIRE_PREDICATE GAME_IS ~bg2ee iwdee eet~ ~This component is not compatible with your game.~
DEPRECATED ~This component is currently broken:
http://forum.baldursgate.com/discussion/40487/opcode-338-disable-rest-or-save-seems-to-be-partially-broken~

LOAD_TRA ~EET_Tweaks/lang/%LANGUAGE%/%COMPONENT_NUMBER%.tra~

<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/dplayer3-et.baf
IF
  HotKey(Q)
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    SaveGame(1)
    GoToStartScreen()
END

/*IF
  HotKey(Z)
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    RestParty()
END*/
>>>>>>>>
EXTEND_TOP ~DPLAYER3.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/dplayer3-et.baf~

//alternative for spell - area flag patching
/*COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
	READ_BYTE 0x14 "flags"
	PATCH_IF ("%flags%" BAND 0b00000001) BEGIN
		SPRINT are_sprint ~%are_sprint%%WNL%  !AreaCheck("%SOURCE_RES%")~
		SET are_set = 1
	END
BUT_ONLY

ACTION_IF (%are_set% = 1) BEGIN
	PRINT ~Areas with 'no sleep' flag:%WNL%%are_sprint%~
	COPY - ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/dplayer3-et.baf~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/dplayer3-et.baf~
		REPLACE_TEXTUALLY ~\(!Detect(NearestEnemyOf(Myself))\)~ ~\1%are_sprint%~
END

//add 'no save' flag to all ARE files
COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
	READ_BYTE 0x14 "flags"
	WRITE_BYTE 0x14 ("%flags%" BOR 0b00000001)
BUT_ONLY

//remove 'no sleep' flag for shops
COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(StartStore.*\)~ BEGIN
			PATCH_PRINT ~Patching %SOURCE_FILE%: %MATCH1%~
		END
		~RemoveAreaFlag(NOSAVE)%WNL%%MATCH1%~
	END
BUT_ONLY
*/

//copy Ironman Mode spell
COPY ~EET_Tweaks/%COMPONENT_NUMBER%/K#IRONM.SPL~ ~override~
	SAY 0x9e @0 //In 'Ironman Mode' you can save your progress by pressing 'Q' key.

//update global scripts
ACTION_FOR_EACH script IN BALDUR BALDUR25 BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%script%.BCS~ BEGIN
		ACTION_IF NOT MOD_IS_INSTALLED ~EET_Tweaks/EET_Tweaks.tp2~ ~14~ BEGIN
<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/baldur-et.baf
IF
  Global("K#IRONMAN","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("K#IRONMAN","GLOBAL",1)
	ActionOverride(Player1,ApplySpellRES("K#IRONM",Myself))
    Continue()
END

IF
  GlobalGT("K#IRONMAN","GLOBAL",1)
THEN
  RESPONSE #100
	ActionOverride(Player1,RemoveSpellRES("K#IRONM"))
    Continue()
END

IF
  HPLT(Player1,1)
THEN
  RESPONSE #100
    SaveGame(1)
    StartMovie("DEATHAND")
    GoToStartScreen()
END
>>>>>>>>
			EXTEND_TOP ~%script%.BCS~ ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/baldur-et.baf~
		END ELSE BEGIN
			COPY ~%script%.BCS~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY ~\(NumInPartyAlive(0)[%newline%]+THEN[%newline%]+RESPONSE #100\)~ ~\1
    SaveGame(0)~
				END
			BUT_ONLY
		END
	END
END

//update ini file
ACTION_IF (FILE_EXISTS ~%USER_DIRECTORY%/Baldur.ini~) BEGIN
	COPY ~%USER_DIRECTORY%/Baldur.ini~ ~%USER_DIRECTORY%~
		COUNT_REGEXP_INSTANCES ~'Difficulty Level'~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_EVALUATE CASE_INSENSITIVE ~\('Difficulty Level',[ 	]+\)'\([0-9]+\)'~ BEGIN
				SET "value" = "%MATCH2%"
				PATCH_IF ("%value%" < 3) BEGIN
					SET value = 3
				END
			END
			~%MATCH1%'%value%'~
		END ELSE BEGIN
			REPLACE_TEXTUALLY ~INSERT INTO options ROWS (~ ~INSERT INTO options ROWS (
		'Game Options',	'Difficulty Level',	'3',~
		END
		COUNT_REGEXP_INSTANCES ~'Maximum HP'~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~\('Maximum HP',[ 	]+\)'[0-9]+'~ ~\1'0'~
		END ELSE BEGIN
			REPLACE_TEXTUALLY ~INSERT INTO options ROWS (~ ~INSERT INTO options ROWS (
		'Game Options',	'Maximum HP',	'0',~
		END
	BUT_ONLY
END ELSE BEGIN
<<<<<<<< EET_Tweaks-inlined/%COMPONENT_NUMBER%/Baldur.ini
CREATE TABLE options (
	section string,
	name string,
	value string
);
INSERT INTO options ROWS (
	'Game Options',	'Difficulty Level',	'3',
	'Game Options',	'Maximum HP',	'0'
);
>>>>>>>>
	COPY ~EET_Tweaks-inlined/%COMPONENT_NUMBER%/Baldur.ini~ ~%USER_DIRECTORY%/Baldur.ini~
END

LAM ~EET_Tweaks.log~
